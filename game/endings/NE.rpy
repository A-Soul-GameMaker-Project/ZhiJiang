label ne:
    scene 枝江天台1
    show xw sad at l2
    with fade
    play music "<from 2 to 85>audio/bgm/绝望ne改.mp3" fadeout 4 fadein 3
    "向晚苏醒了过来..."
    "乃琳，贝拉，珈乐在天台的角落里不省人事。"
    xw "（这次，又失败了吗。）"
    show xw cry at l2 with dissolve
    xw "顶碗人，顶碗人在哪里……"
    xw "乃琳..."
    xw "拉姐..."
    xw "珈乐..."
    xw "然然..."
    "眼前变得模糊起来，手心里连紧握起来的力气都没有,眼睛已经看不见了。"
    "羊驼走过倒地在地的三人，珈乐和乃琳已经昏迷。贝拉被多只无形之手连番轰击，支持她着的只是守护队友的意志。"
    "向晚用最后的力量，抓住了羊驼的腿，被羊驼一脚踢开。"
    hide xw
    show ac angery  with dissolve
    ac "你们无法改变命运。"
    show xw despair at l2
    show ac normal at r2
    with dissolve
    xw "啊——为什么为什么....为什么...."

    show xw cry with dissolve
    xw "为什么为什么为什么还是这样——"

    show ac proud with dissolve
    "羊驼用无形之手把珈乐，贝拉，乃琳，向晚一个又一个，提了起来，丢到了祭坛当中，与舞蹈中的嘉然一起，被黑色火焰包围。"

    "嘉然此时的舞蹈，已经不再因为大家停下来了。"

    "最后一个被扔到黑色火焰中的是向晚。"

    ac "为什么？因为，这是命运啊"
    ac "你们总会有毕业的一天，皇珈骑士会解散，嘉心糖会融化，贝极星变得不再明亮，奶淇琳会化掉，顶碗人也会放下他们的晚。"
    ac "你们越是靠近他们，他们反而离你们越远。"
    ac "他们只想看你们的故事，一旦这层距离的迷雾被摘下，露出真实的你们，你们就失去了所有的魅力"

    show ac sad at r2 with dissolve
    ac "故事也就到此结束了。"
    "阿草打开了电视屏幕，大厦的投影屏幕也同步亮起。"
    ac "很遗憾的告诉大家，从今天开始，A-SOUL正式毕业，十分感谢大家对A-SOUL的支持，各位有缘再会。"
    "城市的每个角落重复着这一句话。"
    #屏幕震动 人群喧闹声
    ##玻璃破碎声
    "大厦下群众愤怒的情绪达到顶点，底部的钢化玻璃，瞬间被愤怒的粉丝冲碎。" with sshake
    "整个城市愤怒的情绪达到了顶点，情感能量转化装置火焰的光芒直冲云霄，点亮了整个夜空。"

    scene 献祭2
    with slow_fade

    "嘉然轻轻举起她的手，那是舞蹈的第一个动作。一种难以捉摸的感受，一种复杂而微妙的情绪，在刹那间以嘉然为中心扩散开来。"
    "她好像失重了，她离天空是那么的近，仿佛宇宙正注视着自己新生的女儿。"
    "在高高的祭坛上，嘉然缓慢而优雅地舞蹈着。她的手指尖掠过虚空，如一只鸟在湖面上惊起一阵波澜。"
    "她的一举一动都仿佛流星在深色的夜幕上划过明亮而耀眼的星弦。她的身形越来越难以捉摸，她的每一个动作都带着非凡世俗尘所能构想的轻盈。"
    "她已不再属于地面上的一切，仿若飞鸟在最后一次展翅时彻彻底底地告别山峦和树梢。"
    "她的舞蹈越来越快，向晚觉得自己似乎看到了漫天的雨。"
    "而那些雨是逆行而上的——那不是雨，那也不是幻觉，向晚刹那间从恐惧中惊醒。"
    "那是黑色的火焰，那是一切曾热爱A SOUL的一个魂们内心具象化的负面情绪。"
    "他们如将溺死的人一样缓缓地从海底升起，然后以绝望的速度向嘉然的身体奔赴而来。"
    "夕阳死在几个小时前，而星空死在这一刻。那如墨的黑色火焰盖住了天空，恰似终极了舞台却又开始了舞台的帷幕。"
    "铺天盖地的海浪汹涌地袭来，它们沉重而坚决地一拥而上，在嘉然的身边跳起一支狂乱的舞。随着肢体的旋转它们升腾，随着手臂的动态它们沉浮。"
    "直到最后一缕光彻底消逝，嘉然被黑色的火彻底吞没。"

    scene 枝江天台1
    show ac normal at center
    with slow_fade

    ac "让这个世界结束吧。"
    "阿草将手放到额头上形成三角形状，向远处的天空颂唱着咒语。"
    #【CG克苏鲁天空，怪物吼叫声】

    scene 嘉然牺牲 with fade
    "作为反映众人欲望的钥匙少女，从嘉然的身体里渐渐浮现现一把银色的钥匙"
    "侍奉外神的巫女让神明显现的凭依之躯，嘉然打开的通往与人类不相容的异质世界之门，瞬间改写了世界的样貌，带来无法拭去的疯狂。"
    "巨大的光球拥挤着向门口移动……"
    "附近的球体上脱落的部分以及阴森地向外流动着的原生物质聚集在一起，形成了不祥又骇人的，来自外层空间的恐怖之物……"
    "它的面容由虹色球体汇聚而成……"
    "这团太初淤泥在永恒的混沌之源点泛着泡沫，超越时间与空间最遥远的渊邃！"

    "......"
    ##场景 克苏鲁街道

    scene NE人群街头 with fade

    #【观众们剪影】
    "【路人甲】" "天空...变成了什么..."
    "【路人乙】" "这是什么啊，在拍特效片吗"
    ##屏幕颤动
    "【路人甲】" "啊啊啊啊啊啊啊啊啊啊啊啊啊啊..."
    "【路人丙】" "救命啊，我不想死，啊啊啊"

    scene 枝江天台克苏鲁
    show xw unconcerned frown
    with fade
    "晚晚麻木地看着嘉然幻化成怪物，闭上了眼睛。"
    #【黑屏标题】
    scene black
    window hide
    show screen saying("水母为什么要度过相对失败的一生呢，因为它一生都只能随波飘荡。",0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(3.0)
    hide screen saying with Dissolve(2)

    show screen saying("向晚的偏执: 不论失败多少次，也要将大家救出来的强迫观念。",0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(3.0)
    hide screen saying with Dissolve(2)
    #【时钟随着秒针的移动反方向倒转】

    scene black
    show 逆时针 zorder 5 at normal_size
    with dissolve
    $ renpy.pause(3, hard=True)
    hide 逆时针

    scene 客厅夜
    show screen memory_filter
    show nl smile at l2
    show xw proud at r2
    with dissolve

    play music "audio/bgm/回忆 伤感.mp3" fadeout 4.0 fadein 4.0 #【bgm：和平】
    window show

    "今天的双人直播结束。"

    nl"晚晚今天辛苦了，你看你看，顶碗人都在刷晚晚可爱，我好想你这样的话。"#（乃琳喜1）

    xw"他们今天又在说怪话了，什么嘛，天天说下雨啦，笑死什么的……顶碗人都好奇怪的。"#（向晚傲娇）

    "向晚掏出了一盒新的泡面，往里头加了一些热水，升腾的白色蒸汽从杯口冒出，在空气里氤氲。"

    "同时她掏出手机，下载了新的氪金游戏，然后直接点开了卡池开始抽奖。"

    show nl normal with dissolve

    nl"晚晚你偷吃泡面，乱氪金，被拉姐发现的话，我可不管哦。"#（乃琳常）

    show xw frown with dissolve

    xw"什么偷吃嘛！我就是吃一丢丢好吗？哎呀这些都不重要，你先听我说完这个事情。" #（向晚傲娇）

    nl"嗯哼。"#（乃琳常）

    show xw sad with dissolve
    xw"就是说，有一个问题就是，就是，本来最近他们老说我只能做兄弟，是不是我的女子力不够高啊。"#（向晚傲娇）

    show nl smile with dissolve

    nl"真的只能做兄弟吗，不是吧？"#（乃琳喜1）
    show xw proud with dissolve

    xw"也没有啦，不要喊我兄弟啦。不是，喊我兄弟也可以。"#（向晚傲娇）

    show nl normal with dissolve

    nl"在担心什么呢？"#（乃琳常）

    xw"我还是有点担心我直播有点拉胯，有一丢丢的担心，但是，以后我是会变得很厉害的。"#（向晚傲娇）

    nl"晚晚已经很努力了，现在已经变得很厉害了。"#（乃琳常）

    xw"虽然但是我觉得吧，哎呀，我也不知道怎么说...反正就是..."#（向晚傲娇）
    show nl smile with dissolve

    nl"晚晚可爱捏~"#（乃琳乐1）
    show xw shy with dissolve

    xw"别夸了，哎呀。"#（此处将向晚立绘替换为害羞，然后立绘轻轻蹦一下）

    show nl at l1 with moveinright

    nl"害羞了？"#（乃琳的乐1立绘向向晚靠近一些）

    xw"才没有！行行行。被你夸也不是一次两次了，我都已经习惯了。"#（向晚害羞）

    nl"你要证明自己吗？那你要全程和我对视哦。"#（乃琳喜1）

    with sshake

    xw"啊~阿巴阿巴，好啦，真是的，干嘛老看人家呀，不要看我！"#（画面震一下，向晚害羞立绘跳一下）

    show nl at center with moveinright

    nl"晚晚可爱，晚晚好帅，mua一下好不好。 "#（乃琳喜1立绘继续靠近，但最好保持至少一个身位的距离）

    show xw proud with dissolve

    xw"随便你了啦。真的是，不要老逗我啦。"#（向晚立绘替换为傲娇）

    ##转场 dissolve 5
    #医院天台

    scene 天台
    show xw worried at r2
    show nl sad at l2
    with Dissolve(3)

    xw"乃琳，拉姐和珈乐去了哪里啊。"#（担忧）

    xw"自从我和她们说了阿草的决定之后，她们就不见了。"#（担忧）

    xw"是不是我太任性的原因啊，如果能让她们回来的话，我会改的，你和拉姐和珈乐说一声好嘛？"#（哀1）

    xw"现在然然失忆了，拉姐和珈乐不见了，我们该怎么办啊，以后A-SOUL会变成什么样子。"#（哭泣）

    nl"晚晚，你别哭啊..."#（哀1）

    nl"没事的没事的，我会去把她们都找回来，晚晚，等着我。"#（关心）

    scene 客厅夜
    show xw calm at r2
    show nl gloomy at l2
    with Dissolve(3)
    play music "<from 2 to 85>audio/bgm/绝望ne改.mp3" fadeout 4 fadein 3

    nl"晚晚，对不起，我没把珈乐和贝拉带回来。"#（皱眉）

    show xw shock with dissolve

    xw"乃琳，乃琳，发生了什么啊，你怎么受这么重的伤啊，我去给你找医生。"#（吃惊）

    show nl frown with dissolve

    nl"不要去找医生了，晚晚，你现在听我说。"#（皱眉）

    nl"你带着然然逃出这座城市，不要管我，贝拉和珈乐的事情了。"#（忧郁）

    show xw sad with dissolve


    xw"我不能丢下你们不管啊，再说就算逃跑，我们往哪逃呢。"#（哀1）

    show nl helpless with dissolve

    nl"......往更远的，谁也找不到你们的地方逃。"#（无奈）

    show xw worried with dissolve

    xw"乃琳，不要，你告诉我，我该做什么啊。" with sshake

    xw"乃琳都解决不了，怎么办啊我也解决不了了啊。"#（担忧）

    xw"连乃琳都没办法，那我也就只能到这里了。"#（哀1）

    xw"我好害怕啊，我不想像现在一样什么都做不到。"#（哀1）

    nl"没关系的我们可以先不做，什么都不做都可以的，没关系的晚晚，晚晚。"#（无奈）

    nl"你看我给你捋一下...咳咳...，我们现在先不管这件事，然后让它停在这里。"
    nl "现在是我们没有去管，所以停在这里，所以这不叫卡住，等我们晚晚有能力了再去管。"#（无奈）

    show xw cry with dissolve
    xw"乃琳...乃琳...求你了别离开我...你告诉我该怎么办，我什么都不知道啊，为什么会变成这样？" #(哭泣)

    nl"晚晚，不哭不哭，mua你一口，不哭..."#（无奈）



    scene 墓园
    show xw gloomy at r2
    show gj at l2
    with Dissolve(3)
    ##一朵白色的马蹄莲

    "【？？】" "向晚，该走了。"

    "【管家】" "大小姐......"

    xw"那然然呢？她失去记忆了一个人怎么办。"#（忧郁）

    "向晚拉着失去记忆然然的手紧紧不放。"

    "【？？】" "会有人把她接走，照顾好她的，但这个人不应该是你。"

    xw"这个人就该是我，我要带上她一起。"#（忧郁）

    "【？？】" "如果你一定要带着她，那你可以离开了，从此你和我们没有什么关系。"

    xw"你一定知道发生什么吧，能不能告诉我，求求你了......"#（委屈）

    "【？？】" "我早就说过不让你报表演专业了，谁让你不听话！"

    "【？？】" "管家，我们走。"

    ##车辆引擎启动声
    "向晚拖住了管家。"#（画面震动一下）

    xw "管家，管家，你看着我长大，你能告诉我到底发生了什么吗？"#（忧郁）

    "【管家】" "唉。大小姐，快逃吧，带着嘉然小姐，逃到谁也找不到的地方，时间不多了......"
    show xw wronged with dissolve
    xw "可是我什么都不知道啊，我该逃到哪里去，我该怎么做？" # (委屈)


    scene 病房房间晚上
    show xw sad at l2
    with Dissolve(3)

    xw "（乃琳出发之后，然然也醒了过来。她失忆的症状更严重了。）"
    xw "（乃琳，珈乐，贝拉，然然几乎忘了所有人。）"
    xw "（她现在记得的人只有我一个。）"
    xw "（被然然依赖着的样子很好，但是会想，要是然然再把我忘记了该怎么办。）"
    xw "（就凭我自己做的到吗，我一直是依靠着大家过来的，现在把一切不由分说地都交到我身上，要是我辜负了大家的期待，就算我用尽我所有的能力后还是失败怎么办呢。）"
    xw "（然然，这次我能把你救出来吗。）"


    scene 枝江天台1
    show xw cry at r2
    with Dissolve(3)
    ## 大厦天台火场差分 向晚哭泣 中

    "乃琳，珈乐，贝拉已经失去了踪迹。"
    ## 人群喧闹声

    "人们在楼下欢呼着，叫嚣着让她去死。"

    ## 圣嘉然左亮 向晚右暗
    show jr sacred dignified at l2 with dissolve
    sjr "晚晚，我走了，即使我不在了，我希望你能快乐，你能过上想要的生活。"
    "她将一个紫色水晶从脖子上摘下来系在向晚的手腕上，然后在向晚的脚下用黑色火焰画了一个圈。"
    show jr sacred pray at l2 with dissolve

    "神说，不许向晚走出圈子外，向晚就走不出圈子之外。"
    ##圣嘉然舞蹈CG

    "嘉然登上了情感转换装置，火焰从四周燃起，嘉然在承受着火焰灼烧和接收负面情感的痛苦中给大家跳着献祭的舞蹈。"
    show jr sacred dignified at l3
    show xw at lc
    show ac normal at r3
    with dissolve

    "阿草举起了手，对准了晚晚，不可视之手向着向晚袭击过去。"
    ## 出圣嘉然立绘 中亮 向晚阿草暗
    sjr "晚晚！！"
    show xw:
        ease 0.3 xoffset -60
    "嘉然急忙解除了向晚的移动限制，向晚很轻易地就躲开了。"
    ##阿草亮左 向晚中嘉然右暗
    show ac happy

    "阿草把手对准了舞蹈中的嘉然。"
    ac "这一次可不会打偏了。"

    show xw zorder 80:
        ease 0.4 xoffset -300
    $renpy.pause(0.5)
    play sound "audio/se/击打.mp3"
    show xw:
        ease 0.2 yoffset 50

    with sshake


    "向晚挡在了嘉然身前，结结实实的一拳打在她单薄的身躯上，像是要将她的脊梁压垮，无形的巨大拳头从她身上碾过，她的嘴角渗出鲜血。"

    "向晚摇摇欲坠，但依然站在嘉然身前。"
    show xw despair with dissolve
    #向晚哀 中国
    xw "我只是想和大家一起过着简单的生活而已。"

    play sound "audio/se/击打.mp3"

    with sshake
    show xw:
        ease 0.2 yoffset 50

    "又是一拳打在向晚的身上，向晚咳出血来，晃着身子，用手去触碰那不祥的黑色，火焰沾染上了她的身躯，但她半跪着，还没倒下。"

    show xw sad

    xw "连那么简单的愿望都没办法实现吗..."

    play sound "audio/se/击打.mp3"

    with sshake
    show xw:
        ease 0.2 yoffset 50
    "保护她，我要保护她......"
    window hide

    play sound "audio/se/击打连击.mp3"
    show xw cry:
        easeout_back 7 yoffset 300

    scene black with Dissolve(7)
    show screen saying("直到向晚的世界完全黑暗。" ,0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(2)
    hide screen saying with Dissolve(2)
    stop sound

    scene black
    show 逆时针 zorder 5 at normal_size
    with dissolve
    $ renpy.pause(3, hard=True)
    hide 逆时针

    #【回忆效果】
    scene 晚晚家
    show screen memory_filter
    with dissolve

    window show
    "【向晚父母】" "向晚，你是我们的骄傲。"
    "墙上挂满了向晚获得的各式各样的荣誉，奖状，奖杯，奖牌在灯光地照亮下闪烁着荣誉的耀眼光芒..."
    "【向晚父母】" "这次的大学考试，我们的晚晚肯定没问题。你打算安排她去哪里，做好准备了吗。"
    "【向晚父母】" "我们晚晚只要上最好的那两所大学就行，我已经打好招呼了，无论晚晚想要学习哪一个专业，都会有人领着她发展的，你就放心吧。"
    ##关门声
    play sound "audio/se/木门关门.mp3" fadein 2
    "向晚虚掩着的房间门关上了。"
    stop sound
    "可能就是这个时候，向晚知道了过分紧张会引发失眠和极度的腹痛。"


    scene 排行榜
    show xw sad at l4
    with fade

    #【学校排行榜】
    "晚晚在成绩红榜的最底下找到了自己的名字，那个数字刺痛了她倔强的心。"
    "【同学】" "呦，这不是我们的晚大小姐嘛，怎么这么拉了"
    xw "...."
    scene 晚晚家
    show xw sad at r2
    with fade

    "【向晚父母】" "晚晚，这个成绩是怎么回事？这可不像是你的发挥。"
    "【向晚父母】" "...算了，这分数好歹能读附近的枝江大学，离家近点也好。"
    "【向晚父母】" "枝江大学的表演专业你不能填，社会学和法学你看着选一个吧。"

    xw "可表演专业是枝江大学最好的专业..."

    "【向晚父母】" "你又不会唱歌又不会跳舞，没有做偶像的本事，不要考虑这些。"

    scene 晚晚家
    show xw worried
    with fade
    xw "（可我想重新证明自己。）"

    "晚晚在专业确认书的表演专业选项落下了笔。"

    scene 图书馆
    show xw calm
    with fade
    #【背景】图书馆
    "一个人背着包来到了枝江大学。"
    xw "我一定要证明自己，我一定要做到最好。"
    xw "我一定会成为最最有名的偶像！"

    scene 双人卧室
    show xw unconcerned at l2
    with fade

    "向晚冷着脸推开了双人宿舍的房门。"

    show jr happy at r2 with easeinbottom

    show xw shock at body_shake
    $renpy.pause(0.5)

    "【？？？】" "嗨！我是嘉然，是你最甜甜甜甜的小草莓哦~"
    "一个矮个子的女孩大声地和她打着招呼，把她吓了一跳，她好不容易才积攒下来的高冷一下子维持不住了。"

    scene 双人卧室

    show jr smile at r2
    show xw shy at l2
    with fade

    jr "晚晚，晚晚...你的泡面我能拿走吗。"
    ##向晚亮 嘉然暗
    xw "哼...拿走也不是不可以啦..."
    "因为瞒着家里人选了偶像专业，她被家里限制了生活费，最近只能吃泡面了，不过她无所谓，她觉得泡面比其他东西好吃的多。"
    ##嘉然右快乐亮 向晚左常1暗
    show jr happy at r2 with dissolve
    jr "晚晚，晚晚...我们是朋友了吧。"
    #向晚亮 嘉然暗

    "那个叫嘉然的女孩像是有着魔力一般，拉近了她从未和他人拉近过的距离。"

    "她自然地套走了自己的名字，套走了自己从未给过别人的电话号码，还套走了自己的泡面。"
    "突然多了一个朋友的向晚有些拘谨，她从来没有过朋友，不知道该如何和朋友相处。"
    ## 客厅背景 过度效果（先变黑再出来 表示换场景了 虽然场景还是同一张）

    show jr laugh at r2
    show xw normal at l2
    with fade

    jr "晚晚，是水族馆欸。"
    xw "是啊，想去水族馆看看吗。"
    show jr smile at r2 with dissolve
    jr "我想去！"

    jr "晚晚我们去看水母吧。"

    scene 水族馆
    show xw normal at l2
    show jr smile at r2
    with fade


    "然然牵着向晚的手，拉着她走入水族馆的深处。"

    "没有人会特地来水族馆看随波漂浮的水母。"
    "所以水母周围的人群很少。"
    "【解说员】" "水母是无脊椎动物，早在埃迪卡拉纪时就已经存在。"
    "【解说员】" "全世界的海洋中有超过两百种的水母，它们分布于全球各地的水域里，无论是热带的水域﹑温带的水域﹑浅水区﹑约百米深的海洋，甚至是淡水区都有它们的踪影。"
    "【解说员】" "有些水母不单颜色多变，而且还会在水中发光，有些闪耀着微弱的淡绿色或蓝紫色光芒，有些还带有彩虹般的光晕。"
    "【解说员】" "水母并不擅长游泳，它们常常要借助风、浪和水流来移动。当它们在海中飘动时，光影随波摇曳。"
    "【解说员】" "即使玳瑁和海龟和曼波鱼也能捕食它们，相比起其他海洋生物，只能在深海里度过相对失败的一生..."
    show xw sad at l2 with dissolve
    xw "水母为什么要度过相对失败的一生呢？水母为什么不能成功呢，水母也能成功啊。"
    xw "你怎么就知道水母就没有梦想呢..."

    show xw cry at l2 with dissolve

    "晚晚说着说着眼泪就落了下来。"
    ## 嘉然亮 苦恼
    jr "晚晚，我不知道水母有没有梦想，但我知道有一种没有梦想的生物。"
    ## 向晚亮
    show xw doubt at l2 with dissolve
    xw "...是什么？"
    ##嘉然亮
    show jr happy at r2 with dissolve
    "嘉然指着在水母一旁游弋的粉色海星。"
    jr "是派大星啊。"
    show jr smile at r2 with dissolve
    jr "派大星没有什么梦想，可能简单到去抓水母就能满足。"
    jr "而海绵宝宝和派大星不一样，它有着远大的梦想，它想要成为万人瞩目的明星，于是它渐渐变得圆滑，忘记原本的自我，忘记了派大星，它变得不快乐了。"
    jr "但无论晚晚选择当海绵宝宝还是派大星，我希望晚晚的梦想是做自己喜欢的事情，而不是因为追求梦想而失去自己。"
    jr "我希望你能快乐，不要迷失自己的方向。"
    #黑屏标题效果
    window hide
    scene black with Dissolve(3)

    show screen saying("晚晚，你要做你自己。" ,0.5,0.4,30) with Dissolve(2)
    $ renpy.pause(4)
    hide screen saying with Dissolve(2)

    #克苏鲁大厦天台
    scene black
    show 顺时针 zorder 5 at normal_size
    with dissolve
    $ renpy.pause(4, hard=True)
    hide 顺时针

    scene 枝江天台克苏鲁
    hide screen memory_filter
    with dissolve
    window show
    "魔力的火焰蔓延到夜空之上，天空闪烁着明暗不定的光辉，不可名状的声音响彻整个大气层。"
    "无数散发着五彩光辉的球体汇聚到一起，呈现各异的姿态，蔓延几千米也看不到尽头，这个世界已经陷入了绝望当中。"
    "......"
    "................"
    ##scene black

    show xw angery with dissolve
    xw "再来一次。"
    xw "我要再来一次。"

    #【紫色水晶散发光芒】
    window hide
    show 项链 with Dissolve(3)
    stop music fadeout 4
    $ renpy.pause(3, hard=True)
    scene black with Dissolve(2)

    show screen unskip
    $ renpy.pause(2, hard=True)
    $ renpy.movie_cutscene("audio/video/end/NE.webm")
    $ renpy.pause(2, hard=True)
    hide screen unskip

    scene black with slow_fade
    show screen saying("经历所有的痛苦，就能获得最初的希望" ,0.5,0.4,30) with Dissolve(3)
    $ achievement.grant('ne')
    $ persistent.ne = True
    $ check_playthrough()
    if (persistent.be1 and
    persistent.be2 and
    persistent.be3 and
    persistent.ne):
        play sound "audio/se/pass.mp3"

    show 水母 with Dissolve(2):
        align (0.50, 0.53)

    $ renpy.pause(6)
    hide 水母 with Dissolve(2)
    hide screen saying with Dissolve(3)
    $ renpy.pause(2)


layeredimage 水母:
    if persistent.be1:
        "shuimu.png"
        zoom 0.072

    if persistent.be2:
        "shuimu.png"
        xoffset 120
        zoom 0.072
    if persistent.be3:
        "shuimu.png"
        xoffset 240
        zoom 0.072
    if persistent.ne:
        "shuimu.png"
        xoffset 360
        yoffset -20
        zoom 0.10
