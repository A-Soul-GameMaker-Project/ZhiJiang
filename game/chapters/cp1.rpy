# 第一章上半部分

label cp1_start:
    window hide
    $ save.cp_name = "第一章"
    if not config.developer:
        show screen unskip
        $ _skipping = False
    $ renpy.movie_cutscene("audio/video/start/1.webm")
    if not config.developer:
        hide screen unskip
        $ _skipping = True

    scene black
    show screen saying("某种意义上，医院里亟待救治的病人其实有两种：\n一种是众所周知的，遭受病痛折磨的，时不时在病床上发出有气无力呻吟的。",0.5,0.4,30,addImg=True) with Dissolve(3)
    $ renpy.pause(5.0)
    hide screen saying with Dissolve(1)

    show screen saying("还有一种，是在病房外苦苦期待，精神上遭受着巨大的痛苦的。\n他们坐在家属区的座椅上蜷缩着身体，捂住面颊的双手支撑着全身的重量。\n病房里的任何信息都会使他们高度紧张，一阵风、一声咳嗽、\n甚至是自己的幻听全部一拥而上摧残着他们脆弱的神经。",0.5,0.4,30,addImg=True) with Dissolve(3)
    $ renpy.pause(10.0)
    hide screen saying with Dissolve(1)

    show screen saying("轮转的日月像是绞盘，每转一圈就使他们的呼吸困难一分。",0.5,0.4,30,addImg=True) with Dissolve(3)
    $ renpy.pause(5.0)
    hide screen saying with Dissolve(1)

    jump .part1

label .part1:
    # 【场景：枝江医院走廊-1】
    # 【bgm：平淡且忧郁】
    play music "audio/bgm/第一章医院.mp3" fadeout 5.0 fadein 4.0
    scene 医院走廊1 with dissolve
    window show

    "枝江医院。"

    # 【珈乐立绘居中出现】
    # 【立绘：jl_日常】
    show jl normal eyeclosd with dissolve

    jl "呼....啊，你什么时候醒的？"

    # 【jl立绘左移，bl立绘于右侧出现】
    # 【立绘：bl_日常】

    show jl normal at l2 with moveinleft

    show bl frown at r2 with dissolve

    bl "刚醒，不比你早多少。"
    show jl normal at l2 with dissolve
    jl "那病房里现在是...."
    bl "是晚晚在照顾，乃琳刚去公司了，马上就回来。"
    show jl sad at l2 with dissolve
    jl "你觉得然然的病多久会好？"

    # 【立绘：bl_悲伤】
    show bl sad at r2 with dissolve
    bl "我不知道....我甚至不知道然然能不能好起来。"

    show jl sad at l2 with dissolve
    bl "你不觉得然然的病在变得越来越严重吗？"
    bl "然然最开始是在开往医院的救护车上苏醒的，那时她还认识阿草，甚至开玩笑说自己晕车，找我要了水喝，找阿草要了糖吃，但等到了医院，她已经把阿草是谁都忘记了！"
    bl "我这几天都在想，然然会不会在不远的某一天忘记我们？"

    "贝拉紧咬牙关，眼泪大颗地滴落。珈乐手足无措地安慰她。"


    show jl firm at l2 with dissolve
    jl "那也没什么可怕的！只要大家还在一起就好，如果然然把我们忘记了，那我们就与她再相逢，重新来过。"

    "贝拉忍着抽泣，点了点头。"

    # 【音效：高跟鞋走路】
    play sound "audio/se/高跟鞋走路.wav" fadein 3.0

    "一串踢踢踏踏的脚步声响起，乃琳缓步向贝拉和珈乐走来。"
    stop sound
    # 【bl立绘由右侧左移至居中，nl立绘于右侧出现】
    show jl firm at l3 

    show bl hesitate at center 
    with easeinright
    show nl surly at r3 with easeinright

    "乃琳瞥见红着眼的贝拉，立刻收起笑容，弯腰坐了下来，一只胳膊环住贝拉的脖颈，轻轻把贝拉的头靠在自己肩上，另一只手轻柔的拂着贝拉的头发，同时给珈乐使了个眼色。"
    "珈乐恍然大悟，掏出纸巾轻轻擦干贝拉的眼泪。"

    # 【立绘：bl_日常】
    # 【立绘：jl_日常】
    show bl hesitate at center with dissolve
    bl "谢谢，我只是...."
    show jl normal  with dissolve
    jl "不要想那么多，任何事情我们都会一起面对的。"

    "贝拉抬头看向乃琳。"

    bl "公司那边怎么样？"

    # 【jl、bl、nl立绘消失】
    # 【画面切到CG：jr病房内，xw一只手握着门把手，默默地隔着门偷听，整体画面偏暗、偏冷色调】
    scene 门口向晚 with fade
    "向晚站在病房内，静静地听着。"
    # 【立绘：nl_日常】
    nl "非常不好，现在人们都在猜测我们之间发生了什么，一些激进的粉丝甚至闯进公司后广场砸了一些员工的车——其中就有阿草的。"

    jl "这倒是唯一的好消息。"

    "贝拉噗嗤一声，破涕为笑。"

    bl "但这样下去不是个办法，嘉然短时间不可能复播，我们可不可以用请假这个借口来掩饰嘉然失忆的事实？"

    nl "正因为嘉然短时间不可能复播，我们才不能对观众有所隐瞒。一旦请假，时间越往后拖局面就会越不受控制。"

    jl "如果我们实话实说呢？我是说，让粉丝和我们一起来面对嘉然失忆的事情。"

    bl "不行，别忘了嘉然是为什么晕倒的。一旦消息公布，粉丝一定会想方设法地接触嘉然，我们不能再让她遭受刺激了。"

    # 【背景回到医院走廊】
    scene 医院走廊1 with fade
    # 【立绘： jl，bl，nl_日常】
    show jl angery at l3
    show bl frown at center
    show nl thinking at r3

    show bl frown with dissolve
    bl "我这几天想了很多，如果嘉然不能再担负偶像的职责，那么我作为队长也..."

    # 【立绘：jl_愤怒】
    show jl angery with dissolve
    jl "贝拉！现在还远远没有到谈论应不应该毕业的时候！"
    bl "其实不是应不应该的问题。"

    "珈乐疑惑地看向贝拉。"

    bl "而是在利益的天平上衡量这个问题本身，就已经是对队友的背叛了。"

    "贝拉认真地看着珈乐，一字一句地说。"
    show jl pain with dissolve
    jl "可是贝拉，临阵脱逃难道不是对粉丝的背叛吗？你有没有考虑过粉丝的意愿？他们日复一日为我们倾注情感与心血，现在我们内部出了问题就想着抛弃观众临阵脱逃，这难道也是队长的职责吗？"

    "贝拉露出纠结的神色。"
    show nl thinking with dissolve
    nl "贝拉，我无意使你倾向于某种选择，但你必须在一个更冷静的状态下思考这个问题，至少不是现在。"
    show bl shock with dissolve
    bl "（叹气）我会重新认真考虑的。"

    # 【音效：开门声】
    play sound "audio/se/室内关门.mp3"

    "开门声打破了走廊尴尬的气氛，向晚一脸倦色地从病房走出"

    show xw sad at l3, dark 
    show jl sad at l1 
    show bl sad at r1 
    show nl frown at r3 
    with easeinleft

    "几个人对视一眼，没来得及说话，主治医生已经领着几个实习生朝她们走来。"

    "【珈乐，向晚，贝拉，乃琳】" "医生！"

    show xw shock with dissolve

    # 【nl、jl、bl立绘消失】

    "【医生】" "你们好，我们来查房了，嘉然今天饮食怎么样？"


    # show xw shock with dissolve
    xw "她今天上午没有吃东西，中午吃了医院提供的普食。"

    "【医生】" "精神状态呢？"

    # 【立绘：xw_悲伤】
    show xw sad with dissolve
    xw "她还在丢失自己的记忆，昨天她还记得直播房间的布置，今天她已经没办法回想起来了。"
    "【医生】" "我们这就进屋给嘉然做进一步的检查，你们这些家属也休息休息吧。"

    # 【立绘：xw_日常】
    show xw shock with dissolve
    xw "医生，嘉然到底是因为什么失忆的？"
    "【医生】" "现在很难讲，病人昏厥之前发生的事情我们已经知道了，但是据我们诊断不像是受外部刺激导致的选择性失忆，倒更可能是无明显诱因导致的连续性失忆。"

    "向晚没有说话，只是默默盯着病历看，似乎想找到“无明显诱因”里的一丝缝隙，然后将它扩张，撑开。在缝隙后面，里面，深处，到底藏着什么？"
    "医生对几个女孩点了点头，步入了病房中。"

    # 【立绘：医生消失，nl，jl，bl_日常】
    # hide "【医生】"

    "四个女孩在走廊简单确认了一下嘉然今天的情况。"

    nl "我上午已经休息好了，你们三个去休息吧，今晚我来照顾嘉然。"
    bl "辛苦你了，那我们先去吃饭？"
    jl "我也去餐厅吃个晚饭，马上回来。"
    xw "我想去外面走走。"
    nl "好，那我送你出去吧。"
    "几个人商议一番，贝拉和珈乐去三楼的餐厅就餐，而乃琳和向晚两个人则缓缓朝医院正门走着。"

    # 【立绘：四人消失】
    hide xw 
    hide jl 
    hide bl 
    hide nl 
    with dissolve


    # 【背景：切到医院走廊2，即另一张医院走廊的图片，BGM不变】
    scene 医院走廊2 with dissolve


    # 【立绘：nl_日常，xw_日常】
    show xw gloomy at l2 
    show nl normal at r2 
    with dissolve
    nl "晚晚，你的气色不太好呀，最近太累了吗？"

    "乃琳声音温柔夹杂着一丝丝疲惫，从进入A-SOUL见到乃琳的第一天，向晚就觉得她有一种大姐姐的感觉。那种气质让向晚发自内心相信她可以掌控任何局面，且深藏着不为人知的秘密。"

    xw "还好，最近经历的事情太多，心里很乱。"

    "乃琳微笑着点点头，然后二人陷入沉默。"

    show nl gloomy at r2 with dissolve
    nl "晚晚，然然失忆是一个咱们所有人都必须接受的事实。我们都很难过，但我们——"
    "乃琳观察着向晚的表情，出乎她预料的是，向晚竟然冷静地点了点头。"

    # 【立绘：nl_严肃】
    show nl frown at r2 with dissolve
    nl "但我们必须提前准备面对更大的风暴，比如——你其实在病房听见我们在走廊说什么了吧？"

    "向晚点了点头。"

    # 【立绘：xw_皱眉】
    show xw sad at l2 with dissolve
    xw "A-SOUL已经一周没有直播了，很难想象粉丝们在不知情的情况下会做什么。"

    nl "你的考虑很对。但这只是其一，其二是——我刚刚去见了阿草。"

    # 【立绘： xw_吃惊】
    show xw shock at l2 with dissolve
    xw "！！！"
    xw "阿草自从嘉然病倒就没有来见过我们，他在做什么？"

    # 【立绘： nl_日常】
    show nl gloomy at r2 with dissolve
    nl "准确的说，他拒绝向我透露任何信息。"

    "乃琳轻轻地靠住向晚，贴近她的耳侧轻声说"

    nl "但我发现我们宿舍区搬进了五个新来的女孩。"

    "向晚呆立在原地，不知是因为疲惫还是震惊，她并没有如乃琳想象中一样做出任何反应。乃琳意识到，危机真的可以令人成熟，于是鼓励地拍了拍向晚的肩。"
    show nl frown at r2 with dissolve
    nl "晚晚，接下来的话你要听好：不论之后遇到什么困难都要镇定，仔细地观察每个人的动作与想法，做出最好的选择。"
    # 【立绘： xw_严肃】
    show xw worried at l2 with dissolve
    xw "我明白了。"

    # 【立绘： nl_日常】
    nl "去枝江大厦看看吧，阿草要见你。"

    hide nl gloomy 
    hide xw anxiety 
    with dissolve


    jump .part2

label .part2:
    # 【傍晚 枝江城街景，】
    # 【BGM"舒缓中带着忧伤的音乐（可参考"Aruarian Dance）】
    # 【这一段是xw一路走向枝江大厦的内容，接下来可以通过点击路人或者galgame自动浮现路人图标的方式触发六段对话。考虑到工作量，这部分的路人立绘可以不做，然后xw的立绘也尽可能少地出现，这可以更好的表现xw在迷茫的行走，同时以一个旁观者的身份去观察这座城市的人。】

    # 【背景：枝江街景】
    # 【音效： 街头人流杂音】
    play music "audio/bgm/第一章黄昏街道.wav" fadeout 5.0 fadein 8.0
    scene 枝江街头1黄昏 with slow_fade
    play sound "audio/se/shopping_malls1.mp3"
    "下午五点，夕阳已经把这座城市的影子拖得很长，此时，枝江的街道便开始拥挤起来了。"
    "上班族夹着公文包，疲惫地耷拉着脸，从高高低低的建筑物里涌出来，然后再像涨潮一样漫开去。"
    "顷刻间，大小纵横的街道哗哗地满是车流与人潮，这是白日将尽的一次热闹和高潮。"

    show xw wronged at r2 with dissolve

    "身前是夕阳将堕，旁边是熙熙攘攘的人流，身后亮起明明暗暗的灯火"
    "向晚感觉自己又一次处在了这方世界光与暗的交界处，这条光与暗的分界线不断向西推进，向晚麻木地被身后的黑潮驱赶着向前走"
    "向晚正走向这座城市的中心——枝江大厦。"
    "她几乎是在无意识地向前挪动步伐"
    "这段时间她经历的事情是如此的突然且复杂，自从A-SOUL直播的第一天，她几乎日日夜夜都想象着这一刻"
    "可等这一刻终于到来时，她反而只有全心的麻木和浑身的疲惫。"
    "拥挤的人流中，她撞上一个又一个同样迷茫而麻木的灵魂。"
    "医院街对过的路边小吃摊升腾起了烟火气，上班族拥挤地排着队，有两个孩子在一旁流连。"
    # 对话一
    "【妹妹】" "呜呜，哥，我好饿。"
    "【哥哥】" "别哭了，会让外人看笑话的！王大叔他们说A-SOUL今天晚上就复播了，只要复播就能产生多巴胺，等哥哥把咱们的多巴胺抽取出来卖到黑市上，明天就有饭吃了！"
    "【妹妹】" "真的吗？我要吃芝麻烧饼！"

    # 【立绘： xw_悲伤】
    xw "......"
    # 【xw立绘消失】
    # hide xw sad with dissolve
    "沿着小吃摊一直走，亮丽的办公楼逐渐褪色，衰败，佝偻，变成了一排排灰黄的居民楼，这座城市的高楼大厦所遮掩的底色也逐渐显现。"
    "居民楼底商做着各种各样的生意，古董、服装、餐馆、理发店，当然，还有那些给去不起大医院的贫民开设的小医馆，"
    "这种医馆的医生当然也是专业的，但设备的缺乏使他们对于复杂病症终究无力回天，医馆门口铺着一张张的草席，那是贫民迎来生命最终时刻的地方。"

    # 对话二
    "【贫民1】" "啊...向晚...你是向晚妹子吗？"

    # 【立绘： xw_吃惊】
    show xw shock  with dissolve
    xw "！！！"

    "【贫民2】" "不好意思啊姑娘，我兄弟时日不多了，这几天恍惚之间常常念叨他喜欢的偶像向晚。"
    "【贫民1】" "向晚...你是向晚妹子吗？"

    # 【立绘： xw_悲伤】
    show xw sad with dissolve
    xw "我是..."
    # 【xw立绘消失】
    # hide xw with dissolve

    "【贫民1】" "妹子，我就知道你会来看我！听阿兄一句劝，挣了钱早早离开枝江，这不是人呆的地方。"
    "【贫民1】" "啊...你能来看我，也不枉我一番心意了。"
    "【贫民2】" "姑娘，谢谢你。"

    # hide xw with dissolve


    # 对话三
    "再往前走是一家古董店，店里唯一的古董是这栋居民楼。"
    "【字画摊主】" "瞧一瞧看一看，王右军、颜鲁公真迹、达芬奇手稿，卡拉瓦乔画作，保证都是手工的。您要来看一看吗，哈利路亚！ "
    "【路人】"  " 这幅字看上去不错。"
    "【字画摊主】" "您的品味真高，这幅字的墨水用胆汁调过，所以颜色比较鲜艳。"
    "【路人】"  "唔，听上去有些恶心。"
    "【字画摊主】" "话不是这么说的先生，牺牲，sacrifice，是这座城市的主题，更是这个世界的铁律。神视万物，也许我们早已不知什么时候与救主订立了契约，哈利路亚！"

    "【字画摊主】" "也许你也在不知不觉间与救主订立了契约，哈利路亚！哈哈哈哈哈哈！"

    "向晚走过居民区的理发店，理发店近几年有成为娱乐场所的趋势，不少人来理发店是为了体验头发簌簌掉落的出尘感，仿佛他们的烦恼丝真的被剪断了。"
    # 对话四
    "【理发师】" "顺便给您做个发型吧，今天新店开业八折优惠。"
    "【醉汉】" "呵呵，不行啊，我身上只剩一百块了，五十块买酒喝，五十留着打给A-SOUL。"
    "【理发师】" "这....还是身体更重要吧！你们当地人真奇怪！"
    "【醉汉】" "呵呵，我何罪之有？你瞧瞧这些人，为了蝇头小利压抑天性，最后花尽一生积蓄反倒背上一身重债。我们同样是在捞水中月的蠢人罢了。"
    "【理发师】" "这人醉得厉害，真是头疼。"
    "【醉汉】" "文人墨客，俱为喉舌；酒社诗坛，浅浅一党。寒窗无用，何必凿光。嗝....卖书沽酒，同去癫狂。"

    # 【再次点击街边手术摊(?】
    "【醉汉】" "真想醉倒在一场A-SOUL的直播中啊。"
    "走出这座小区，向晚的视野又一次明亮起来，一幢幢外表素白造型典雅的欧式别墅映入眼帘，街道那头就是富人区的别墅区，一辆辆出租车饥渴地停靠在街边拉活。"

    # 对话五

    "【老人】" "年轻人，去DSA大楼，我就要冷冻自己啦。"
    "【出租车司机】" "老先生，现在去DSA公司...那儿的冷冻仓早就满额了。我还听说DSA在偷偷销毁冷冻人体..."
    "【老人】" "呵呵，这你就不懂啦。"
    "【老人】" "冷冻的意义并不在于重生，而在于让我在死前的最后一刻也能满怀希望。这样的希望，连死亡都奈何不了哇。"
    "【老人】" "小芮，帮爷爷把包放上...这里面装着我女儿的老物件，还有偶像女团A-SOUL的一些海报。"

    "【出租车司机】" "想不到您还看偶像呐！"
    "【老人】" "是啊，这里面有个叫贝拉的孩子，说话声音很像小芮他妈妈，我当时靠看她的直播缓解自己的丧女之痛，我至今都很感激他们呀。"
    "【出租车司机】" "原来如此，老人家，我们这就出发了。"
    "【老人】" "小芮，就送到这里吧，让我们爷儿俩满怀着希望告别吧！"
    "【小芮】" "爷爷，我们一定会在未来再见的，因为我会成为世界上最最最伟大的科学家！"
    "【老人】" "哈哈哈哈，出发！"

    # 【再次点击出租车三人】
    # 【立绘： xw_悲伤】
    show xw cry with dissolve
    "【小芮】" "为什么最后爷爷会哭呢？冷冻就像是睡着了，总还有苏醒的那天呀。"
    xw "......"
    # 【xw立绘消失】
    # hide xw with dissolve
    # 对话六
    "向晚的心中升腾起一种奇妙的感觉。当她走过潮湿的贫民窟，孩子们在老旧楼房围成的轮回中走向麻木，她觉得这座城市很矮很矮；"
    
    "而当她走过鳞次栉比的高楼，巨大的LED广告屏像佛一样悲悯地俯视众生，她又觉得这座城市很高很高。"

    "【颓废小哥】" "嗯？你还指望我说什么吗？我对这个世界没什么可说的了。"

    "向晚就这么放空大脑，任凭直觉和人潮裹挟她向前，"
    "她确信终点一定会是枝江大厦，因为它是这座城市的中心，不论是地理、人口，还是经济、信仰，毫不夸张地说——"
    "它支配了枝江人的一切生活。"


    jump .part3

label .part3:
    # 【这一段是xw走进枝江大厦坐电梯前往七楼见阿草的内容。】

    # 【CG： xw站在月华大楼前】
    # play music "audio/bgm/悬疑诡异气氛通用BGM1.mp3" fadeout 5.0 fadein 5.0
    scene 枝江大厦外 夜 with slow_fade
    show xw unconcerned frown at l2 with dissolve

    "前方视野的亮度逐渐变暗，变暗，这颗恒星赤红的余烬最终被一座深黑色的巨型建筑所遮挡。"
    "整座大厦一万多个单向窗户的镜面反射出向晚身后追赶她的夜幕。"
    "映现的虚像与镜后的实物使向晚彻底被包裹在黑色中，浅紫色头发的她站在这黑棺一样的大楼前——"
    "——像是水母终于沉入了深海。"

    # 【背景： 月华大楼前台】

    "枝江大厦到了。"
    "向晚快步走向了大门，早就等候在那里的门童讨好地替她拉开了宽大的玻璃门，"
    scene 枝江大厦前台 
    show xw unconcerned frown at l2 
    with dissolve

    # show "【门童】" at center with dissolve
    "【门童】" "尊贵的制作人Z004，请进。"
    "门童印象中那个以往总是开朗爱笑的向晚小姐，今天不知道怎么回事，好像心事重重，既不报以迷人的微笑，也没有给出小费。"
    "向晚疲惫地皱着眉头，一路小跑着进了直达七楼的电梯。"

    # 【音效： 电梯开关门声效】
    play sound "audio/se/电梯开门.wav"
    scene 电梯内部 
    show xw unconcerned frown 
    with dissolve

    # 【背景： 电梯】
    "说来奇怪，这栋大楼非常高，但员工实际能达到的最高楼层只有十三层"
    "当她看着电梯上13楼到顶层的按钮上密密麻麻的红叉时，她总是心悸的抬头向上，仿佛深黑的天井有着某种深邃而不可告人的秘密。"

    # 【音效： 电梯开关门声效】
    play sound "audio/se/电梯开门.wav"
    scene 电梯井
    show xw unconcerned frown at l2

    with dissolve

    "电梯很快将她带到了一条光线有些幽暗，地上铺着松软的地毯，墙壁上镶着名贵木料的走廊上面。"
    "这里就是枝江大厦的十三楼，一共有四大区三十四间房间，"
    "呈回字形布置——枝江大厦的二到十三楼是回字形的中空布局"
    # 嘉然、向晚、贝拉、乃琳、珈乐等人在这里都有自己的房间。"
    "除了办公室和工作室外剩下的几间房间，大部分时间作为杂物室使用，最近则腾出给新来大厦工作的几位“储备演员”居住。"
    "十三楼西部是办公区，她们习惯将占了十三楼一半面积的办公区称之为“西区”。"

    jump .part4

label .part4:
    #【向晚（时间意义上的） 阿草办公室】
    #【BGM：沉静的音乐（可参考：Clear Water 或 624 Part 2）。】
    # play music "audio/bgm/放松 温暖 平静.mp3" fadeout 1.0 fadein 3.0

    "向晚走到阿草办公室门口，几个同事恰好拖着几个大纸箱走出办公室，她往里瞟了一眼，纸箱里无非是一些平日的办公用品，但有样东西极为眼熟——贝拉的浅绿色美瞳盒。"
    "向晚深吸一口气，敲了敲虚掩着的门。"
    play sound "audio/se/清脆的敲门声.mp3"
    ac "请进。"

    # 【阿草立绘出现】
    scene 阿草办公室 夜 # 【背景切到办公室。】

    show ac normal at r2 
    show xw anxiety at l2
    with dissolve
    "办公室里，向晚见到了焦头烂额的阿草，他眼睛里面布满血丝，整个人有气无力地坐在办公桌后的椅子里面，好像个行将就木的老人。"
    show ac normal with dissolve
    ac "你总算来了，嘉然情况怎么样？到底是出了什么问题？"

    play music "audio/bgm/悬疑.mp3" fadein 5.0 fadeout 5.0
    #【向晚立绘出现】
    show ac normal with dissolve
    show xw anxiety with dissolve
    xw"嘉然失忆了，非常严重，她失去了成为偶像这段时间的记忆，但还依稀记得我们四个人。"
    xw"医生诊断为选择性失忆症，且无明显诱因。"
    ac"但我们都知道她出事前发生了什么，她受了刺激，很可能是她的大脑为了防止自己受到过大精神创伤而触发了自我保护机制。"
    ac"逼迫她的观众就是诱因，他们几乎毁了我们！"
    xw"阿草，你在有罪推定，真相查明前没有人可以言之凿凿地加罪于我们的观众。"

    show ac panic with dissolve
    ac"向晚？"

    show xw worried with dissolve
    xw"怎么了？"

    show ac normal with dissolve
    ac"你似乎一夜之间变成熟了，我很难想象你会说出这种话。"
    xw"嘉然病倒之后我想了很多，人是会变的，就像我同样很难想象你要解散A-SOUL。"
    "阿草突然吃惊地盯住向晚，专注看她的眼，像是想要透过她深海色的瞳孔里看到什么。"

    show ac panic with dissolve
    ac"我并非要解散A-SOUL，只是...."

    show xw anxiety with dissolve
    xw"只是恰好把贝拉工位上的东西清走，然后新的五个女孩也恰好入住了宿舍区，是这样吗？"

    show ac normal with dissolve
    ac"向晚。"
    ac"你很愤怒，我理解这种感受，你们几个姐妹之间情谊深厚。"
    ac"可你看不到的是，公司早已与整座枝江的命运联结在了一起。"
    ac"A-SOUL直播早已成为了这座城市每个人生活的一部分，影响着他们的娱乐形式、社交方式、文艺审美、价值认同。"

    show ac normal with dissolve
    ac"你以为这是场演出,实则不然。"
    ac"这是每天长达两个小时的心理治疗，这份短暂的快乐支撑着多少人迎接明天太阳升起，从一个倦惫的早晨奔赴另一个倦惫的早晨？！"
    ac"枝江一天都不能没有演出。"
    ac"我筹备开启新企划，是因为我从多数人的利益出发"

    "向晚感到一阵反胃，刚才还迁怒于观众的人，现在说自己从观众的利益出发。"

    show xw worried with dissolve
    xw"可不可以出一份公告说明嘉然的情况并请假，我们四个补齐嘉然的直播时间？"
    ac"请假？请多久？无限期吗？观众绝不会接受这种含糊其辞的说法。"
    ac"现在摆在我们面前的只有两种选择，暂时让嘉然毕业，或者中止A-SOUL企划开启新企划，请你考虑吧。"

    show xw wronged with dissolve
    xw"...."

    "房间陷入了诡异的寂静，为了缓和气氛，阿草从抽屉取出一瓶红酒，打开后倒入两只高脚杯，把其中一杯推到了向晚面前。"

    show xw worried with dissolve
    xw"谢了。"
    "向晚一饮而尽。"
    "阿草盯住向晚，专注看她的眼，看她的眼神是不是发痴，发呆。"
    "他还看向晚的脸，是不是没有预兆地，没有节制地，乱笑，瞎笑，无头无绪地笑。"
    "看了许久，未见端倪，他斜着眼，再瞟向晚一眼，小心翼翼地问"

    show ac normal with dissolve
    ac"你不会也那样了吧？"
    xw"哪样？"
    ac"精神出现异常，我总觉得你和平时不太一样。"

    "向晚盯住阿草的眼睛。"

    xw"谁面对这种变故时能和往常一样呢？"

    "阿草也讪笑一声，举起杯喝干。"
    "两个人碰了碰空的酒杯。"

    xw"其实我考虑过嘉然毕业的情形，我也想过继续直播。"
    ac"嗯哼，这想法很好。"
    xw"但每当我产生这样的想法，脑子里就会浮现出我和她相处的点点滴滴，以及嘉然在病床上平静温柔地看着我的景象。"
    xw"负罪感就像是鼓槌一样在我头上敲打着，仿佛要催促我终结这种不负责任的幻象。"
    xw"我当然可以装作不在乎地继续向前，告别旧的人，面对新的人，道旁依旧是鲜花和欢呼，前面是一场比一场盛大的演出。"
    xw"可嘉然呢？她永远停留在过去了。"
    xw"她也是A-SOUL这个团队的一员，我们做出决定的时候问过她的意见没有？"

    show xw anxiety with dissolve
    xw"就因为她失忆了，所以她应当被无视？"
    xw"阿草，你知道你在做什么吗？"

    show xw angery with dissolve

    xw"你在决定一个人的前途命运，而这个人甚至根本不在场。"
    xw"嘉然失忆了是没错，那她怎么了？她是一个笔误？她需要橡皮擦擦掉？或者她就是一个包袱，你们急于甩脱的负担？"

    "阿草深吸了一口气。"

    ac"所以你是不会留在这里了。"
    ac"我明白你的意思了，但我希望你能尊重其他几个姑娘的选择权，把我的意见转告给她们。"

    show xw worried with dissolve

    xw "我会的。不论她们如何选择，我不会阻挠。"

    show xw anxiety with dissolve

    xw "但我相信她们最终会和我做出相同的选择。"

    show xw worried with dissolve

    "阿草点了点头，二人又用空酒杯碰了一次"
    "他们又谈论了一下今晚的天气，朗月在空，雾霾尚未降生；谈论了新企划，名字就叫B-SPIRIT，宣传工作已经就绪；"
    "还谈论了新来的几个姑娘，都是很有天赋且努力的姑娘，如果A-SOUL几个人都不愿留在这儿，这几个姑娘会代替她们，开启这座城市新一轮的狂欢。"

    ac"可惜她们还是缺乏经验，如果你们肯再留一阵子，带一带她们就好了。"
    xw"抱歉啊，我嘴太笨教不会别人的。"

    "向晚强作欢笑地望着他，言辞恳切，两个人如机械般用空酒杯碰了一次又一次，场面颇有些萧条，然而在枝江，人与人的关系就是如此，无论如何面子都得维持。"
    "冷语相对摔门走人不是枝江人的风格。他们早已习惯了笑脸迎人，即便内核溃败如一只烂苹果。"
    "一张脸，像是真的，又像假的。仿佛笑容早已成为最基本的礼节，而愤怒才是一个人最后的自由。"
    "天色向晚。言笑晏晏，春风和煦。这场会面几乎得以体面了结。"
    "直到——"

    ac"如果可以的话，能不能今后尽可能少的接触嘉然？她目前最好少受到外界刺激。"

    show xw angery with dissolve

    xw"你已经对她造成刺激了！"
    xw"抱歉，恕！难！从！命！"
    hide xw with easeoutleft
    play sound "audio/se/砰的一声关门.mp3"
    "向晚起身头也不回地走出，砰的一下关上了门。"
    show ac proud at center with easeinright
    ac "...."


    jump .cp1_end

label .cp1_end:
    if persistent.playthrough > 1:
        "但向晚对此已经毫不在意"
    stop music fadeout 5.0

    scene black with Dissolve(3)

    jump cp2_start

