
label cp7_n_start:
    $ save.cp_name = "第七章"
    window hide
    scene black
    show screen saying("然而在黎明到来之前，娇小的少女已经被无知的人掳走。" ,0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(4.0)
    hide screen saying with Dissolve(2)

    if not config.developer:
        show screen unskip
        $ _skipping = False
    $ renpy.movie_cutscene("audio/video/start/7.1.webm")
    if not config.developer:
        hide screen unskip
        $ _skipping = True

    jump .part1


label .part1:


    scene 枝江市广景昼 with dissolve#场景：枝江市广景
    play music "audio/bgm/电颤琴 枝江.mp3" fadeout 3.0 fadein 3.0
    window show

    "人群骚动着涌向了一个不起眼的街区。" with sshake #文本框震动

    "谁会知道，在这么一个物欲横流的世界里，还有什么会让他们失去近乎所有的理智？"

    scene 双人卧室 with fade #场景：道路 早
    show jr smile with dissolve #立绘：嘉然-日常
    "嘉然不知道。"
    "她只记得自己躺在房间里安静的看着电视，沉重又急促的敲门声突然响起。"
    "来不及多想，她一遍答应着一边踩着鞋子一蹦一跳的赶到门口。"
    "拧开把手，看到的是一群不速之客。"

    "【一个魂们】" ".....嘉然小姐？"

    show jr smile with dissolve #立绘：嘉然日常
    jr "大……大家好啊，我是嘉然。"
    "虽然她不知道这股狂热从何而来，但出于礼节，还是有些紧张的打了个象征性的招呼。"


    "一个魂听到了这句再熟悉不过的开场白，更是像火山爆发般活跃起来。"
    "【一个魂们】"  "真的是她！……嘉然！我的嘉然！……圣嘉然！……嘉然小姐！！" with sshake #文本框震动

    show jr doubt with dissolve
    "这场狂欢并没有持续多久，一个魂们很快就意识到这个小房间容不下他们的明星。"
    "本就失去理智的他们早已放下最后的的礼节，无数只蠢蠢欲动的手伸向了那位穿着粉色连衣裙的少女，哪怕她对此并不知情。"
    "还没来得及反应，推搡的人群已经将最后一块空间占领，娇小的身躯像是浮萍，被人海推动着，走向一个不确定的地点。"
    "就像一支纪律严明的禁卫军，来势汹汹，气势逼人。"
    "相比较于之前的漫无目的，现在他们充满了信心。"
    "这次行军没有终点。"
    "亦或是，带走这位“女王”就是他们的终点。"


    scene 购物中心早 with Fade(1,0.5,1)  #【黑屏转场】 #【场景：购物中心 早】
    show jr normal with dissolve #【立绘：嘉然日常】

    play music "audio/bgm/诡异.mp3" fadeout 4.0 fadein 4.0 #【bgm：诡异】

    jr "你们……不累吗？话说……这是要去哪啊"
    "栗色头发的少女有些担忧的看着簇拥着她的人群。"
    "人，很多的人。"
    "他们有说有笑，他们眼里有光。"
    "这是嘉然最直接的印象。"

    show jr smile with dissolve #【立绘：嘉然喜悦】
    "她也很想和这些新来的“朋友”凑在一起，分享一下他们的快乐。"

    hide jr with dissolve #【无立绘，仅背景】
    "但每当她靠近任何一个人时，对方都会不由自主的往后退。"
    "他们的眼神在那一刻变得很复杂。"
    "欣喜却又胆怯，还有那么一丁点难掩的失落。"
    "这不是她想要的回应。"
    "陌生的环境，奇怪的人，不知走到哪的旅途……"

    show jr sad with dissolve #【立绘：嘉然悲伤】

    jr "呜……晚晚……"
    "少女停下了脚步，任由自己的情绪蔓延。"

    "几乎是同时，这支“大部队”也停顿下来，短暂的寂静后，是一群人的窃窃私语。"
    "【一个魂们】"  "……什么？……哭了？……谁？"
    "【一个魂们】"  "是你！……你！……你怎么敢把嘉然！！……嘉然！我的嘉然啊！" with sshake
    # 【BGM：危机】
    "队伍里的怒意开始蔓延。人们开始互相推搡，嘴里也是骂骂咧咧。"

    show jr shock with dissolve #立绘：嘉然吃惊
    jr "别……大家别这样啊，好孩子是不可以说脏话的……"


    play sound "<from 0 to 10>audio/se/嘈杂的人声.mp3" fadeout 3.0 fadein 3.0 #【BGM：嘈杂人声】
    "还没等她说完，一个魂们就已经失去了最基本的理智，一场不可避免的冲突已经发生。"

    show jr shock with dissolve #【立绘：嘉然吃惊】
    jr "呜……这是？你们在干什么？喂！借过，借过一下可以吗？让开啊！"
    #【BGM：急迫】
    "但她看不到，最里圈的人们手拉手结成了人墙，将这出闹剧隔绝于世。"
    "回过神的嘉然胡乱地抹了抹眼泪，原本干净的衣袖多了几道水痕。"

    show jr angery with dissolve #【立绘：嘉然愤怒】
    jr "别打了！停！停下来啊！你们……还有你！停！手！"
    "她从来没有这么声嘶力竭地喊过，本就孱弱的身体更是透支了力量。缺氧带来的头晕，声带几近撕裂的颤音，还有像风箱一样工作的肺部……它们在愤怒地抗议着这种行为。"


    "最不理智的声音带来的却是最理智的结果。"
    "人们的骚动逐渐平息，虽然眼神里还带着一抹疯狂。"
    play music "audio/bgm/电颤琴 枝江.mp3" fadeout 3.0 fadein 3.0

    show jr smile with dissolve #【立绘：嘉然喜悦】
    jr "这样，这样才对嘛，大家都是最好最好的朋友……咳咳，有什么事，咱们可以慢慢聊嘛~"


    "（短暂的沉默）"
    #【BGM：激动】
    "【一个魂们】"  "圣嘉然！圣嘉然！圣嘉然！" with sshake

    show jr shock with dissolve #【立绘：嘉然吃惊】
    jr "（好奇怪……他们说的是我吗？哎，算了，现在就挺好）"
    "看起来一切都归于了平静。"


    "【一个魂A】"  "嘉然小姐！您可以再给我们跳一支舞吗？"
    "【一个魂B】"  "对对对，还记得吧！就是那个宅舞二十连跳！随便！随便一支都可以！"
    "【一个魂C】"  "我想听《Quiet》！哪怕就是一小段！"
    "一个魂们回想起他们印象里的那个女孩，亦如现在的模样。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】

    "面对着热情的请求，少女只是安静的坐在椅子上，很努力的维持着微笑。"
    "这是她目前唯一能安抚大家的手段了。"
    "人们七嘴八舌的诉说着他们的愿望，她也在静静的听着。"


    "终于，热情的一个魂把手机举到了她的面前。"
    "那是一个和她长得一模一样的女孩，跳着她不曾接触过的舞蹈。"

    show jr normal with dissolve #【立绘：嘉然日常】
    "就看了短短几秒，嘉然就很肯定的认为这是她会为之羡慕的模样。"
    jr "真好啊……（小声）要是我也能这样……"


    # 【BGM：焦虑】
    "凑在嘉然身边的人已经察觉到了一丝异样。"
    "不安的情绪又在人群中传播开来。"
    "人海默不作声的从两侧分离。"
    "道路的尽头，是一副A-SOUL的海报。"
    "五个少女笑得熠熠生辉。"

    show jr normal with dissolve #【立绘：嘉然日常】
    "少女盯了很久，却还是面无表情。"


    "【一个魂们】"  "嘉然……小姐？"
    "他们期待得到一个答案，一个完美的答案。"

    show jr sad with dissolve #【立绘：嘉然日常】
    # 【BGM：冷静】
    jr "贝拉……乃琳……珈乐……晚晚……"
    "她轻声念着四个人的名字，唯独最后一个名字如鲠在喉。"


    "有人颤抖着举起手指向最后一人的画像，问道。"
    "【一个魂们】"  "她是谁啊？"

    "全场陷入了死一般的寂静。"

    show jr normal with dissolve #【立绘：嘉然日常】
    jr "……我是嘉然。"

    "一切好像都结束了，一个魂们找到了他们朝思暮想的嘉然小姐，她会唱着《猫中毒》，画着简笔画，跳着宅舞，永远的陪在大家身旁。"


    "真的吗？"

    scene 购物中心早 with dissolve #【无立绘，仅背景】
    "少女的下一句话缓缓地从口中脱出，犹如绝对零度般，冻结了全场的气氛。"

    show jr normal with dissolve #【立绘：嘉然日常】
    jr "我不认识她。"
    # hide jr normal with dissolve
    play music "audio/bgm/诡异.mp3" fadeout 4.0 fadein 4.0 #【bgm：诡异】

    jump .part2

label .part2:
    scene black with Fade(1,0.5,1) #【黑屏】 #【黑屏转场】
    "理想主义和现实主义最大的差距是什么？"
    "一个只会空想……而另一个……"
    "行动至上。"

    scene 购物中心午 with dissolve #【背景：购物广场，午】

    "人群在逐渐散去……"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "哎？大家……你们要去哪里啊？还有你，停下，停一下啊……"
    "看上去大家都接受了现实……不是吗？"
    "很快，她意识到了些许的不对劲。"
    jr "这是……医院？。"

    scene 购物中心午  with dissolve#【背景：购物广场，午】
    #【无立绘】

    "原本投放着广告的大屏突然断了信号，随即而来的是一条紧急新闻。"

    #$ tmo_name = "主持人"
    "【主持人】" "现在紧急插播一条信息……有大批人群……冲进……请各位市民……"
    "西装革履的主持人尽可能的想将事态描述的缩小化，但是前方传来的画面让谁看了都会触目惊心。"

    #$ tmo_name = "前方记者"
    "【前方记者】" "好的主持人，我们现在可以看到……等下，你们在干什么！我们是记者！有豁免……"
    "镜头剧烈的抖动了几下，居然没有中断，然而接下来的一幕又带着些许的戏剧性。"
    "【一个魂们】"  "嘉然小姐！看到了吗！你会想起来的！一定会……"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    "她当然能看到，除了看到这些人几近扭曲的面庞，还能看到背后乱成一团的现场。"
    "信号切断了，主持的脸色比之前更差了一些，强打精神背着那套官方说辞。"

    show jr normal with dissolve#【立绘：嘉然日常】
    jr "（呆呆的挥了挥手）……看得到哦。"



    scene black with Fade(1,0.5,1) #【黑屏】 #【黑屏转场】
    show screen saying("什么？A-SOUL在这个城市的热度？开玩笑，还有谁不是他们的粉丝吗？",0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(2.0)
    hide screen saying with Dissolve(1)

    show screen saying("还有谁不是这场暴乱的参与者呢？",0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(2.0, hard=True)
    hide screen saying with Dissolve(1)
    #【本句在屏幕间居中出现】

    scene 人群 #【背景：枝江市】

    "有的人目标明确，有的人漫无目的。"
    "医院，蛋糕店，舞台……"
    "他们的热情无处安放，他们的失望无以复加。"
    "在病房里，踩着一地狼藉的粉丝们终于找到了那个女孩的病历。"
    "蛋糕店中，精致的点心无人问津，唯有草莓蛋糕被洗劫一空。"
    "舞台之上，巨幅的海报被强行拽下。"
    "那是他们的旗帜。"
    "那是他们的希望。"

    # scene 购物中心午 with Fade(1,0.5,1) #【背景：购物广场，午】  #【黑屏转场】
    play music "audio/bgm/西贝柳斯 云杉.mp3" fadeout 3.0 fadein 3.0 #【bgm：和平】
    show jr normal with dissolve #【立绘：嘉然愤怒】

    "人们回到最初的起点，奉上了他们最虔诚的供品。"
    "他们的眼神里又恢复了那种神采，只不过她试着与其对视之时，又会黯淡下去。"
    "像是突然想起了什么一样，她双手叉腰，声音提高了八度。"

    show jr angery with dissolve #【立绘：嘉然愤怒】
    jr "你们！是！大笨蛋！打架的！都不是！好孩子！"
    "这是她能做出的唯一抗议。"
    "还没来得及担心自己是不是太凶，人群却爆发出海啸般的欢呼。"

    # scene 人群 with dissolve #【背景：人群】
    #【bgm：激动】
    "【一个魂们】"  "嘉然！嘉然骂我！……对对，就是这样！"
    "她干脆一屁股坐在地上，把头别过去以示抗议。"

    show jr normal with dissolve #【立绘：嘉然愤怒】
    jr "他们到底在想什么啊……"
    "小声咕囔着抱怨的语句，却又偷偷瞥了一眼欢呼的人群。"


    "所谓的别过头根本是不可能的，周围环绕的人群让她无死角地处在目光之下，中间留出来的这一片空地，正是一个魂们最向往的舞台。"
    "他们期待着少女能突然站起来，整理一下衣服，微笑着和大家再打一次招呼……"
    "但嘉然完全做不到，换句话说，她也很羡慕那个会唱会跳，和她长得完全一样的女孩。"

    "【一个魂们】"  "对对，不是有那个……对！就是那个！快！快念啊！"
    "嘉然已经习惯了人群的骚动，从之前的恐惧变成了有那么一丁点的小期待。"
    "【一个魂们】"  "……认知性障碍及连续性失忆？"
    "如果说他们最开始的疯狂是一场赌注，那这一刻除了她的所有人都变成了输得精光的穷鬼。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    "嘉然歪着头，努力回想着这几句词。"
    "她听过好几次，医生在她的病床前反复重复着几个奇怪的问题，那些问题在她看来太怪了，太难了。"

    #【立绘：嘉然日常，其余四人悲伤】
    scene 病房
    show jr normal at center
    show bl sad at r4
    show xw sad at r2
    show jl sad at l2
    show nl sad at l4
    show screen memory_filter
    with fade
    "她只记得自己回答不出这些问题之后，其他四人走进病房，脸色很不好看，尤其是晚晚。"
    "她很难过，用力抓住晚晚的手。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "我会好好学的，所以……大家，开心一点好嘛？"

    hide bl with moveoutright
    "最先坚持不住的是贝拉，她一脚把门踹开，只留下了急促的脚步声。"
    hide jl with moveoutright

    "其次是珈乐，张了张嘴又把话憋了回去，狠狠的抽了一下鼻子，决绝的离开了房间，摔上了门。"

    hide xw
    hide nl
    with moveoutright

    "晚晚已经有些脱力了，还是她请求乃琳把她搀出去的。"

    scene 人群
    show jr normal
    hide screen memory_filter
    with fade #【背景：人群】

    "久违的安静再次占领了街道，只不过这次大家都是各怀心思。"
    "总归有人要打破这场平静，哪怕是以放弃尊严为代价。"
    "【一个魂们】"  "……会不会。……这样。……对！可以！……试一试！"
    "少女看着人群重新焕发了活力，心情也好了很多。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "对嘛，大家开心就好，整天哭丧着脸可是会……"

    "【一个魂们】"  "嘉然小姐！您还记得那块半生的鸡胸肉吗？"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "哎？"

    "有别于之前的混乱，大家甚至可以称得上秩序井然。"
    "但是每个人表述的内容却犹如天书。"
    play music "audio/bgm/缓和弦乐.mp3" fadeout 3.0 fadein 3.0 #【bgm：和平】

    "【一个魂A】"  "草莓蛋糕！看啊嘉然小姐，您最爱吃的草莓蛋糕！哦对了，还有，还有，那双草莓袜子到底是谁的？求求您告诉我吧！"
    "这些话拆开来看对于嘉然来说都不难。"

    show jr normal with dissolve #【立绘：嘉然日常】
    jr "嗯……蛋糕好吃……袜子……不知道，然然不知道哪里有那双袜子。"
    "很明显，在把复杂的问题变简单这方面，她一直都是最优秀的。"

    "【一个魂们】"  "……什么啊！浪费时间！快走快走！……我来！"
    "那个人很快就被拖拽回队伍里，速度快到让她还来不及挽留。"

    "地上那块堪称巨大的草莓蛋糕就这么暴露在阳光之下，长时间的日光照射让其有些走形。"

    show jr normal with dissolve #【立绘：嘉然日常】
    "然而一身粉色连衣裙的少女却顾不上这些。"
    "她正在清点着手头的那一把零钱，偶有几张大额钞票。"
    "就在几分钟前，这位面包师从人堆里挤出来，大喊着要她们赔钱。"
    "从他衣服上别的胸针来看，这是一位B-Spirit的忠实粉丝。"
    "少女缓缓站起，小心翼翼的攥紧那一把钞票和硬币，生怕落下一枚。"

    hide jr with dissolve # 【无立绘，仅背景】
    "然而少女已经站定在了对方面前，高高的把手里的\"宝贝\"举起。"
    "面包师脸色阴翳的一把抓过，快速清点起来。"

    show jr panic with dissolve #【立绘：嘉然紧张】
    jr "会不会不够啊……"


    "在清点到最后时，有一张写的歪歪斜斜的字条。"
    "那是嘉然偷偷撕下了地上散落的传单的一角用口红写出来的。"
    "面包师死死地盯着，仿佛要用目光将这张纸条灼烧殆尽。"
    "\"草莓蛋糕真的很好吃，相信您一定会成为世界上最棒的糕点师。\""
    "那些不堪回首的往事，被人全盘否定的痛苦，得意作品被丢进垃圾桶的无力……"
    "所幸，终于等到了一句肯定，哪怕显得有那么些许的稚嫩。"
    "他颤抖着，像是患了风寒，随即便彻底情绪失控，嚎啕大哭。"
    "她隐约还能听见含混不清的\"谢谢\"。"
    "粉色的少女轻拍着他的后背，更像是抚慰着那被流言蜚语而撕裂的伤口。"
    "原本已经走形的蛋糕在柔光的照射下，有了一种别样的美。"


    scene 草莓蛋糕 with dissolve#【背景：草莓蛋糕】
    jr "好可惜……这么大一块大家分一分也好啊。"
    "她缓缓起身，掸了掸裙子上的浮土，径直向那块蛋糕走去。"
    "蛋糕上斜插着一把银质的餐刀，刀柄精致的花纹让谁看了都会认为是一件艺术品。"
    jr "好，那就从这里……嘿！"
    "大家都在目不转睛的看着她的一举一动，等待着奇迹的发生。"

    jr "呼……（小声）一二三四五六七……啊不管了。（大声）大家~一起~吃蛋糕吧！"
    "这句话再度点燃了大家的感情，他们欢呼着，痛哭着，语无伦次着。"
    jr "来~这是你们的~这是你的~呃，这块可能切小了，实在对不起。"
    "没有想象中的一哄而上，她拖着托盘在这个舞台来回走动。"
    "直到嘉然走到另一处后，他们才会窸窸窣窣的走到那块蛋糕前，看了很久才决定由谁来品尝第一口。"
    jr "好了，分完啦！虽然不能人人都有……大家还是要分享一下啊！"
    "阳光之下，俏脸微红的少女满意地看着自己的劳动成果，还有大家认真品尝的神情，语调又变得有神采了起来。"


    "嘉然很希望大家能接过她的蛋糕，对着她说一声谢谢。"
    "然而人们面对着这份礼物却有那么一丝丝的畏惧"
    "这让她不经意的想起了宠物店的仓鼠。"
    "他们很馋，他们很饿。"
    "但是他们不敢。"
    jump .part3

label .part3:

    scene 草莓蛋糕1 with Fade(1,0.5,1) #【黑屏转场】

    #【bgm：调皮】
    "嘉然蹲在地上，打量着大家。"
    "他们吃的狼狈不堪，奶油抹得到处都是。"
    "他们笑又是那么开心，气氛变得无比和谐。"
    "有那么一瞬间，嘉然觉得这样也挺好。"

    scene 人群
    show jr normal
    with dissolve #【立绘：嘉然日常】

    "【一个魂们】"  "嘉然小姐！嘉然小姐！看这里！对，没错！在听吗？"
    "他从人堆里被挤出来，有别于上次那个人带来的大蛋糕，他的手里……只有一部手机？"

    show jr laugh with dissolve #【立绘：嘉然喜悦】

    jr "看的见哦！（招手）"
    "相比较之前的戒备，她现在些许的意识到，这些人也许说的话怪了些，但绝对没有恶意。"
    "没有恶意的人就可以成为朋友，成为嘉然的朋友。"
    "那个人的面部因为极度的兴奋而抽搐了一下，在他们看来，这种良好的开端就是胜利的一半。"
    "谁说他们输得精光？"
    "真正的赌徒就连自己也是一枚筹码。"

    play music "audio/bgm/第二章 悲伤回忆.mp3" fadeout 4.0 fadein 4.0 #【bgm：和平】

    "【一个魂B】"  "咳……嘉然小姐，我是您的一名粉丝……"
    "对方举起手机，清了清嗓子，像朗诵诗篇一样，语调平稳而温和。"
    "【一个魂B】"  "……今天迟到了，因为自行车扎破了胎，老板还不错，看到我摔出个洞的裤子只象征性地罚了五十块钱。"
    "【一个魂B】"  "为了一个项目，客户在酒桌上大施淫威，坐在路灯下，不知是第几次体验到地球自转的滋味……"
    "【一个魂B】"  "我是一名外卖员，但今天的午餐很丰盛，那是我一天都挣不回来的工资。"

    "原本欢快的氛围已然不在，一种不可名状的悲怆重新占领了现场。"
    "从一开始的窃窃私语到现在的鸦雀无声，每个人的头都埋得很低。"
    "这些事不一定发生过，但它一定存在着。"
    "它无形地蹂躏着在场的每个人，体无完肤，遍体鳞伤。"
    "它仗着命运的宠爱而有恃无恐。"
    "存在即合理。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "……呃，大家，不要这样子啊，是我做错了什么嘛？"
    jr "吃了一顿丰盛的午餐……不是挺好的嘛（默念）。"
    "她真的不太理解那些被艺术化的语言，她只知道，在这个故事中，有人喝醉了，有人摔倒了，还有人……吃了一顿很好的午餐？"

    "讲述者看着有些疑惑的嘉然，咬了咬牙，声音又提高了一点。"
    "如果说之前的是诗篇，那现在的就是檄文。"
    show jr at dark

    "【一个魂B】"  "……回到出租屋，绕开几个不算友善的舍友，打开卡到不行的笔记本，试了几下又觉得不妥，只能低声下气地去借了一台好不到哪去的笔记本。"
    "【一个魂B】"  "网速不算太好，因为有人玩游戏。"
    "【一个魂B】"  "看了看手机，晚上九点半。"
    "【一个魂B】"  "咬了咬牙，打开热点，就算不知道那点流量能不能撑住。"
    "【一个魂B】"  "时间过得很快，十点十五，她又加了十五分钟的班，也不知道这傻姑娘什么时候能学学乃琳，准点下班。"
    "【一个魂B】"  "又是赔着笑脸把那台还带着余温的本子送回去，好在对方没有什么意见，只是看我的眼神有些复杂。"
    "【中年大叔】" "中年大叔：年轻人，专心工作才是正事。"
    "【一个魂B】"  "回到床上，裹紧了自己的被子，虽然自己的身边依旧吵闹，鼾声，游戏声，咀嚼食物的噪声……"

    "【一个魂B】"  "我还是睡了过去，因为这些都不是我该管的，更不是我能管的。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "唔……然后呢，第二天就迟到了吗？就像刚才那样，把裤子摔了个大窟窿？"
    "少女挠了挠头，相比较过程，她更想听到结局。"

    "【一个魂B】"  "（苦笑）不，第二天我起的很早。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "为什么啊，你不累吗？是舍友把你吵醒了吗？还是……"
    "叙述者摆了摆手，只有他能回答这个问题，哪怕嘉然猜的是对的。"

    #【bgm：积极】
    "【一个魂B】"  "……因为她昨晚答应给粉丝画一个头像，我梦见了，很好看，所以我就得早点醒过来，看看是不是那样。"

    #【bgm：沮丧】
    "【一个魂B】"  "但我没能看到。"
    "【一个魂B】"  "滚烫的手机把我从半梦半醒的状态拉回现实，明明是百分百的电量却像玩了一晚上游戏一般不堪重负，随时都会爆炸。"
    "【一个魂B】"  "看到移动热点那四个字，心凉了半截。"
    "【一个魂B】"  "好在账单的数目不是很高，也就是两天的饭钱。"
    "【一个魂B】"  "习惯性地扫了一眼屏幕，断开链接的时间是昨晚的十一点半。"
    "【一个魂B】"  "看来这位大叔还真是一个养生爱好者。"

    show jr normal with dissolve #【立绘：嘉然日常】
    # 【bgm：和平】
    jr "嗯，不贵就好哦，要是太贵了那得多心疼啊……"
    "少女认真地回味着刚才的故事，直播，手机，大叔，舍友……这些东西她也接触过。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】

    jr "然然也是哦，每天下午都会看动画片，嘿嘿，有机会你们也去看一下哦，特别开心。"
    jr "舍友嘛，我有四个哦！她们对然然特别好！就是有时候也挺坏的，挠痒痒让然然笑得都快岔气了才停手，哼。"
    jr "还有手机！哎？不过没电了……"
    "这些回答完全是跑题的，但也总比少女坐在那里呆呆地看着海报发呆强。"
    "人们就这么安静地听着她的碎碎念，生怕错过一点信息。"
    jr "对啦，我这里……嘿！哎，太好了，果然还在！"
    "嘴上讲着自己的故事，手却在连衣裙的口袋中不断的摸索着什么。"

    show jr thinking with dissolve #【改立绘-嘉然-思考】
    jr "唔，耳机……小镜子…………一张，两张……"
    "嘉然自己也不知道为什么这个口袋要缝的那么大，大到什么什么奇怪的东西都能塞在里面。"
    "她握不住这些零散的物品，只好又蹲下身，把它们平铺在地上。"
    jr "太好了，就是这个！"

    show jr laugh with dissolve #【改立绘-嘉然-喜悦】
    "那是一颗海蓝色的糖果，包装很简单，只是一张透明的塑料纸。"
    "她来不及多想，直接剥开塞进了嘴里。"
    "海盐味的薄荷糖，原本不堪重负的声带一下子像是得到了解脱一般，又变的跃跃欲试。"
    "纸巾，口罩，暖宝宝，甚至还有一支口红……"
    "嘉然就这么蹲在那里，挨个把玩着，甚至还旋开口红盖子想要试着涂一下。"

    show jr distressed with dissolve #【改立绘-嘉然-苦恼】
    jr "我还小……不能玩这个。不然贝拉会生气的。"
    "一想到这里，她就有些遗憾的摇摇头，慢慢又把盖子扭了回去。"

    show jr laugh with dissolve #【改立绘-嘉然-喜悦】
    jr "好！找齐了！"
    "少女顾不上整理，又把地上的东西胡乱地抓起来塞回口袋，像踩着弹簧一样起身，跌跌撞撞的冲向刚才的一个魂。"
    "距离越来越近，在间隔十步之遥时，少女停止了奔跑。"
    "她知道前几次大家都接受不了这种热情的奔赴，于是这次她决定换一种方式。"
    "再次调动起脸部的肌肉，挤出一个标准的微笑，随后便小心翼翼的摊开了手心。"
    "几张皱巴巴的纸币和银晃晃的硬币攥在嘉然的手心，像是什么无价的宝物。"

    "【一个魂B】"  "这是……"
    "他不太明白他的嘉然小姐为什么会掏出这一把零钱，也想不通这点钱能做什么。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "来！要记得吃早餐哦！很重要的！（走过去）"
    "少女热情洋溢的靠近那名讲故事的人。"


    "而对方却开始全身发抖，和刚才讲故事的状态判若两人。"
    "瞳孔紧缩，心跳加快，双腿止不住的打颤……"
    "终于，在两人还有不到五步的间隔之时，他近乎是连滚带爬的钻回了人群，还伴随着凄厉的嚎叫声。"
    "人群里又传来了谩骂的声音，甚至隐约能看到拳脚相向的架势，原本凄厉的哀嚎这下变成了彻底的惨叫。"

    # scene 购物中心午  with dissolve #【背景：购物广场，午】
    #【bgm：沮丧】
    "带着少女体温的硬币掉落在地上，随即像秋叶一样的纸币紧随其后。"

    show jr sad with dissolve #【立绘：嘉然日常】

    "她只是呆愣着任由手里的钱币掉落，硬币滚了个完美的圆弧最后又落到了她的脚下。"
    "动画片里的内容是假的，世界上根本没有什么汤姆和杰瑞。"
    "换做之前，被这么拒绝的她已经开始闹脾气了，不是蹲下哭就是躺在地上来回打滚。"
    "但她现在完全不想这么做。"
    "她更想凑近一点，抓住他们其中一个人的手，问问他们……"



    show jr normal with dissolve #【立绘：嘉然日常】

    jr "大家觉得……然然是什么样的人啊？"

    "原本人声鼎沸的广场像是退潮一般，最终变成了完全的死寂。"

    "嘉然总觉得这份安静很熟悉。"

    "唯独不同的是空气中还弥漫着草莓蛋糕的甜香。"

    "而不是医用酒精和消毒液的味道。"

    "....."
    scene 病房
    show screen memory_filter # 【房间】【做旧处理】

    with fade #【黑屏】
    play music "audio/bgm/回忆 伤感.mp3" fadeout 4.0 fadein 4.0 #【bgm：和平】

    "……——，又睡不着了吗？"
    "嗯……——姐姐，给我讲个故事吧~"
    "好好好，那就一个哦，要答应我，听完就睡。"
    "好的！拉钩！"
    "拉钩~上吊~一百年~不许变~"
    "好~那就讲一个希腊故事吧……"
    scene black
    hide screen memory_filter
    with fade #【黑屏】

    show screen saying("——，你知道忒修斯之船吗？",0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(2.0)
    hide screen saying with Dissolve(1)
    #【居中排列】
    jump .part4

label .part4:
    scene 人群
    show jr normal
    with fade #【场景：购物中心 午】

    "嘉然有些累了，她干脆又一屁股坐在了地上。"
    jr "呼……好累，（低头看向衣服），对不起啦晚晚，这次我会自己洗的！"

    "经历了之前的那些，人海又恢复了平静的模样。"

    scene black with fade #【黑屏】
     #【BGM：黑暗】
    "不过她错了。"
    "何为湖？何为海？"
    "前者清风徐来，水波不惊。"
    "后者波涛汹涌，暗流涌动。"

    # scene 购物中心午 with fade#【无立绘，仅背景】
    play sound "<from 0 to 10>audio/se/嘈杂的人声.mp3" fadeout 3.0 fadein 3.0 #【BGM：人声喧闹】
    # "（人潮再度拥挤）"
    scene 人群
    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "对了！大家！还没回答我的！问题！"
    "然然！是！什么样子的！"
    "少女的声音已经略带嘶哑，但这并不影响她求知的热情。"


    "石沉大海，没有任何回应。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "不应该啊，大家这是累了吗？"

    hide jr  with dissolve #【无立绘，仅背景】
    "这场闹剧持续了将近半天，大家累了反倒是嘉然能想到的最合理解释。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    #【BGM：阳光】
    jr "大家！可以！说出来！然然！会！认真！听！"

    "嘉然一直都是一个好孩子，她喜欢回答问题，更喜欢回答正确的感觉。"
    play sound "audio/se/窃窃私语.mp3" fadeout 3.0 fadein 3.0#【BGM：窃窃私语】
    "人群出现了骚动……"
    "终于，靠在最前排的一个粉丝颤抖的举起了手。"

    #【人形立绘，无正脸】
    "【一个魂C】"  "嘉、嘉然小姐……能像这样……跳，跳支舞吗？"

    #【无立绘，仅背景】

    "距离有点远，嘉然只好凑近了看，她接过来那块平板时，那个人抖得厉害，就像得了风寒一样。"
    "这回她的经验就很丰富了，二话不说的拿着平板就跑回了之前的地方，最后转过身来，远远的打了一个招呼。"
    "效果很好，那个人一下子就像扎了一针肾上腺素一般，又蹦又跳。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "猫，中，毒？"

    play music "audio/bgm/猫中毒.mp3" fadeout 5.0 fadein 4.0 #【BGM：猫中毒（小声）】

    "她一字一顿的念着，一板一眼的学着。"
    "上面的动作不算复杂，但歌词听不懂。"
    "她试了好多次，就是结结巴巴的，唱不明白。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    #【BGM：激动】
    jr "大家！然然！只跳舞！可以吗！"

    "让别人一直等着是很不礼貌的，尤其是现在还有这么多人。"
    play sound "audio/se/欢呼声.mp3" fadeout 3.0 fadein 3.0 #【BGM：欢呼声】
    "一个魂再度爆发出了欢呼。" with sshake
    "这对于在场的他们来说，是一个里程碑式的胜利。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "开始了哦！"

    #【立绘：嘉然猫中毒mv随意截图动作】

    "少女尽可能的学着屏幕上的动作，像是在玩一场游戏，脚步轻俏，笑容可人。"
    play sound "<from 0 to 10>audio/se/嘈杂的人声.mp3" fadeout 3.0 fadein 3.0 #【插入背景音：人声嘈杂】
    "第一次试跳已经结束了，掌声，欢呼声，笑声，甚至还有哭声。"
    "但是她还是觉得自己能做的更好。"
    "于是，她又默默的开始准备第二次……"

    show jr normal with dissolve #【立绘：嘉然日常】
    jr "一二三四……二二三四，停（小声）。"

    # hide jr  with dissolve #【无立绘，仅背景】
    "这次节奏卡的仍有瑕疵，但对于初学者来说，这已经是相当耀眼的表现了。"
    "如果某人看到了刚才的这一幕，在高兴之余应该会更加严厉的提高以后的训练标准吧。"
    "所以没看见也不一定是件坏事，真的。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "大家！然然这次！有进步嘛！（大声）"

    play sound "audio/se/欢呼声.mp3" fadeout 3.0 fadein 3.0 #【插入背景音：欢呼声】
    "【一个魂们】"  "有！"  with sshake
    "这可能是目前为止众人最异口同声的回答。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    "被夸奖的她有些不好意思地低下了头，本就因为过度运动的俏脸这下更是镀上了一层火红。"

    show jr normal with dissolve #【立绘：嘉然日常】

    play music "audio/bgm/钢琴1 缓和.mp3" fadeout 4.0 fadein 4.0 #【bgm：和平】
    "但她像是突然想起了什么一样，头直勾勾地抬了起来，四处张望着什么。"
    "那种迫切的目光像是锐利的剑一样，细看还能看见樱唇嗫嚅的样子。"
    "没人知道该怎么回答，只能无一例外地低下了头。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】
    jr "……你好？请问……"

    "她又拎着平板跑回了刚才的那个人面前，因为这是她目前唯一一个能找到的\"熟人\"。"
    "这次也刻意保持了间距，不过看上去对方这次的反应已经比之前好太多。"
    "但也顾不上这些了，她实在是等不及了。"

    show jr normal with dissolve #【立绘：嘉然日常】
    jr "有水吗？能给我喝一口吗？"

    #【BGM：激动】
    "等了不一会，人群中就开始滚出无数个瓶子。"
    "当然也不止是水，花花绿绿的包装里装的是不重样的东西。"
    "不少人用力过猛，瓶子滚到了对向的人群，但很快又会以几何式增长的倍率传回来。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    jr "好啦！谢谢！谢谢大家！哎哎哎！别！别扔啦！可以啦！停！"

    "瓶子的数量不减反增，最后几乎可以堆成一个小台子。"

    show jr laugh  with dissolve #【立绘：嘉然喜悦】
    #【BGM：调皮】
    "嘉然一开始还试着把这些瓶子都捡过来，但最后发现这种想法和制止他们一样毫无可行性。"
    "她干脆放弃了，反正也是白费力气。"
    jr "唔……矿泉水……好，就这个了。"

    "就算渴的不行，她还是很认真的来回打量着包装，特别是瓶盖。"
    "这是乃琳教她的，不要随便喝陌生人给的东西。"
    "但是这次是例外。"
    "大家相信她就是那个在舞台上又唱又跳的嘉然。"
    "她也相信大家就是那种可以无话不谈的朋友。"

    show jr normal  with dissolve #【立绘：嘉然日常】
    jr "1，2，3……嘿！"
    #【插入背景音：瓶盖拧开】
    "随着一声脆响，瓶盖应声而开。"
    "少女小心翼翼地尝了一口。"
    "没问题，至少她觉得是没问题。"
    #【插入背景音：喝水声】
    "换做谁来看到这一幕都会惊讶，这么一个小小的姑娘居然在顷刻就喝掉了大半瓶水。"

    show jr normal  with dissolve #【立绘：嘉然日常】


    jr "有点喝不下了……算了，先放一边吧。"
    "少女很仔细的把盖子拧好，蹲下身扒出了一小块空地，把瓶子放在中间。"
    "思来想去还是觉得有点不妥，嘉然突然有了一个好主意。"

    show jr laugh  with dissolve #【立绘：嘉然喜悦】
    "她把那支口红旋出来，在瓶上歪歪斜斜的写上了自己的名字。"
    "人们狂热的眼神近乎快要把这只简单的塑料瓶子融化。"

    show jr angery  with dissolve #【立绘：嘉然生气】
    jr "不可以！然然喝完了才能扔！不能浪费！"
    "她很快就意识到自己的情绪，于是为了缓和所谓的气氛，做出了让步。"

    show jr laugh  with dissolve #【立绘：嘉然喜悦】
    jr "不过你们一直这样乖乖的，一会，一会然然喝完了也可以给你们哦。"


    scene black with fade #【黑屏】

    "在这一瞬间，嘉然又想起了那部动画片。"
    "汤姆和杰瑞也许真的可以在现实世界中握手言和。"
    "就像现在这样。"

    scene 人群
    show jr normal
    with fade #【场景：购物中心 午】
    #【BGM:喧闹】
    "【一个魂们】"  "嘉然小姐！可以跳这个吗……嘉然！看这边！"
    "全场的气温直线升高，人们高呼着偶像的名字，偶像也回应着他们的诉求。"
    "就算偶像对自己的身份不自知。"
    play music "audio/bgm/A-SOUL - Quiet.mp3" fadeout 3.0 fadein 1.0#【BGM:Quiet（小声）】

    show jr normal  with dissolve #【立绘：嘉然日常】
    jr "好好好……呃，就这个吧。"
    "她一眼相中了那个团舞。"
    "没有别的原因，就是因为上面的人长得太像的四个舍友。"
    "还有另一个她。"

    show jr doubt with dissolve #【立绘：嘉然疑惑】

    jr "唔……这样……然后……这样。"
    "少女对着平板认真的练着，丝毫不在意身旁人狂热的目光。"
    "但是，总有些让人措手不及的突发状况。"
    "平板的屏幕越来越暗，最后成功停摆。"

    show jr sad  with dissolve #【立绘：嘉然悲伤】

    play music "audio/bgm/枝江音乐盒.mp3" fadeout 5.0 fadein 3.0  # 【bgm：和平】
    jr "没电了……"

    scene MV截图 with fade #【背景：MV截图】

    "她刚想再去借一个的时候，原本面前那块播放新闻的大屏却突然换了内容。"
    "那正是她要学的团舞。"
    "人群彻底沸腾。"

    play sound "<from 0 to 10>audio/se/嘈杂的人声.mp3" fadeout 3.0 fadein 3.0 # 插入背景音：喧闹
    "如果说之前的是里程碑，那现在几近抵达终点。"
    "没人知道是谁干的，但能确定的一点是，他就在现场，就在默默无闻的一群人之中。"

    show jr laugh at l3  with dissolve#【立绘：嘉然喜悦】
    jr "谢谢！谢谢大家！我一定，一定会跳好的！"
    "这种气氛感染着现场的每一个人，她自己也不例外。"
    jr "Quiet quietquiet ……大脑在牵引（小声）"
    "天赋异禀已经不足以形容这个姑娘，这才看了五遍，她基本就找到了节拍，配合着舞步，已经有了大体的雏形。"

    # scene 购物中心午 with fade #【背景：购物中心 午】
    "人们欣赏着，少女享受着。"
    "就像酸性物质与碳酸钙反应一样，经过剧烈的变化，最终要达成完美的平衡。"


    scene black with fade #【黑屏】
    "但嘉然知道，她一直在等。"
    "……等她们来。"
    jump .part5

label .part5:
    scene 人群晚 with fade #【背景：购物中心 晚】
    show jr laugh
    with fade
    "人们像是不知疲倦般的有说有笑，又蹦又跳。"
    "他们并非感觉不到累。"
    "因为支撑他们狂欢的不是美味佳肴，也不是玉露琼浆。"
    "只是站在中央的那个少女。"

    show jr happy with dissolve #【立绘：嘉然喜悦】

    play music "audio/bgm/A-SOUL - 超级敏感 (伴奏).mp3" fadeout 3.0 fadein 3.0 #【BGM：超级敏感（伴奏）】
    jr "大家！然然这次！有进步嘛！（挥手）"

    scene 人群晚  with dissolve#【背景：人群举着手机，星星点点】
    "【一个魂们】"  "嘉然！嘉然！嘉然！（齐声）" with sshake #【文本框震动】
    "每个人的手上都有着发光的东西。"
    "她觉得这个场景很像贝拉在病房里前几天给她讲的神话故事。"
    "……五光十色，形状各异。"
    "……明晦共生，九九归一。"

    show jr angery with dissolve #【立绘：嘉然生气】
    #【bgm：调皮】
    jr "有没有进步！！"
    "换做是之前的嘉然是绝对不会这么做的。"
    "但现在她摸到了窍门，情绪越高昂，整体的氛围也会变得水涨船高。"

    scene 人群晚  #【背景：人群举着手机，星星点点】
    "【一个魂们】"  "有！！（齐声）" with sshake #【文本框震动】
    "有别于之前的窃窃私语和七嘴八舌，现在的他们完全是整齐划一，分毫不差。"

    show jr smile with dissolve#【立绘：嘉然喜悦/wink】
    jr "有就好哦！然然！最喜欢！大家了！"

    scene 人群晚  #【背景：人群举着手机，星星点点】
    # 【BGM:喧闹】
    "【一个魂们】"  "……哦哦哦！……嘉然！我的嘉然！嘉然小姐你是我的光！" with sshake #【文本框抖动】
    "少女不经意的一个wink更是引爆了全场，这场面，比当年她们巡演的场景有过之而无不及。"
    "毕竟一天吃一顿和一个周吃一顿的差距还是不小的。"
    "更何况这些像饕餮一般的粉丝。"

    "所有的努力在这一刻得到了回报，所有的不快都被扔到了九霄云外。"
    "从今往后，这里就是歌珊地，这里就是伊甸园。"



    "但很快，外来的不速之客就让他们措手不及。"
    "兵来将挡，水来土掩。"
    "谁来闹事就打回去，谁来劝阻就骂回去。这种堪称万金油的做法在之前屡试不爽。"
    "但很明显，来的这四位明显不能用这一套，也不吃这一套。"
    "人群缓缓地又拉开一个口子，她们就这么势不可挡的走进了圈子。"

    show bl angery  with dissolve #【立绘：贝拉愤怒】
    play music "audio/bgm/纷乱 第三章.mp3" fadeout 4.0 fadein 4.0 #【BGM：超级敏感（伴奏）】

    bl "好你个嘉然……跳的挺开心啊。"
    "作为队长的她看着不远处刚刚停下动作的粉色小矮子，眉眼里就差喷出火。"
    hide bl  with dissolve

    show nl smile  with dissolve #【立绘：乃琳微笑】
    nl "哎，队长，然然这样说不定也算是记起来什么了呢~"
    "堪称妖媚的女子抿嘴轻笑，手里拎着一只坤包的她眼神却一直和嘉然对视，试图想挖出点什么东西。"
    hide nl  with dissolve

    show jl angery  with dissolve #【立绘：珈乐不爽】
    jl "这算什么？私人演唱会？"
    "紫发少女环顾了下四周，有些不满的挑了挑眉毛，显然是对整件事情很不爽，但处于大庭广众之下，又只能揶揄这靠瓶子堆成的隔离带。"
    hide jl  with dissolve

    show xw happy with dissolve #【立绘：向晚喜悦】
    xw "来，然然，到这边来，大家都很想你哦。"
    "她做出了个拥抱的姿势，更像是在呼唤一只走丢的小猫。"
    hide xw  with dissolve


    show jr laugh with dissolve#【立绘：嘉然喜悦】
    #【bgm：激动】
    jr "我！来！啦！"
    "少女像一颗出膛的子弹一般，直直的射向面前的目标。"
    hide jr  with dissolve

    show bl angery with dissolve #【立绘：贝拉愤怒】
    "贝拉特别想让这个冒失鬼长长记性，但一边的乃琳早已在她打算动手之前凑到嘉然身边嘘寒问暖。"
    hide bl  with dissolve

    show bl angery at l2 #【立绘：贝拉愤怒+乃琳微笑】
    show nl smile at r2
    with dissolve

    "最气人的是，她还趁着回头拿纸巾的功夫，毫不掩饰的抛了个媚眼，完完全全是一副计划得逞的样子。"
    hide bl
    hide nl
    with dissolve

    show jl angery with dissolve#【立绘：珈乐不爽】
    "紫发少女没心思看这无聊的一幕，环顾了一圈之后，看到三人还在继续，她直接了当的冲贝拉摇头、摊手，最后还长叹了一口气。"
    hide jl  with dissolve

    show bl angery at l2 #【立绘：贝拉愤怒+珈乐日常】
    show jl normal at r2
    with dissolve
    "贝拉脸色阴沉的盯着珈乐，但就算这样，对方仍然是我行我素，丝毫没有改观。"
    jl "好啦，队长，她们三个就这个德行，也没必要这么认真……喏，喝点。"
    "她挑了好久才找到两瓶矿泉水，递了一瓶过去。"


    show bl sad at l1 #【立绘：贝拉悲伤+珈乐日常（凑近）】
    show jl normal at r1
    with dissolve

    "就在二人接触的时候，贝拉却一把抱住珈乐，哭的比嘉然还像个孩子。"
 
    jl "安啦，都找到了，都找到了哦，不哭了。"
    "少女在安抚下渐渐平稳了情绪，但还是死死抱着珈乐不愿离开。"
    jl "这么多人呢，多不好……"

    show bl serious with dissolve#【立绘：贝拉认真】
    bl "……搞不定？（小声）"

    show jl normal with dissolve #【立绘：珈乐日常】
    jl "好啦~别闹脾气了~"
    "珈乐双手搭肩，直直的把贝拉扶起。"
    "事实上要是贝拉不配合，以她的力气，两个珈乐也不够看。"

    show bl normal with dissolve #【立绘：贝拉日常】
    #【bgm：激动】
    bl "好！A-SOUL的各位，集合！"
    "其余两人听到了这句口号，迅速地交换了一下眼神。"
    "人太多了，就算珈乐是一辆跑车，那也是在没有阻挡的路面上才能发挥出实力。"
    "但她们还有第二手打算。"
    hide bl 
    hide jl
    show xw normal
    with dissolve

    xw "来，然然，咱们再跳一次舞好吗？"
    hide xw with dissolve


    play music "audio/bgm/A-SOUL - 超级敏感.mp3" fadeout 3.0 fadein 5.0 #【BGM:超级敏感（正常）】

    show jr laugh with dissolve#【立绘：嘉然喜悦】
    jr "好！等一下哦，然然让大家放个音乐！"
    "一旁的人们从头到尾见证着这一切，他们甚至不敢相信他们的眼睛，更不敢相信他们的运气。"
    "已经消失一周的偶像们即将在他们面前又唱又跳，这是何等美妙的享受。"
    hide jr with dissolve

    scene MV截图 with fade #【背景：MV截图】

    "因为嘉然的不熟练，很多地方显得不是那么整齐。"
    "甚至贝拉都被带跑偏了一次，站错了位置。"
    "但随后的一幕让在场的所有人再一次陷入了理智清零的混乱之中。"

    # show bl serious with dissolve #【立绘：贝拉认真】
    bl "好……就是现在（默念）"
    "借着舞步转身发力，她脚边的一个水瓶被她狠狠的踹了出去。"
    "一道完美的抛物线后，落进了人群中。"
    "绝大部分人没有注意到这件事情。"
    "但捡到瓶子的人可淡定不起来了。"
    "那个矿泉水瓶上歪歪斜斜的用口红写着嘉然二字，隐约还能闻到樱花的香气。"

    scene 人群晚 with fade#【背景：人群举着手机，星星点点】
    "【一个魂们】"  "给我！……是我的！……让开！"
    "局面开始失控，就像一块泡腾片丢进水里一样，在短暂的延迟后产生了剧烈的反应。"
    "【一个魂们】"  "……那是什么？是瓶子！是嘉然小姐的瓶子！"
    "人们闻风而动，原本坚不可摧的包围圈现在的某些地方已经薄弱不堪，甚至干脆出现了缺口。"

    show bl serious with dissolve#【立绘：贝拉严肃】

    bl "珈乐！"
    hide bl with dissolve

    show jl angery at l1  #【立绘：珈乐严肃】
    show jr doubt at r1  #【立绘：嘉然疑惑】
    with dissolve
    jl "明白！嘉然！给我搂紧了！"
    "还没等嘉然反应过来，她就被向晚一把推给了一旁急不可待的紫发少女。"
    "随后她就被扔到了背上，耳边只传来呼呼的风声。"
    "出乎意料的顺利，她几乎没费多少力就撤了出来。"
    hide jl
    hide jr
    with dissolve

    show jl happy  with dissolve #【立绘：珈乐喜悦】
    jl "贝……唔！"
    "刚喊出第一个字就后悔了。"
    "就算瓶子的吸引力再大，也总是有盯着她们的人。"

    play music "audio/bgm/诡异.mp3" fadeout 4.0 fadein 4.0

    show jl firm with dissolve
    "她暴露了。"

    scene 人群晚 with dissolve #【背景：人群】
    play sound "<from 0 to 10>audio/se/嘈杂的人声.mp3" fadeout 3.0 fadein 3.0 #【bgm：人声嘈杂】
    "【一个魂们】"  "不能走！……珈乐小姐！……乃琳小姐！我的嘉然小姐呢！！"
    "包围圈重新合拢，这次的情况甚至更糟。"
    "被迫分成了两个阵地，8字形的人群将她们隔开。"
    "人们七嘴八舌的讨论着怎么将她们留下来，甚至还有人喊着不知所云的口号。"

    show bl serious  with dissolve#【立绘：贝拉严肃】
    bl "……乃琳。"
    hide bl with dissolve

    show nl frown  with dissolve #【立绘：乃琳严肃】
    nl "我觉得……有点难？"
    hide nl with dissolve

    show xw gloomy with dissolve#【立绘：向晚失落】
    xw "拉姐，我们好像是被……"
    hide xw with dissolve

    "再度合流的情形很清晰的道出了一个事实，哪怕三人都不想去承认。"
    "她们又被包围了。"
    "提高了警惕的人们自然不会像上次一样那么好骗，更何况他们的目标从头至尾也只是这五位少女。"
    "正当三人背靠背筹划着下一步的计划时，原本喧闹的人群却突然变得死寂，还没等她们反应过来，道路却再一次打开。"

    show bl shock at center
    show xw shock at l2
    show nl shock at r2
    with dissolve
    #【三人立绘：吃惊】
    "三人：什么？"
    hide bl
    hide xw
    hide nl
    with Fade(1,0.5,1) #【黑屏转场】

    show jl angery at l2 #【立绘：珈乐严肃，嘉然喜悦】
    show jr laugh at r2
    with dissolve
    #【bgm：沮丧】
    jl "这下糟了……"
    "完全看不到有什么突破的可能性。"
    "她现在只想狠狠地抽自己，为什么非要喊那一下。"
    "明明队友也不是瞎子，现在她们应该在前往目的地的路上。"
    jr "珈乐，珈乐？别这样嘛，一直哭丧个脸是会长皱纹的……"
    "随后少女就注意到了人们的骚动。"
    "他们正在等待着嘉然小姐的下个指令。"
    hide jl
    hide jr
    with dissolve

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    play music "audio/bgm/梦幻曲 德彪西.mp3" fadeout 3.0 fadein 2.0

    jr "珈乐你不要担心，然然一定把大家带出去！（小声）"
    "嘉然趴在她的耳边，呼出来的热气弄得她怪痒的。"
    hide jr with dissolve

    show jl angery with dissolve #【立绘：珈乐不爽】
    jl "切……痴人说梦。"
    "少女听完，不禁嗤之以鼻。"
    hide jl with dissolve

    show jr angery with dissolve #【立绘：嘉然愤怒】
    jr "你看好吧！哼！然然从不撒谎！"
    "嘉然缓缓起身，清了清嗓子。"

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    # 【bgm：调皮】


    jr "大家，然然要走了哦。（挥手）"
    "大家都是朋友，都是无话不说的朋友，嘉然相信他们。"


    "没有回应，很明显没人愿意接受这个现实。"

    show jr normal with dissolve #【立绘：嘉然日常】

    jr "大家是然然很棒的朋友，她们也是。"
    "但是她仍然自顾自的说着。"
    jr "然然今天玩的很开心……虽然不知道大家说的A-SOUL是什么，但是她们应该也会在世界的某一个角落，唱着歌，跳着舞，发着光……"
    "少女的眼神里是掩饰不住的羡慕，殊不知，她正是其中的一员，之前是，现在是，以后也会是。"

    show jr sad with dissolve #【立绘：嘉然悲伤】

    jr "大家！可以给然然一个机会吗！（大声）"
    "少女的泪水夺目而出，刚才还嘻嘻哈哈的她瞬间变成了泪人。"


    "【一个魂们】"  "……嘉然小姐！别哭啊我的嘉然！……嘉然！"
    "人群再度骚动，他们想不通会有什么伤心事会让她哭成这样。"

    show jr sad with dissolve #【立绘：嘉然悲伤】
    jr "可……可不可以嘛！呜……"
    "一个魂们经过了极为短暂的反应后，得出了统一的结论。"


    "【一个魂们】"  "可以！" with sshake  #【文本框震动】

    show jr laugh with dissolve #【立绘：嘉然喜悦】
    #【bgm：积极】
    jr "然然……会努力的！直到比上视频里那五个人的那一天！"
    "……然然会回来的！然然保证！"
    "少女胡乱抹去泪水，衣袖已经脏的不像样子。"
    "但她的笑容还是那么温暖。"
    "以至于能融化坚冰。"



    show jr doubt #【立绘：嘉然疑惑】
    with Fade(1,0.5,1) #【黑屏转场】


    "少女懵懂的看着沉默的人群，她期待着一个结果。"
    "一秒，两秒……时间从未如此漫长。"

    play music "audio/bgm/b站女声圣咏.mp3" fadeout 3.0 fadein 2.0#【bgm：神圣】

    scene 分海CG with Dissolve(4)
    "就在她有些失望的时候，人潮开始缓缓分裂。"
    "春已至，冬将逝。"
    "迎面看到的正是一脸惊讶的三人。"

    jl "真有她的……"

    "人群已经逐渐散去，就像一块块浮冰一样。"
    "来的一厢情愿，散的心甘情愿。"

    scene 购物中心晚
    with Dissolve(6) #【背景：购物中心 晚】

    stop music fadeout 8.0

    "为约定而来，依约定而散。"
    "他们三五成群走在路上，回想着今天的一切。"
    "明天也应该是太阳照常升起的一天。"

    scene black with Dissolve(3)

    jump .cp7_n_end

label .cp7_n_end:
    #########
    # 内容：

    jump cp8_n_start


init python:
    declare('道路 早', 'bg/8 5道路(早).png', 'p')
    declare('枝江街头', 'bg/1 2枝江街头.png', 'p')
    declare('病房', 'bg/0 3 病房.png', 'p')
    declare('MV截图', 'bg/8 MV截图.png', 'p')
    declare('人群', 'bg/8 人群.png', 'p')
    declare('人群晚', 'bg/8 人群(晚).png', 'p')
