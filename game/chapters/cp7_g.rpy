label cp7_g_start:
    $ save.cp_name = "第七章"
    window hide
    scene black
    show screen saying("无知之人扑了个空，娇小的少女踏上归途。" ,0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(4.0)
    hide screen saying with Dissolve(2)
    if not config.developer:
        show screen unskip
        $ _skipping = False
    $ renpy.movie_cutscene("audio/video/start/7.2.webm")
    if not config.developer:
        hide screen unskip
        $ _skipping = True


    jump .part1

label .part1:
    #bgm 紧张的音乐


    play music "audio/bgm/悬疑.mp3" fadeout 2.0 fadein 3.0
    #场景 黄昏 医院内
    scene 黄昏医院 with dissolve
    window show

    "【粉丝A】" "老实交代！然然去哪了！"

    "【医生】" "对不起，我们真的不知道。嘉然小姐今天中午就已经出院了。"

    "【粉丝B】" "你要是敢骗我们，就等着你这破医院被干碎吧！"
    "【粉丝B】" "走！我们去别的地方找！"

    #场景 黄昏 街道上

    scene 黄昏街头 with fade
    #bgm 紧张的音乐
    #声音 【嘈杂的人声】
    play sound "/audio/se/难分辨的人群噪音-2.mp3"
    "【粉丝C】" "什么？全都不见了？"

    "【粉丝D】" "医院那边说然然中午就走了，剩下四个人也不知道去哪。直播间里一个人都没有！"

    "【粉丝C】" "该死！沿着街道继续找！枝江市就这么大，不可能找不到她们。"
    "【粉丝C】" "一定要找到她们，一定要找到！"

    #声音 【嘈杂的人声（渐弱处理）】
    play sound "/audio/se/难分辨的人群噪音-2.mp3" fadeout 1.5
    stop sound
    #声音 【灌木丛摇晃的声音】

    #【贝拉的立绘亮】
    #【向晚的立绘亮】
    #【乃琳的立绘亮】
    #【珈乐的立绘亮】
    show bl frown at r3
    show xw worried at r1
    show jl helpless at l1
    show nl normal at l3
    with dissolve
    #立绘 贝拉（皱眉）：
    bl "走了吗？"

    #立绘 向晚（皱眉）：
    xw "应该走了，他们沿着街道往北边去了。"
    show bl soft at r3 with dissolve
    #立绘 贝拉（微笑）：
    bl "多亏了你，晚晚，如果我们不及时转移的话，恐怕已经被找到了。"

    #立绘 珈乐（皱眉）：
    jl "晚晚，嘉然一个人在病房里，不会有问题吧。万一医院的人……"

    #立绘 乃琳（日常）：
    nl "放心好了，那个地方即便是医院也不知道，是枝江市高层专门为特工准备的。"
    nl "里面的机关我都处理好了，整个病房都在我的监控之下。"
    show xw normal at r1 with dissolve
    #立绘 向晚（日常）：
    xw "谢谢你，乃琳。"
    xw "我们再等一会，确保万无一失。"
    xw "你们先去枝江大厦等我，千万不要暴露行踪。我去接嘉然，马上就和你们汇合。"
    xw "一定注意安全，晚上见。"

    #转场 黑屏
    scene black
    #场景 居中于屏幕，白字：
    show screen saying("按照乃琳的指示带着大家躲开了粉丝的袭击后\n向晚让其他三人先去枝江大厦准备，自己则一个人来到了嘉然的病房……",0.5,0.4,30) with Dissolve(3)
    $ renpy.pause(2.0, hard=True)
    hide screen saying with Dissolve(1)

    #"bgm 抒情"
    play music "audio/bgm/伤感双吉他.mp3" fadeout 2.0 fadein 2.0
    #"场景 黄昏 病房内"
    show 黄昏病房
    show jr normal at l2
    with dissolve
    "雪白的被褥因为太长而从床沿边一泻而下，像裙摆一样轻飘飘地挂着。"

    "黄昏时分，金色的光从窗口探出头来，穿着粉色衣服的少女躺在床上，双眼在窗外的景色上流连。"

    "她在想什么？她精致的脸蛋被空白的感情所填满。"

    "风吹过树梢，夕阳在山峦上沉默地漂浮，这一切能否在她心中激起几丝涟漪呢？"

    "这时，一直紧闭的门毫无征兆地被推了开来。"

    #"此处应是开门声"
    play sound "/audio/se/开门声.mp3"

    show xw sad at r2 with easeinright

    "来人的动作很温柔，没有发出太大声音，但嘉然依然如受惊的鸟一样迅速地看了过去。"

    "是向晚——她和嘉然四目相对后，一言不发地走向她的病床边。"

    "向晚的到来让房间里多了些生气，她留下的影子和呼出的空气，活跃了几乎要凝固在时间中的病房。"

    "嘉然的嘴角泛起笑意，她满眼都是那淡紫色的少女的身姿"

    #【嘉然的立绘亮】
    #立绘 嘉然日常

    jr "晚晚，你来了。"
    jr "我一个人在这里呆着觉得有些无聊，幸好你来了。"
    jr "晚晚？你怎么了？是有什么心事吗？"

    #【嘉然的立绘暗】
    "向晚没有说话，她坐在嘉然的左边，双手紧握。"
    "她低着头，头发如紫罗兰色的瀑布般滑落，遮住了她的脸。"
    "嘉然看不见的是，向晚正死死咬着自己的嘴唇，试图不让自己发出声音。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    # 立绘 向晚 不安
    show jr smile at l2
    show xw worried at r2 
    with dissolve

    xw "然然……你现在……身体感觉怎么样了？"

    #立绘 嘉然 喜悦
    jr "感觉好多啦！我觉得自己现在已经可以下地走路了，说不定还能小跑呢！"
    jr "虽然脑子还是晕晕乎乎的，但是，身体重新恢复力气以后，我感觉……"
    jr "感觉……多了信心吧！所以晚晚，你也要打起精神哦。"

    #立绘 向晚（不安）：
    xw "嗯……我会的……然然"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "听到嘉然那恢复了往日元气的声音，向晚心中的悲痛仿佛得到了某种召唤，如滔天巨浪般再一次涌起，不断地拍打她勉强铸成的心理防线。"

    #【嘉然的立绘亮】
    #立绘 嘉然 疑惑
    show jr panic at l2
    show xw sad at r2 
    with dissolve
    jr "晚晚，你……"
    #【嘉然的立绘暗】
    "竭尽全力控制自己情绪的向晚，没有注意到已经从床上撑起身子的嘉然。"
    "嘉然前倾着身子，轻轻地用手拨开了挡着向晚的脸庞的发丝。"
    "那一瞬间，向晚的眼前再一次被金色的夕阳光芒所充满，紧张之下，她一边试图捂住自己的脸，一边看向嘉然。"
    "但为时已晚，她泛红的眼眶和泫然欲泣的神色已被嘉然尽收眼底，嘉然那悬在空中的手像蜂鸟般顿了一下，随后搭在了向晚那欲盖弥彰的手上。"

    #【嘉然的立绘亮】
    show jr laugh at l2 with dissolve
    show xw sad at r2 with dissolve
    #立绘 嘉然 微笑
    jr "晚晚，没事的，不要自己一个人撑着。有什么事告诉我就好了，我会好好听的。你说的每句话，我都会听的，晚晚。"
    jr "你看，眼睛都肿了。"
    #【嘉然的立绘暗】
    "说着，嘉然的手自然而然地攀上了向晚的面庞。她的手白皙而修长，微微发亮，却柔软的好似一朵云。向晚没有说话，她睁大的双眼好像在看嘉然，又好像朝着嘉然身后无限延伸的地平线。"
    show jr laugh at l1 with move
    "嘉然温柔地用指肚拂过向晚那微微红肿的眼角——微微有些湿润的触感，让嘉然的动作突然停了下来。"
    "温柔的少女有些不好意思地收回了手，随后，又害怕自己这有些应激的行为会让向晚误会，手忙脚乱地解释了起来。"

    #【嘉然的立绘亮】
    show jr panic at l1 with dissolve
    show xw sad at r2 with dissolve
    #立绘 嘉然（惊慌）：
    jr "抱歉！向晚，我也不知道怎么回事，突然就……突然就，很想摸摸你的眼睛，和你的脸……感觉你像要哭了一样，我想着这样会不会让你感觉好一些……"
    jr "我弄痛你了吗？晚晚？"

    scene 嘉然晚晚 with sshake

    "向晚没有回答，随后，她一把将眼前的少女搂入怀中，放声大哭起来。"
    
    #画面震动特效
    #声音 【布料摩擦的声音】
    play sound "<from 4 to 12>/audio/se/衣服布料声.mp3"
    #【向晚的立绘亮】
    #立绘 向晚 哭泣
    xw "呜啊啊啊啊，然然……然然……我……我又……又搞砸了。"
    xw "我已经试了那么多次，我把所有能做的都做了，我把……我把所有该完成的都完成了……为什么还会这样啊……呜啊啊……"
    xw "然然……对不起……"

    #【向晚的立绘暗】
    "眼泪一滴一滴地落在被褥和嘉然的衣服上，化作深色的水渍。嘉然也抱住了向晚，有些瘦削的身躯在她的怀中抽搐着，从手心感受传来的温度，嘉然轻轻地拍了向晚。"

    #【嘉然的立绘亮】
    #立绘 嘉然（微笑）：
    jr "没事的，晚晚，你做的很棒，你已经很厉害了。一切还没结束呢，你一定可以的。"
    jr "晚晚，不是你的错哦。我想，自己一定是忘了很重要很重要的事情，你才会这么伤心的。所以我一定会想起来的，晚晚，我一定不会全部忘记的。"
    jr "晚晚，我在这里呢，我会一直陪着你的。我答应你哦，我会陪着你的。想听我为你唱一首歌吗？"
    #jr "“到人间冒险，让结局变个样……”" 歌词待定

    #【嘉然的立绘暗】
    "歌声久久地萦绕在病房之中，向晚轻轻地抽泣着，她与嘉然的心跳正轻轻地回应着彼此。久久没有松开的拥抱，让两人的心与心，灵魂与灵魂以最为靠近的方式紧紧贴在了一起。"
    "像水从河流流入海洋般自然，嘉然如触摸一颗晶莹剔透的水晶般，触碰着向晚剔透的心。她忘了很多事情，但有些不能被忘记的东西已经刻入了身体之中。"
    "烈日烧干了海，却不能烧掉地图上的蓝；失忆带走了嘉然的记忆，却未能带走化为习惯的爱与温柔。"
    "一曲唱完，嘉然轻轻抚摸向晚的头，发丝柔顺而温驯，像听话的波浪般从手中掠过，留下一丝撒娇般的触感。"

    #【嘉然的立绘亮】
    scene 黄昏病房
    show jr laugh at l1 
    show xw angery at r2 
    with Dissolve(2)
    #立绘 嘉然（微笑）：
    jr "晚晚，我不会忘的哦。你看，我还记得这首歌。晚晚，你已经做的很棒了。"

    #立绘 向晚（愤怒）：
    xw "还不够……然然，还不够。还不够！"
    xw "然然，我做的一切还远远不够……所以我一定要……我一定要。"
    xw "一定要带你走……"
    xw "然然……我答应你。"
    xw "我答应你，我会拼尽全力——再一次。我要带你走。"
    xw "我会带你去一个没有人能伤害你的地方。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "向晚双手搭在嘉然的肩膀上，缓缓将她推开，嘉然没有抵抗，任由向晚松开了这长久的拥抱。"
    show xw sad with dissolve
    with Pause(0.75)
    show xw angery with dissolve

    "向晚那双紫色的眸子直直地看向嘉然，泪水的痕迹还未消退，夕阳下，隐约的泪珠闪烁着细碎的光。但那眸子里已被坚定的决心所充满。"
    "嘉然笑着，握住了向晚的手。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr smile at l1 with dissolve
    show xw worried at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "晚晚，我相信你。但你很累了，你得好好休息休息，不然会伤到自己的。"
    jr "对了！来，晚晚，扶我一下。"


    #立绘 向晚（惊讶）：
    xw "你要干什么？"

    #立绘 嘉然（日常）：
    jr "我要下床，带你去一个地方。"
    show jr happy at l1 with dissolve
    jr "嘿咻！你看，晚晚！我就说吧，我现在已经恢复体力了，自己行走已经完全不是问题。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    "嘉然的身体显现出了稍有些异样的活力，她撑着床沿和向晚的手，从容地走下了床。站在向晚的旁边，她面带着微笑，拉着向晚的手带她站了起来。"
    "嘉然稍稍有些娇小的身子过了许久以后，再一次以直立的姿态站在了向晚的面前，无论是服装还是那一如既往的温柔神态，都恰似之前的模样。"
    "不知何处袭来的既视感让向晚的胸口抽痛了一下，但她迅速压下了胸中的情感，握住了嘉然的手。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw worried at r2 with dissolve
    show jr laugh at l1 with dissolve
    #立绘 向晚（不安）：
    xw "然然，你不要太勉强。你的身子现在还有些虚弱，最好再……"

    #立绘 嘉然（微笑）：
    jr "晚晚，没事。"
    jr "让我带你去吧，让我带你去我长大，我曾经生活过的地方。"
    jr "晚晚，不用为我担心，我的身体已经恢复了。那个地方，我一定要亲自领着你去。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "嘉然那坚定的语气让向晚有些意外，她从嘉然的眼中看到了不容置否的坚决神色。"
    "向晚的心被强烈地动摇了，她心底被埋藏的最深的那些情绪此时此刻又一次被嘉然的神态所缓缓地搅动起来。"
    "有那么一瞬间，向晚觉得自己似乎已经忘了此时此刻该摆出哪种表情——好像一切都那么远，远到那些温柔岁月里的细节都被忘记了"
    "但下一刻，她就想起来了。"
    "她轻轻地微笑着，任由嘉然领着自己，走出病房，走出医院，走向了那金光璀璨的街道。"
    jump .part2

label .part2:
    #bgm 抒情的音乐
    play music "audio/bgm/抒情双吉他.mp3" fadeout 1.0 fadein 3.0
    #场景 黄昏 林荫小道

    scene 黄昏林荫小道 with fade
    play sound "/audio/se/虫鸣鸟叫声.mp3"
    "这正是一天中最短暂，却也最令人心醉的时刻。月亮飘在一片湛蓝的光上，跟着时钟的指针一步一步地，将那些消失了一整天的星光重新点在降温的天穹上。"
    "树与灌木在温柔的光中俯首称臣，叶片的边缘好像吸饱了水分，散发着神秘的光晕，如浮在光之海洋中的小船。"
    "熟悉的街道和楼房顺着嘉然和向晚的视线铺就开来，它们一言不发，好像已经在这里存在了许久，又好像它们刚刚因嘉然的心意一动，而存乎于此处一般。"
    "如梦似幻的氛围里，嘉然拉着向晚的手，向金光闪烁的方向走去。"
    "月亮和群星忠诚地在她们身后，扶起如裙摆般险些滑落到地上的身影。"
    "十分钟后，嘉然带着向晚沿着一条被灌木所包住的小径，离开了大路。"
    "小径是人们踩出来的，坚硬的表面被隐约的龟裂所覆盖。"
    "在一片翠绿的草坪上，土黄色的它显得有些突兀。"
    "小径的两侧，是数不清的法国梧桐。宽厚的树叶遮住了炫目的金光，沐浴在树荫里，向晚觉得皮肤稍稍发凉"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    #声音 【杂乱的脚步声】
    play sound "/audio/se/清脆的脚步声-3.mp3"
    show xw gloomy at l2
    show jr normal at r2
    with dissolve
    #立绘 向晚（疑惑）：
    xw "然然，我们这是在往哪走啊？"

    #立绘 嘉然（日常）：
    jr "马上到了，到时候你就知道啦。"
    jr "那里是我从小长大的地方，说起来，我已经有一阵子没去过那里了。"
    jr "许久不见，我还有点小紧张呢。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "嘉然虽然这么说着，脚步上却没有丝毫的踌躇。她坚定地领着向晚往前走去，穿过那些因为久疏修剪而狂野生长的灌木，似乎有一栋建筑在树叶的缝隙间闪烁着神秘的光"
    "向晚没有说话，她感到自己的胸口仿佛浸入了海水般一样，猝然被某种重物所压上。"
    "隐隐约约的预感，让她感到轻微的呼吸困难。她于是握紧了嘉然的手，看不见的风暴席卷整座城市，她觉得唯独眼前的少女是可靠的山峦。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr smile with dissolve
    # 立绘 嘉然（喜悦）：
    jr "看，晚晚，我们到了。"
    # 转场 【白光淡出】


    jump .part3

label .part3:
    #bgm 抒情的音乐
    #场景 黄昏 教堂外

    #with Fade(1,0.5,1)
    #转场 【白光淡入】

    "嘉然突然停下了脚步，这时，向晚才从自己的想法中回过神来。"
    "失去了法国梧桐的遮蔽，金光再一次一泄而下。向晚用手挡住自己的双眼，这才意识到自己已经离开了那阴凉的小径。"
    with flash_in
    show jtw_1
    "她抬起头来，顺着嘉然的手指望去。"
    "只见在一片仿若森林般茂盛而宁静的树丛之间，有着一块圆形的空地。在那之上，一座造型颇为复古的古希腊式教堂正巍然耸立着。"
    scene 黄昏教堂外 with dissolve
    "第一眼，向晚甚至没有意识到这是一座教堂。那建筑物看起来太过朴素，仿佛在一座中世纪风格的小屋上额外增加了穹顶和尖塔一样。"
    "不同于巴洛克或是哥特风格，古希腊式的教堂似乎自带这岁月与时间的痕迹。"
    "它幸运或是不幸地免于被当下的现代性进行无情的解构和重构，这让时间未能如它所愿地侵蚀并改造这一古老的形态。"
    "向晚看到造型优雅的浮雕描绘着葡萄藤，动物，和神明的符号。那些浮雕年久失修，但不失其原本的美。"
    "与世间万物一起，这座古老的教堂正沉醉在夕阳之中。嘉然怜爱地看着这座教堂，轻轻地走了过去。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr laugh at r2
    show xw proud at l2
    with dissolve
    #立绘 嘉然（微笑）：
    jr "晚晚，欢迎你来到这里。这里就是我的家。"
    jr "来，走近些，晚晚。"

    #立绘 向晚（惊讶）：
    xw "然然，这真的……是一座教堂吗？它看起来……"

    #立绘 嘉然（日常）：
    jr "你是觉得它不像一座教堂吗？哈哈，以前我也这么觉得。"
    jr "我长大了以后，见到了其他类型的教堂，都比这座教堂要气派，要宏伟。"
    jr "但是这座教堂也有自己的美，向晚，它叫圣母教堂，据说还有一个很好听的英文名。"
    jr "可惜的是，老修女没有告诉我。也许她忘了吧。"
    jr "可以的话，我还真的想再问问她叫什么名字呢……"
    show jr haggard at r2 with dissolve
    jr "啊！抱歉！我触景生情，一不小心就说了太多了。"
    show xw normal at l2 with dissolve
    #立绘 向晚（微笑）：
    xw "没事的，然然，我愿意听你说这些。"
    xw "那我们要进去吗？这里可以进去吗？"
    show jr smile at r2 with dissolve
    #立绘 嘉然（喜悦）：
    jr "当然要进去啦！那里面很好看，而且有好多有意思的东西。"
    jr "跟我来，晚晚。不要担心，这个时间教堂里没有人。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    # 声音 【杂乱的脚步声】
    play sound "/audio/se/清脆的脚步声-3.mp3"
    "说着，嘉然握住向晚的手，和她一起轻巧地跑向了教堂的正门。"
    "正门是一个窄而高的木质大门，最顶端有着圆弧状的轮廓，门框上镶嵌着精巧的浮雕。"
    "嘉然轻轻地推了推大门，深色的木质大门看样子有段时间没有人使用过，门把手上落着一层薄灰。"

    jump .part4

label .part4:
    #bgm 神圣的宗教音乐
    play music "audio/bgm/b站女声圣咏.mp3" fadeout 1.0 fadein 3.0
    #场景 黄昏 教堂内

    scene 教堂内 with fade
    #声音 【开门的声音】
    play sound "/audio/se/开门声.mp3"
    "吱呀一声，门开了。顺着那小小的开口，向晚窥见了被多边形的狭长阳光所切割开来的昏暗内室。被彩色玻璃晕染上颜色的阳光从那细长的窗户穿入，然后落在了整齐排列的座椅上。"
    "丁达尔效应让室内的浮灰被照耀成闪光的蜉蝣，它们自得其乐地在寂静的教堂里漂浮着。"
    "嘉然将门又推开了些，和向晚缓步走入。空气中弥漫着一股陈旧的香气，似乎是上一次礼拜时点燃的香薰。"
    "和信仰一样，香气也久久地萦绕在教堂里，被时间紧紧锁住。"
    "嘉然缓缓地在木质地板上踱步而行，她每走一步，地板上都会留下一个脚印——灰尘积了一层，也许神也是会寂寞的。"

    #【嘉然的立绘亮】
    #声音 【缓慢的脚步声】
    play sound "/audio/se/逐渐远去脚步.mp3"
    show jr laugh with dissolve
    #立绘 嘉然（日常）：
    jr "晚晚，你看，这里是不是很漂亮？"

    #【嘉然的立绘暗】
    "嘉然转过头来，看向向晚。"
    stop sound
    "她说的无疑是教堂那精美的穹顶，金质的吊灯垂下，描绘着静谧的宗教场景的壁画被雕刻在环形的穹顶之上。"
    "样式复杂而精巧的木质唱经台在教堂的两侧依次排开，即便是灰尘也无法阻止那打了蜡后的光芒闪烁在夕阳里。"
    "烛台和经书在最前方的圣龛上依次排开，圣龛背后的石台上铺着金色与红色相间的丝绸。丝绸上有颜色各异的线勾勒出圣洁的图片。"
    "一切都井然有序，就好像它们随时准备着再一次为信仰而发光发热。"
    "向晚的视线划过这一切，无以言喻的感动充斥在心中。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr laugh at r2
    show xw expect at l2 with dissolve

    #立绘 向晚（惊喜）：
    xw "好美啊……然然，这里以前一定很热闹吧。"
    xw "如果所有人都还在，那些蜡烛都被点燃的话，场面该多么的壮观啊。"
    xw "你看你看，那个石台上面，还有这么多的座位旁边，还有那个吊灯，在礼拜的时候应该都会被点燃吧。"
    show xw normal eyeclosd at l2 with dissolve
    xw "那样的话，在一片光芒中祭拜神明，一定很美。"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（喜悦）：
    jr "对呀！可好看了"
    jr "虽然直到现在也不知道他们所说的是什么意思，但那些来的人也的确因为这些而得到了心灵上的解脱。"
    jr "晚晚，虽然我不是神父，也当不成修女。不过在这里，我会尽我所能。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "教堂里，高悬在石台之上的十字架上悬挂的并非常见的神明，而是一位慈眉善目的女性。她留着长发，低垂双眼，优雅而沉静。"
    "但向晚看不到她，她满眼都是嘉然。那粉色的少女背靠着闪光的教堂，仿佛替代了这教堂本身所供奉的神明般，在刹那间，被赋予了神性。"
    "向晚急忙调整自己的呼吸，有那么一瞬间，那些话语就要脱口而出。"
    "但她忍住了，尽管这种忍耐与其说是果断，不如说是犹豫。"
    "向晚看着嘉然，一言不发，却又有千言万语，在喉咙那里不停打转。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw shy at l2 with dissolve
    show jr laugh at r2 with dissolve
    #立绘 向晚（感动）：
    xw "谢谢你……然然，我一定会告诉你的。很快。"
    xw "我只是还没有准备好……然然，我们一会再说吧，你能带我在教堂里走走吗？"

    #立绘 嘉然（微笑）：
    show jr smile with dissolve
    jr "没事的，晚晚，不要担心。"
    jr "跟我来吧，我对这里可熟了，每一个角落我都知道的一清二楚。"

    show jr normal with dissolve

    jr "要是别人来当导游的话，我还真不放心呢。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    scene 教堂走廊 with dissolve
    play sound "/audio/se/清脆的脚步声-3.mp3"
    "说着，嘉然带着向晚向左边走去。那里有一个小小的通道，十分不起眼。在通道的入口处摆着两根蜡烛，烛台的造型十分精致，但显然已经失去了其本身的作用。"
    "进入通道后，嘉然拐了个弯，带着向晚来到了紧贴着大厅的走廊上。"
    "走廊里铺着一条泛红色的地毯，走廊左边有一排房间，门上贴着相应的编号。"
    "嘉然来到了编号为873的房间面前，她轻轻推了推门，门却没有听话地打开。它被锁住了，而锁头上布满灰尘，显然这里已经很久没有被打开过。"
    "嘉然却并不显露出意外的表情。她转过头，对向晚摆了个鬼脸，然后娴熟地蹲下身，从地毯下方摸出了一把钥匙。"
    "钥匙造型典雅，似乎还有金丝做装饰。锁头在被解开时发出了一声清脆的响声。这一声响似乎发动了时钟的发条一样，让整片走廊里近乎于凝滞的时间重新开始流动。"
    play sound "/audio/se/开门room_door_O.mp3"
    "有风从通道的另一侧灌入，吹散了那些氤氲许久的香气，向晚感到自己的头发正在空中飘散，她看着嘉然为自己打开了房间的门。"
    "随后，她和嘉然一同步入了其中。"

    jump .part5

label .part5:
    #bgm 放松的音乐
    play music "audio/bgm/放松 温暖 平静.mp3" fadeout 1.0 fadein 3.0
    #场景 黄昏 嘉然房间内
    scene 嘉然房间 with fade
    "房间不大，但看上去十分充实，窗户紧闭着，在窗户边上摆着一张单人床。床边是一个将书柜和书桌二合一的家具，配着一把造型古朴的木椅子。"
    "在房间的另一边，有一个衣柜。除此之外，屋子里再无其他东西。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr smile at r2
    show xw calm at l2
    with dissolve
    #立绘 嘉然（喜悦）：
    jr "看——这就是我的房间！"
    jr "当然啦，是很久以前的房间，我已经很久没有来过这里了。"
    show jr laugh at r2 with dissolve
    jr "你看！这是我当时的照片！这些都是我在这里生活时的东西。"
    jr "这个是老修女给我的神像，这些都是来这里礼拜的人送给我的小东西。"
    jr "这些都是经书，当时我还看不太懂，只能听老修女给我讲。"
    jr "真遗憾啊，当时我还不理解每一次礼拜的内容，如今我理解了，却也听不到了。"
    jr "来，晚晚，坐到床上吧，这可比病床舒服多了。"

    #声音 【布料摩擦的声音】
    play sound "<from 4 to 12>/audio/se/衣服布料声.mp3"
    show xw uncertain at l2 with dissolve
    #立绘 向晚（惊讶）：
    xw "哇，真的诶，好软！我还以为教堂里面的生活条件会很艰苦呢。"
    xw "话说回来，然然，这些厚厚的大部头是你的吗？"
    stop sound
    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "说着，向晚指了指书柜上那些深色的宗教卷宗。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr normal at r2 with dissolve
    show xw calm at l2 with dissolve
    #立绘 嘉然（日常）：
    jr "严格来说都是教堂的啦，只不过临时在我这里由我保管而已。"
    jr "每一本我都读过，当时还能倒背如流来着，不过现在就做不到了。"
    jr "不过，虽然详细的内容忘记了，但是那些文字中蕴含的思想却没有。"
    jr "我觉得神父和修女他们也是这样的吧。其实他们也未必能把每一句经文都背下来。"
    jr "但是他们都铭记着那些经文背后的含义，所以即便背不下来，也能布道礼拜。"

    #立绘 向晚（微笑）：
    show xw smile at l2 with dissolve
    xw "然然你好厉害啊，这种看起来这么古老的书也能读进去。如果是我的话，可能不到两页就放弃了。"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "也没有啦，毕竟生活在教堂里。而且阅读经书不仅仅是修女的要求，也是我的使命的一环。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "说到这里，嘉然的脸上流露出了一丝恍惚。她随后立刻回过神来，不可置信地看向自己的双手。"
    "她很快就压制住了自己的惊愕，恢复了正常的神态。"
    "她偷偷看了一眼向晚，暗自庆幸于向晚因为正在浏览那些图书的封皮，而没有注意到自己不自然的停顿。"
    "嘉然伸出手，似乎想触碰向晚，但她的手顿在了半空，终究还是没有碰到向晚那轻轻抖动的头发。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr normal at r2 with dissolve
    show xw calm at l2 with dissolve
    #立绘 嘉然（日常）：
    jr "晚晚。"
    show xw uncertain at l2 with dissolve
    #立绘 向晚（日常）：
    xw "嗯？怎么了然然？"
    show jr sad at r2 with dissolve
    #立绘 嘉然（尴尬）：
    jr "我突然想起来……我还有件事没做呢。老修女拜托过我，如果有机会回来，一定要去参拜一下圣龛，而且只能我自己一个人。"
    jr "所以我可能得先出去一下。晚晚，你就在这里等我，我很快就回来。"
    show jr smile at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "对了，我还可以顺便找找教堂里的猫在不在。"
    jr "那只猫特别可爱，一直生活在这附近。"
    show xw gloomy at l2 with dissolve
    #立绘 向晚（皱眉）：
    xw "然然，只能你自己一个人吗？我跟着你去吧。"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "嗯，因为涉及到一些其他的原因，所以只能我一个人去"
    jr "别担心，晚晚，我没事的。"
    jr "你看，我现在力量都恢复了，再说了，教堂可是我熟悉的地方。"
    jr "我很快就回来。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "说着，嘉然握了握向晚的手。随后，她站起身来，走出了房间。"
    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw sad at l2 with dissolve
    show jr normal at r2 with dissolve
    #立绘 嘉然（日常）：
    jr "等我哦，晚晚，我很快就回来。"
    #立绘 向晚（皱眉）：
    xw "嗯……好，你注意安全。"

    #【嘉然的立绘暗】
    #【向晚的立绘暗】
    "向晚有些担忧地挥了挥手，她面露踌躇，不知道是否该跟上去。"
    play sound "/audio/se/木门关门.mp3"
    #声音 【关门的声音】
    hide jr with dissolve
    "嘉然走了出去，轻轻地将门关上。她的动作很轻，但关门的那一刹那发出的咔哒声依然显得无比刺耳。"
    "门关上以后，外面的声音似乎一下子就被隔绝了。向晚心中的不安迅速地膨胀起来，近乎死寂的沉默让她无所适从。"
    show jrfj_1
    "为了排解自己心中蠢蠢欲动的不安情绪，向晚站了起来，在房间中慢慢地来回踱步，仔细地打量着嘉然曾经住过许久的房间。"
    "她站在书柜上，伸手去触摸那些自己难以想象内容的大部头。这些年龄恐怕和教堂本身不相上下的书籍摸起来十分粗糙，向晚的手指从上面轻轻地掠过，她仿佛听到了自己拨动琴弦时发出的乐音。"
    "向晚随后看向与书柜连接的桌面。桌面空间不大，但是被收拾的十分整齐。虽然落满了灰尘，但并不显得荒芜。"
    "几本似乎没有看完的精装书叠在一起，彩色的书签零散地插在书中，不知道是某种笔记还是没有读完。"
    "一个用木头做成的小十字架被小心地安置在了一个黑色的底座上，十字架做工十分粗糙，黑色的底座上，嘉然那字体稚嫩的签名依稀可辨。"
    "书桌的角落处，放着一个金属制成的小篮子，似乎是用来放置杂物的地方，但里面空空如也，积了一层厚厚的灰。小篮子边上是一个热水壶，样式十分复古，而且插座不在边上，看起来反倒更像是保温杯。"
    "在书桌的正中央，薄的有点过分的便签本和一罐黑色墨水被摆在一起。墨水已经见底，黑乎乎的墨水罐几近干涸。"
    scene 嘉然房间 with fade
    "除此之外，书桌上再也没有其他的东西了。"

    #【向晚的立绘亮】
    show xw mow with dissolve
    #立绘 向晚（日常）：
    xw "然然以前……是在这样的地方生活的吗。"
    xw "没有游戏机，没有电视，没法看视频。"
    xw "只能日复一日的在这种地方生活……如果是我的话，肯定坚持不了几天吧。"

    #【向晚的立绘暗】
    "在这样的想法下，向晚慢慢地转向了嘉然的衣柜。实际上，她一开始没有意识到这竟然是一个衣柜。"
    "过于朴素的外表，几乎不存在的装饰，以及因为灰尘和磨损而难以辨认的把手，都让它身为衣柜的身份被模糊了不少。"
    show xw frown with dissolve
    "向晚有些犹豫，不知道自己是否应该打开看一看。嘉然不在这里，而她对于嘉然的过去已经越发感兴趣。"
    show xw normal with dissolve

    "最终，她开始伸出了双手。衣柜的门轻得令她诧异，她轻而易举地打开了衣柜，随后，一股樟脑丸的气味冲了出来。"
    "虽然已经氤氲了许久，但这种气味依然有些刺鼻。向晚有些不快地捏着鼻子扇了扇风，随后才看向衣柜的内部。"
    "意料之中，她没有看到任何时尚的衣服。衣柜里零散地挂着几件似乎是修女所穿的常服，颜色黯淡，材质看起来也不是很讨喜。向晚试探性地摸了摸，感到了针扎一样的痛觉。"
    "这些衣服看起来比嘉然要小上一圈，大概是专门为嘉然以前的体型所准备的。"
    "衣柜下方有两个抽屉，应该是装内衣的。向晚蹲下身来，手搭在抽屉上，脸上泛起微微的红晕。"
    show xw frown with dissolve

    "毕竟，翻内衣这件事怎么说还是有些过界了，向晚觉得自己脑子发晕，但好奇心还是战胜了羞耻。"

    #【向晚的立绘亮】
    show xw normal eyeclosd with dissolve
    #立绘 向晚（脸红）：
    xw "我……我就看一眼！"
    show xw shy with dissolve

    #【向晚的立绘暗】
    #声音【木头的摩擦声】
    play sound "/audio/se/纸张摩擦14302].mp3"
    "随后，向晚带着一股一不做二不休的气势拉开了抽屉——里面空无一物。"
    "向晚稍稍楞了一下，随后自嘲地笑了起来"

    #【向晚的立绘亮】
    show xw gloomy with dissolve
    #立绘 向晚（尴尬）：
    xw "哎……我在搞什么嘛……内衣之类的肯定是一起带走，怎么可能留下。"

    #【向晚的立绘暗】

    "向晚搞不清楚自己心中涌现的情绪到底是失望还是羞耻。就在她打算关上抽屉的时候，向晚看到了抽屉的角落里躺着的一封信。"
    "信封是由牛皮纸做成的，颜色很低调，混在一片深色的背景里，险些就被向晚看漏了。她连忙拿起那封信，仔细端详。"
    "在信的背面，写着“给嘉然”三个字。而这封信已经有被拆封的痕迹，无疑，嘉然已经看过了里面的内容。向晚抱着侥幸的心理，沿着信封的开口处将手指伸了进去。"
    "令她大感意外的是，信纸居然还留在里面，没有被嘉然拿走。"
    "几乎是下意识的，向晚将信纸抽了出来。那泛黄的纸张工工整整的对折着，一丝不苟的样子暗示着其作者一定也是个性格严谨之人。"
    "看着眼前的信，向晚不由得犹豫起来。她知道，私人信件和装着内衣的抽屉不可一概而论，而如果在此时打开信件的话，嘉然知道了也许会生气。"
    "向晚一点也不想让嘉然生气，但眼前的信纸仿佛有着某种摄人心魄的魔力，驱使着她展开它，阅读它，以一种不那么道德的方式走入嘉然的过去。"

    #【向晚的立绘亮】
    show xw normal eyeclosd with dissolve
    #立绘 向晚（尴尬）：
    xw "然然，对不起！"
    show xw shy with dissolve

    #【向晚的立绘暗】
    #声音 【纸张的摩擦声】
    play sound "/audio/se/折叠_翻开_摩擦纸张18749].mp3"
    #图片【信纸的立绘出现】

    "向晚掩耳盗铃似地闭上了双眼，然后缓缓展开了信纸。随后，她小心翼翼地睁开眼睛，心虚地看向信纸上娟秀的文字。"
    stop sound
    "“嘉然，你应该已经准备好出发了吧。在这里和你一同生活了十六年，得知你要离开的时候，我心中的难过无以言喻。”"
    "“诚然，自从得知了你的使命，我就意识到这一天的来临是无可避免的。每一天，我都反复告诫自己，要学会接受，要遵循主的意志。”"
    "“但人之所以为人，就是因为我们无法控制自己的情感。十六年来，我和神父倾囊相授，将我们所知道的一切，所信奉的一切，全部传授于你。”"
    "“我们看着你羽翼渐丰，看着你的笑容越来越耀眼，看着你的灵魂越来越高贵。而现在，就是你将这一切付诸实践的一天。你要努力地去拯救所有人，唯独你能做到这一点。”"
    "“愿主原谅我的不敬，嘉然，其实从心底里，我不希望你背负这样的使命。我希望你像一个普通的少女一样，享受着完整的青春，拥有幸福的人生。但我知道，这样的想法只是一厢情愿。”"
    "“无论是谁，生下来就背负着相应的使命。对于每一个人，世界上都存在着至少一件‘非Ta不可’的事情等着被完成。无论是你，亦或是我。”"
    "“嘉然，我能感到主在不久后就要召我入祂伟大的国，我只是依旧放心不下你。我一直如爱我的女儿一样爱你，嘉然。”"
    "“有空时，不妨回来看看。我们会在这里等你。”"
    "最后几行文字边上，零零散散地落下了几滴泪痕。没有落款，但向晚已经猜到了。"
    "这也许就是嘉然所提到的老修女给她写的信，里面的内容她大概也都已经猜到。然而，被反复提起的“使命”却勾起了向晚的兴趣。"

    #图片 【信纸的立绘消失】
    #【向晚的立绘亮】
    show xw uncertain with dissolve
    #立绘 向晚（疑惑）：
    xw "使命……说的是什么？成为偶像吗？"

    "就在这时，一阵声音从门外隐约传来。"
    "向晚急忙将信手忙脚乱地塞回了信封，把信封放入抽屉，然后关上了衣柜的门。"
    with sshake
    #特效 【画面震动】
    #声音 【凌乱的脚步声】
    #声音 【木头的摩擦声】
    play sound "/audio/se/纸张摩擦14302].mp3"

    "她感到自己的血在一瞬间都要冻上了，心脏剧烈地跳动着，肾上腺素在一瞬间大量分泌，让她不由自主地开始大口喘气。"
    "向晚侧耳倾听，却发现那门外的声音似乎仅仅是风声。空洞的风刮过同样空洞的走廊，发出了近乎于呜咽一样的共鸣。"

    #【向晚的立绘亮】
    show xw proud with dissolve
    #立绘 向晚（尴尬）：
    xw "什么嘛……吓我一跳，搞得我很胆小一样！"

    "向晚有些不忿地想着，而随后，一个念头如闪电般劈入了她的脑海。"
    "嘉然似乎离开的有些久了。"
    "向晚没有手表，房间中也没有任何钟表。尽管时间的流逝似乎显得有些模糊，但向晚可以确认，嘉然离开的时间已经长地有些异样了。"
    "刚刚压制住的不安再一次一涌而出，向晚这次没有犹豫，立刻走到门边。她无声地打开了门以后，小心翼翼地溜了出去，来到了走廊上。"

    jump .part6


label .part6:
    #bgm 紧张的音乐
    play music "audio/bgm/悬疑诡异气氛通用BGM1.mp3" fadeout 1.0 fadein 1.5
    #场景 黄昏 教堂内
    scene 教堂走廊 with fade
    "走廊里空无一人，空中的浮尘静悄悄地望着她。她看向走廊的尽头，仿佛嘉然的身影还留存在那里。仿佛墙壁和地毯还记得那靓影划过自己身躯时的触感。"

    #【向晚的立绘亮】
    show xw worried with dissolve
    #立绘 向晚（不安）：
    xw "然然！然然！嘉然！"

    "向晚喊了几声嘉然，可除了回声以外，没有任何回应。"

    #声音【急促的脚步声】
    play sound "/audio/se/急促脚步running_in_a_hall.mp3"
    "她沿着走廊，向大厅走去。也许是隔音太好，嘉然没听到自己的声音，向晚安慰着自己。"
    "然而，当她拐入大厅的时候，所有的自我安慰刹那间土崩瓦解。大厅里空无一人，那些死去的物件安安静静地站在它们本来的位置上，波澜不惊地望着向晚。"
    stop sound
    scene 教堂内 with fade
    "而在大厅的另一边，有一个已经被上了锁的通道。夸张的铁链在门上捆出了一个X的形状，怎么看也不像是嘉然能进去的地方。"

    #【向晚的立绘亮】
    show xw unconcerned with dissolve
    #立绘 向晚（不安）：
    xw "怎么会……嘉然！"

    "向晚感到恐惧席卷了自己的四肢百骸。她熟悉这种感觉：当乃琳，珈乐，贝拉失踪的时候，她也感到了同样的恐惧。"
    "无论多少次，这种恐惧都没有因为熟练度的提升而衰减分毫。每一次，每一次，向晚都止不住地为这世界中无所不在的恶意所颤抖。"
    "向晚努力地在恐惧的浪潮中维系住自己的理智，她知道，现在不能困于惶恐，而是得在一切还来得及之前找到嘉然。"
    "大厅里没有人，而既然是嘉然带着自己来到此处，那她必然也不会不声不响地就此离开——如果只是为了引开她的话，根本没有必要如此大费周章。"
    "向晚立刻向自己刚刚所在的走廊走去，她要去往走廊的另一端：那拐入右侧的尽头。"

    #【向晚的立绘亮】
    show xw unconcerned frown with dissolve
    #立绘 向晚（不安）：
    xw "嘉然！嘉然！"
    play sound "/audio/se/急促脚步running1.mp3"
    "向晚大步流星地走着，一边走，一边呼唤着嘉然的名字。她的声音和话语迅速地淹没在永恒的沉默里。"

    #声音 【脚步声】
    #声音 【扭动门把手的声音】
    play sound "/audio/se/开门声.mp3"
    "路过那些宿舍的时候，她没忘记挨个拧拧门把手。但除了873房间以外，所有门都紧锁着。这一次，地毯下也没有备用钥匙了。"
    "当她拐进那走廊的尽头，却没看到能够回应自己期待的画面——杂乱的家具堆砌在一起，封死了走廊，而在短短的一截走廊里，没有任何房间可供进入。"
    "那没有任何其他出口或是房间的整齐墙壁。简直像是在嘲讽她一样，这里的墙壁看起来比873房间外走廊的墙壁还要光滑，还要平整。"
    "活像是那墙壁正对她徒劳无功的找寻而发笑，并告诉她：这里不但没有门，还没有窗，你的嘉然消失了。"
    "向晚大声地呼唤嘉然，她无力的喊声在空荡的走廊里回响。"

    #【向晚的立绘亮】
    show xw gloomy with dissolve
    #立绘 向晚（不安）：
    xw "嘉然！嘉然！然然你在哪！"
    xw "怎么会……怎么会这样！然然她去哪了！"
    xw "冷静，我得冷静！向晚，冷静！"
    xw "她不可能一个人离开，她一定还在这里！"

    "短暂的慌张后，向晚迅速地镇定下来。离开的时候，嘉然走得并不急促。也许是失忆的影响，让嘉然陷入了梦游一样的状态——向晚猜测。"
    stop sound
    "在向晚的大脑里，无数的想法在一瞬间成型。她小心地筛选最具有可能性的选项，却得不出一个能让自己信服的结果。"
    "巨大的挫败感和疲倦感充斥了她的身心。向晚捂住了自己的脸，靠着墙壁慢慢坐了下来，发出了轻轻的呜咽声。"
    "背上传来了坚硬而平滑的触感，冰凉的感觉让向晚打了个激灵。这真的是石头做成的墙壁吗？向晚心想，如果是的话未免也太过冰冷了。教堂内并没有那么冷，甚至可以说有些暖和，石头明显不应该显现出如此的温度。"

    #【向晚的立绘亮】
    show xw shock with dissolve
    #立绘 向晚（惊讶）：
    xw "等等！这个……好像不是石头！"

    "几乎就在同时，向晚睁大了双眼。下一刻，她立刻站起身，开始仔细地端详背靠着的墙壁。"
    "那墙壁根本不是石头做成的，所谓的石头不过是一层墙纸而已。向晚的手指抚摸过那以假乱真的墙纸，感受着冰冷的温度，眼睛中流露出了难以言喻的激动。"
    "向晚看过不少悬疑小说，对于暗室和密道这种元素十分熟悉。几乎是一瞬间，她就推断出嘉然很有可能进入了藏在这里的密道，所以自己才会找不到她。"
    "得到鼓舞的向晚立刻振作起来了，开始在走廊上寻找任何蛛丝马迹。果不其然，在靠近废弃家具的地方，她找到了一块墙纸上的缝隙。"
    "那缝隙看起来似乎被墙纸的图案的掩盖地很好，但夕阳的光芒实在是太过尖锐，任何缝隙都无法在阴影中藏匿。"
    "顺着缝隙，向晚确定了一块大概一人高的暗门的位置。她使劲地推了推暗门，听到了金属发出的令人牙酸的摩擦声。"

    #声音 【令人牙酸的摩擦声】
    play sound "/audio/se/玻璃碎片挤压摩擦20603].mp3"
    #【向晚的立绘亮】
    show xw mow with dissolve
    #立绘 向晚（皱眉）：
    xw "好……沉……啊！"

    "尽管向晚拼尽全身力气，将整个人都压在了密道的入口上，那密道的大门却仅仅是开了个小缝，死死地卡在边缘。"
    "向晚觉得自己头上的青筋都要爆炸了，她一边努力地推，一边从牙缝中挤出一丝声音，试图呼唤可能在密室里的嘉然。"

    #【向晚的立绘亮】
    show xw mow with dissolve
    #立绘 向晚（皱眉）：
    xw "然然！然然！"
    xw "你是怎么……推开……这个门的！"
    xw "然然！你听得到吗……听得到……就帮我——啊！！！"

    #声音 【碰撞声】
    play sound "/audio/se/碰撞音效14697].mp3"
    #特效 【画面震动】
    with sshake
    "也许是年久失修，也有可能是福至心灵，随着一声清脆的断裂声，支撑不住的密室大门终于敞开了。"
    "还压在上面的向晚没反应过来，直接连滚带爬地摔进了密室内。"
    jump .part7

label .part7:
    #bgm 神秘的音乐
    play music "audio/bgm/Leos Janáček - In the Mists.mp3" fadeout 1.0 fadein 3.0
    #场景 黄昏 密实内

    scene 密室内 with fade
    #特效 【画面震动】
    with sshake
    #声音 【碰撞声】
    #【向晚的立绘亮】
    show xw shock at l2 with dissolve
    play sound "/audio/se/碰撞音效13319].mp3"
    #立绘 向晚（惊讶）：
    xw "啊！疼疼疼！"
    show xw worried at l2 with dissolve
    #立绘 向晚（不安）：
    xw "然然，然然！"
    "向晚拍了拍身上灰尘，难以压制自己心中的喜悦。然而，在狭小的密室里，她却没看到嘉然的身影。"
    "没有任何余地可言，不过十几平米大小的密室里，没有任何可供藏身的地方。而除了面前摆着的石台，以及石台上造型典雅的木盒子以外，这个密室里再无其他东西。"
    show msn_1
    "有天光从头顶洒落，向晚抬头望去，发现密室竟然就在教堂尖塔的正下方。从被切割成四份的天空望去，能看到夕阳的光辉和云。"
    scene 密室内 with fade
    "在这勉强可用的照明下，向晚来到了石台前。嘉然不在这里，这一毁灭性的打击让她刚刚重建的信心再一次消失殆尽。"

    "向晚失魂落魄地拿起了那个木盒子，她看得过于专注，以至于忘记了自己的目的。"

    #【向晚的立绘亮】
    show xw shock at l2 with dissolve
    #立绘 向晚（疑惑）：
    xw "这是……什么？"

    "向晚已经顾不得什么道德与否，亵渎与否的事情了，手里的盒子传来了沉甸甸的分量，这分量成为了她最后一根稻草。"

    scene 帷幕之末
    with dissolve
    "几乎不报任何期望，向晚打开了木盒。一柄通体漆黑的匕首正安静地躺在金色的丝绸上，刀锋闪烁着令人胆寒的光。即便只是看着，向晚都感到了一种富于痛苦的切割感。"
    "向晚不是贝拉，对武器这类东西根本玩不来。尽管如此，那武器却仿佛在邀请向晚拿起自己一样，闪烁着神秘的光芒。"

    #图片 【帷幕之末的立绘出现】
    #【向晚的立绘亮】
    #立绘 向晚（疑惑）：
    xw "为什么这里会有一把匕首……这是匕首吧？"

    "向晚一边自言自语，一边用手指试探性地搭上了刀柄。一阵寒意传来，那匕首不知是用什么材质做成的，竟通体散发出惊人的冷意。"
    "向晚打了个哆嗦，刀柄虽然看上去光滑无比，但摸上去却颇有些摩擦力，并不至于轻易地滑落脱手。向晚深呼吸了一下，然后将匕首取了出来。"
    "她的温度迅速地进入了原本了无生机的匕首内部，很快，向晚就感到自己的身体仿佛与这把匕首之间产生一种不可分割的联系。"
    "在向晚手里，匕首不再冰冷，而是像一块被烧热的石头一样，散发着稳定而持久的温度。"
    "向晚将匕首拿到自己眼前端详，惊愕地发现那原本漆黑的匕首竟然逐渐泛出了妖冶的紫色。闪光的纹路从手掌接触的部分蔓延开来，直到扩散到整个匕首。"
    "而与此同时，这闪光的纹路也隐约爬上了向晚的手。"
    scene 密室内
    show xw uncertain at l2
    with dissolve

    #【向晚的立绘亮】
    #立绘 向晚（疑惑）：
    xw "这是什么东西……好怪啊……"

    #图片 【帷幕之末的立绘消失】
    #【嘉然的立绘亮】
    show jr normal at r2 with dissolve
    #立绘 嘉然（日常）：
    jr "晚晚，你在这里干什么？"


    #立绘 向晚（惊讶）：
    show xw shock at body_shake
    $renpy.pause(0.5)
    xw "呀啊啊啊啊！"

    "大概是因为过于专注，向晚没有注意到嘉然来到了密室的门口。惊慌失措之下，她胡乱地举起匕首转过身去。"

    #【嘉然的立绘亮】
    show xw shock at l2 with dissolve
    show jr panic at r2 with dissolve
    #立绘 嘉然（惊讶）：
    jr "呀！晚晚，你小心点！"
    jr "对不起，我是吓到你了吗？"

    "嘉然的声音让向晚清醒了过来，在意识到自己正拿着匕首指向嘉然的时候，向晚连忙收起了这把匕首，然后将它迅速地放回了木盒子里。"
    "随后，她冲过去，一把抱住了嘉然。"
    show xw worried at l2 with dissolve
    #特效 【画面震动】
    with sshake
    #声音 【布料摩擦的声音】
    play sound "/audio/se/衣服布料声.mp3"
    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw shock at l2 with dissolve
    #bgm ——播放放松的音乐——
    play music "audio/bgm/抒情双吉他.mp3" fadeout 1.0 fadein 3.0
    show jr embarrassed at r2 with dissolve
    #立绘 嘉然（惊讶）：
    jr "诶！晚晚！你怎么了？"
    show xw worried at l1 with dissolve
    #立绘 向晚（愤怒）：
    xw "我刚刚还以为你消失了！急死了！你去哪了？"
    xw "我到处找都找不到你，我刚刚发现这个密室的时候还以为你在里面呢！"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "别担心，别担心，我不会突然消失的。"
    jr "我刚刚是去外面找猫了。我不是说我们教堂里有一只猫吗，就是那只猫。"
    jr "可惜猫猫好像已经走了，怎么找也找不到。回到宿舍里，发现你也不见了。我就到处找，然后发现了这个密室。"

    "说着，嘉然轻轻地拍了拍向晚的头。"
    stop sound
    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw calm at l2 with dissolve
    #立绘 嘉然（疑惑）：
    show jr shock at r2 with dissolve
    jr "说起来，我还从来没来过这呢。虽然在这里住了很久，但是这个密室我从来没听过。"
    show xw shock at l2 with dissolve
    #立绘 向晚（惊讶）：
    xw "啊？你不知道吗？"
    show jr normal at r2 with dissolve
    #立绘 嘉然（日常）：
    jr "不知道呀。"
    show xw proud at l2 with dissolve
    #立绘 向晚（日常）：
    xw "不是因为失忆导致的吗？"
    show jr normal at r2 with dissolve
    #立绘 嘉然（日常）：
    jr "肯定不是，我对这里一点印象都没有。"
    jr "对了，晚晚，你刚刚拿的那个东西是什么呀？"
    show xw uncertain at l2 with dissolve
    #立绘 向晚（疑惑）：
    xw "我也不知道，好像是一把匕首。我在这个木盒里找到的。"
    xw "你看，就是这个。我擅自摸了一下，然后它就这个样子了。"

    "说着，向晚带着嘉然来到了那个石台边上。还在幽幽发光的匕首躺在丝绸上，静静地等待着向晚再一起举起它。"
    "嘉然伸出手，拿起了那把匕首。闪光的纹路和紫色的光芒还没有消退，可就在嘉然碰到匕首的一刹那，这些紫色的光芒刹那间消失不见，而就在同时，金色的光从匕首里涌现而出。"

    #stop music fadeout 2.0
    "就好像河流奔腾入海，金色的光从匕首直直的涌入嘉然的手中。"
    "看到这一幕，向晚本能地想让嘉然松开那把匕首，但还没等移动，就发现自己的身体仿佛被麻痹了一样使不上力气。她眼睁睁地看着那些金光尽数涌入了嘉然的体内。"
    play sound "/audio/se/voice_of_light.mp3"
    "沿着血管，金色的光一路向上。从嘉然那双湛蓝色的眸子里，向晚看到了黄金色的光点在不断闪烁。"

    #声音 【闪光的音效】
    #【向晚的立绘亮】
    show xw shock at l2 with dissolve
    #立绘 向晚（惊讶）：
    xw "然然！"
    "向晚呼唤着嘉然，但她却好像没听见一样。等到那匕首中的金光尽数消失，嘉然如同从白日梦中惊醒一样回过神来，向晚才感到自己身上的麻痹感终于消退。"
    play sound "/audio/se/voice_of_light.mp3"
    #声音 【闪光的音效】
    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr haggard at r2 with dissolve
    show xw worried at l2 with dissolve
    #立绘 向晚（不安）：
    xw "然然，然然你没事吧！"
    show jr laugh at r2
    #立绘 嘉然（微笑）：
    jr "啊，晚晚，我……我没事。"
    jr "我只是……想起来了一些东西……我想起来很多东西。"

    "嘉然扶着自己的额头，声音微微有些虚浮。"
    "向晚扶住了她似乎摇摇欲坠的身躯。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw calm at l2 with dissolve
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "晚晚……我想起来了。不，我不是想起来了……我知道了。"
    jr "我知道该怎么做了，我知道我的使命了！"
    show xw uncertain at l2 with dissolve
    #立绘 向晚（疑惑）：
    xw "你在说什么？然然，什么使命？"

    #立绘 嘉然（微笑）：
    jr "晚晚，我有我的使命要去履行，而你一定要帮我，好吗？"
    show xw gloomy at l2 with dissolve
    #立绘 向晚（不安）：
    xw "我答应你，我答应你，然然，我答应你，你的使命到底是什么？"
    #嘉然（微笑）：
    show jr laugh at r2 with dissolve
    jr"不要着急，晚晚，让我先给你讲个故事吧。"
    jr"那是……那是我刚刚加入A-Soul的时候。我刚刚离开教堂，对外面的世界充满好奇。"
    jr"我怀抱着十足的热情，希望用自己在教堂中学到的一切为大家带来快乐。"
    jr"公司对我很好，它为我提供了几乎一切必要的资源。"
    jr"包括一次实地考察。"

    #【进回忆】
    #【向晚立绘暗】
    #【嘉然立绘暗】

    #play music "audio/bgm/抒情双吉他.mp3" fadeout 1.0 fadein 3.0

    jr"公司带我去了枝江市南面的贫民窟。"
    jr"那是一次秘密出访，即便是公司内部也没有几个人知道。"
    jr"阿草让我对你们三缄其口，我答应了。"
    jr"我被保护得很好，好几个便衣保镖跟在我的身边，我坐在汽车里，透过当时最先进的一体式透明车窗观察贫民窟的场景。"

    jr"层层叠叠的自建房如同蛋糕一样摞在一起，颜色各异的建材如同拼贴画一样杂乱无章地缝合在一起。"
    jr"大部分建筑都遵循着基本的建筑原理和物理法则，老老实实地用木头和金属板搭建出长方形的小屋。"
    jr"但也有一些奇妙的建筑仿佛脱离了重力，以一种极其危险的方式保持着平衡。"
    jr"贫民窟大多低矮，最多只有五六层高。它们如同厚厚的地衣一样，完美地反映出地形的起伏。"
    jr"就像一层人工制成的钢铁草皮，一直延伸到很远的地方。"
    jr"这些高低不一的建筑就像是小孩子拼起来的乐高玩具一样，以一种难以辨别的复杂方式拼接在一起。"
    jr"我的目光在那些连接上下层的梯子，楼梯，台阶，绳梯之间流转，试图搞清楚这些层叠的建筑以何种方式连接彼此。"
    jr"但我什么都看不懂，贫民窟看起来就像是一个完整的整体，内部被精致而混乱的纹路所填满。"
    jr"我看到小孩子如矫健的兔子一样在贫民窟里穿行，他们借助犹如地道一样四通八达的通道，消失又出现。"
    jr"我毫不怀疑他们只需要几秒钟就能跑到我们车的跟前。"
    jr "当时正是A-Soul的飞速上升期，在廉价娱乐泛滥的贫民窟里，我们的知名度高得可怕。"
    show jr thinking at r2 with dissolve
    jr "如果我毫不伪装地坐下车，恐怕五秒内就会被人潮给吞没。"
    jr"贫民窟的人早就注意到了我们的车。人来人往的大街上，路过的人无不警惕地看着我乘坐的车辆缓慢地移动着，眼睛里尽是敌意。"
    jr"然而，当车辆的外壳被投影上A-Soul的海报时，他们的目光转瞬间变为喜悦和惊讶。"
    jr"他们凑上前来，我能看到他们的眼中闪动着兴奋的光。原本如河流般流动的大街刹那间停滞。"
    jr"这种光芒不是因为这辆车多么的高级，而是因为A-Soul的出现。"
    jr"公司人员走下车，和那些热情的人们沟通起来。"
    jr"他问他们对A-Soul有多么喜爱，而那些人用近乎于狂热的态度作答。"

    #白天
    scene 贫民窟 with dissolve
    show screen memory_filter
    #"贫民窟"抒情的音乐（不含立绘）
    #粉丝1：
    play sound "/audio/se/难分辨的人群噪音-2.mp3"
    "【粉丝1】" "A-Soul就是我的精神支柱！我每天都要溜五十遍A-Soul的直播录像，二十遍珈乐的歌，十遍向晚的集锦！"
    "【粉丝1】" "没有A-Soul我就吃不下睡不着，你是A-Soul的调研人员吗，你一定要替我转告她们，我特别特别喜欢她们每一个人！"

    #粉丝2：
    "【粉丝2】" "我最喜欢A-Soul了！我愿意相信A-Soul的每一个人！A-Soul给了我梦想和希望，她们让我明白原来美好的事物真的存乎于世界之上！"
    "【粉丝2】" "我最爱A-Soul了，为了你们我以后一定要努力工作，找一个网络更好的地方每天看你们的直播！"

    #工作人员：
    "【工作人员】" "谢谢，谢谢，大家的热情我们都收到了。能够看到大家这么喜欢五个小姐姐，我们也非常感动。"
    "【工作人员】" "感谢每一位一个魂的支持，我们今天已经收到了足够多了调研数据，接下来请大家为我们空出一条路离开。"
    stop sound
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr distressed at r2
    with dissolve
    #jr"
    jr"贫民窟是一个怎样的地方呢？其实时至今日，我的印象已经模糊了。"
    jr"当时，我睁大双眼，将所有的精神集中在眼睛上，试图将自己所看到的一切牢牢地刻印在我的脑海之中。"
    jr"我看到废旧的铁皮和肮脏的污水交织在一起，颜色诡异的小溪如蛇一样蜿蜒的流过那些破旧的房屋。"
    jr"如蜘蛛网一样千疮百孔的破布半死不活地耷拉在那些不知道是用什么材质凑合做成的棚顶上。"
    jr"我看到污浊不堪的广告牌被涂鸦所填满，那些涂鸦要么极度冒犯，要么极度朋克。"
    jr"唯独A-Soul的海报大致还算是整洁。诚然，在那样的一个环境下，你不能要求太多。"
    jr"不知道从哪里飘来的黑色气体和难以言说的腐蚀性气味让我们的海报看起来有点泛绿。"
    jr"但至少，我能看出来我们五个人的脸，而在那样的一个环境里，能看清就已经是最好的评价了。"
    jr"我看不清他们房屋的材质，衣服的材质，饰品的材质。"
    jr"除了他们对我们的爱，我只看得见这个。"
    jr"我们的车行驶在贫民窟的一条主干道上，那上面人来人往，摩肩擦踵。"
    jr"我们的车行驶速度之所以缓慢，也是因为这条路的路况可以用地狱来形容。"
    jr"然而，当工作人员说我们要离开，希望空出一条道的时候，所有的人都好像得到了某种指示一样，自发地排到了道路两侧。"
    jr"离我们最近的那些一个魂们主动帮我们的车护航。我带着耳机，能听到工作人员和粉丝们的交流。"
    jr"我听到那些人带着极大的热情和工作人员解释贫民窟曾经的恶劣治安以及A-Soul的出现所带来的改变。"
    scene 贫民窟 with dissolve
    show screen memory_filter
    #粉丝3：
    play sound "/audio/se/难分辨的人群噪音-2.mp3"
    "【粉丝3】"  "……您也许不知道，在A-Soul出现之前，是没有一辆车——尤其是像您这种高档车——能在贫民窟活过一个小时。"
    "【粉丝3】" "愤怒的人们会像对待过街老鼠一样对待这种车……但A-Soul为我们带来的光，为我们带来了完全不同的东西。"
    "【粉丝3】" "您能想象吗，当所有人都热爱着同一件事物，愿意为同一件事物付出一切，也愿意遵循同一种规则时，一切会变得何等完美无缺。"
    "【粉丝3】" "我们的身份不再重要，我们和那些住在市中心的富人好像也并无不同。"
    "【粉丝3】" "您知道，即便是枝江市内，喜欢A-Soul的人也是多如繁星。"
    "【粉丝3】" "我们是A-Soul粉丝群体中不那么重要的一部分，但正是这样的事实让我们开心。"
    "【粉丝3】" "我们不是特殊的，不是需要被单独拿出来讨论的。我们和别人一样，和所有热爱A-Soul的人一样，是平等的。"
    "【粉丝3】" "您一定能明白A-Soul对我们是何等重要。我们的生活已经与A-Soul紧紧地维系在了一起。"
    "【粉丝3】" "幸福，喜悦，热爱，这些正面的词汇之所以能造访这样落后的地方，都是A-Soul的功劳。"
    "【粉丝3】" "我有时候会做噩梦……不瞒您说，我有时会梦到A-Soul离开我们的那一天真的到来。我会恐惧得无以复加。"
    "【粉丝3】" "我会尖叫着坐起来，汗水浸湿我的衣襟，我大口呼吸，庆幸这一切是个梦。"
    "【粉丝3】" "我的想法很蠢吧……我相信您，我相信A-Soul。您一定要帮我们传达到啊。"

    #工作人员：
    "【工作人员】" "你放心吧，A-Soul会一直陪着大家的。不要担心，梦永远只是梦，不会变成现实。"
    "【工作人员】" "实际上，我们的车搭载了实时全景录制设备，能确保大家的每一句话都完整地传达到五位小姐姐那里。"
    stop sound
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr laugh at r2
    with dissolve
    ##jr"
    jr"工作人员的这句话再一次引爆了大家的情绪，人们激动地大声呼叫，而这些嘈杂的声音最终融合在一起，变成了一次大合唱。"
    jr"先是《Quiet》，然后是《超级敏感》，然后是《红色高跟鞋》。发音不够标准，咬字也不够清晰，但我依旧低着头，泪流满面。"
    jr"我的思绪十分混乱，我当时想了很多很多很多。"
    jr"最后，我想起了老修女对我说的话——她让我为大家带来幸福，带来喜悦。"
    jr"一阵狂喜充斥了我的心扉。我已经许久没有感受到那样的喜悦，那样的激情了。"
    jr"我意识到，眼下就是我完成我的使命最好的时机。不是以前，也不是过去，就在今天。"
    jr"一个天赐良机正摆在我的眼前——不对，它已经降临在我身上。我是A-Soul的一员，我是A-Soul的嘉然，而我正在切实地为这些住在贫民窟里的人带来喜悦，带来欢笑，带来一种田园牧歌式的平和。"
    jr"他们带着一种安然的微笑，让我感到难以言喻的感动。"
    jr"我们的车缓缓离开贫民窟，工作人员坐回了车上，他脸上事务性的微笑已然褪去。"
    jr"他回过头，看向尚未擦干眼泪的我，满意地点了点头。"
    jr"我当时不理解工作人员这意味深长的目光——我虽未经世事，但依旧敏锐非常。这可不是自夸哦，老修女总是这么表扬我的。"

    scene 直播用房间 with dissolve
    show screen memory_filter
    #夜晚"直播室"抒情的音乐
    #jr"
    jr"那天晚上直播的时候，我一直看着弹幕。那一场是我的独播，我看着满屏的弹幕，试图找出可能是来自贫民窟的粉丝发来的那几条。"
    jr"我知道，他们不富裕，所以我读SC的有些心不在焉，反倒读弹幕的时候更加积极。"
    jr"我以为这种小小的改变只有我自己清楚，但工作人员显然注意到了我微妙的不对劲。下播以后，有人把我叫到了阿草的办公室。"
    jr"我并不十分慌张，一种难以言说的预感在我的血管里流窜。我的太阳穴突突地跳动着，好像有什么事情就要来了一样。"
    jr"是一切要开始了，还是一切要结束了？"
    jr"我带着这样的心情来到了阿草面前。"

    #夜晚"阿草办公室"紧张的音乐（柔和版Soul"Search）
    scene 阿草办公室 夜 with dissolve
    play music "audio/bgm/悬疑.mp3" fadeout 1.0 fadein 3.0
    #jr
    show jr normal at r2
    show ac normal at l2
    with dissolve
    jr"阿草背对着我，他的办公桌上干净的让我诧异。空荡荡的桌子上，放着一个小小的盒子。"
    jr"盒子其貌不扬，我第一眼甚至漏看了。但是仔细打量，就会注意到盒子表面精致低调的浮雕和装饰。"

    #ac"
    ac"嘉然，你来了。"
    ac"今天的实地考察，你感觉如何。"
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr laugh at r2
    with dissolve
    #jr"
    jr"我没有立刻回答，实际上，尽管我被贫民窟里的一个魂所感动，公司做出这种事的动机却一直让我十分不解。"
    jr"尤其是是实地考察的当晚，阿草就要和我见面。这种异样的行为令我有些不安。"
    jr"但最后，我还是老老实实地说出了自己真实的感受。"
    jr"这种东西是无法隐藏的……我流的泪，我受到的冲击与感动，是不能作假的。"
    jr"说到最后，我感觉自己都有了些哭腔。"


    scene 阿草办公室 夜 with dissolve
    show screen memory_filter
    show jr lost at r2
    show ac normal at l2
    with dissolve
    #ac"
    ac"如果他们在骗你呢？"
    ac"如果他们所说的一切，只不过是我们一手安排的演出呢？"
    ac"你还会抱有同样的思想吗？你还会想继续给他们带去幸福吗？"
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr laugh at r2
    with dissolve
    #jr
    jr"我如同被踩到尾巴的猫，激烈而愤怒地回击了阿草。我告诉他，我比任何人都相信一个魂，相信嘉心糖。"
    jr"我相信他们真心实意地支持着我，这一真相如同闪烁的钻石，坚不可摧却也容不得亵渎。"
    jr"阿草脸上的笑容变大了。"

    scene 阿草办公室 夜 with dissolve
    show screen memory_filter
    show jr angery at r2
    show ac happy at l2
    with dissolve
    ac "不要动怒，嘉然。"
    ac "我并不打算质疑我们A-Soul，以及你们五个女孩子的能力。"
    ac "实际上，我们的调查显示，已经有60\%左右的枝江市民成为了我们的忠实观众，而他们对你们五个人的爱无需质疑。"
    ac "然而，话是这么说，看不见摸不着的人心，终究还是难测，不是吗？"
    ac "弹幕是建立在语言之上的，而语言是一种极不准确的容器——它效率低得可怜。"
    ac "即便是公司，也不能保证将粉丝们的心意第一时间就完美无缺地传达到你们这里，更不用说粉丝们的表达能力可能都不足以支撑他们说出自己的心声。"
    ac "且不论那些真情实感是否弄虚作假——嘉然小姐——不谈这些无法证伪的陈词滥调。"
    ac "我问你，如果你想为所有人都带去幸福，现在的你是否够格？"
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr laugh at r2
    with dissolve

    jr "他的问题很冒犯，但却一针见血。"
    jr "的确，我并不能每天都出去实地考察，而也不是所有人都会发弹幕。"
    jr "粉丝们的心声在传递到我这里之前，会被各种各样的杂音和障碍所干扰。"
    jr "我突然感到恐慌，害怕自己听不到粉丝们的呼唤，无法成为他们想要的样子，给他们带去幸福。"
    jr "刹那间，我感到晴天霹雳。我像是从海底浮出水面，又好像从一场大梦中惊醒。"
    jr "彼时，A-Soul刚刚开始活动，一切的一切尚未在一个较长的时间尺度中经受着岁月的考验。"
    jr "梦幻的泡泡把我的思想包裹住，让我的其他感官麻痹了。"
    jr "我不得不去重新思考一个问题：我是否完全回应了一个魂和嘉心糖的期待？"
    jr "我说不出来，我犹豫，张开了嘴，想要说什么，却说不出一个字。"
    scene 阿草办公室 夜 with dissolve
    show screen memory_filter
    show jr panic at r2
    show ac proud at l2
    with dissolve
    #ac"
    ac"嘉然，靠近点。"
    ac"拿起那个盒子，看看里面有什么。"

    ac"嘉然，你似乎天赋异禀……我能看出来，你身上那莫名的亲和力。就连我，有时候也难以自制。"
    ac"你的粉丝，嘉心糖，数量和质量都比别人更胜一筹。和你相关的二创作品也总是能拿到最高的热度。"
    ac"嘉然小姐，我不能忽视你的天赋，我不能对你的使命无动于衷。"
    ac"所以我要把这个东西送给你。"
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr laugh at r2
    with dissolve
    #jr
    jr"那个盒子很轻，外壳似乎是用丝绒做成的。打开后，一个小小的水晶瓶正躺在紫色的丝绸上。"
    jr"那个瓶子看起来像某种高档香水，里面盛放着微微发光的液体。"
    jr"阿草说，这液体名为蓬莱之药。他没有说自己从哪里搞到的，他只跟我说了一件事。"
    jr"它的药效。"
    scene 阿草办公室 夜 with dissolve
    show screen memory_filter
    show jr sad at r2
    show ac panic at l2
    with dissolve
    #ac"
    ac"喝下之后，你将拥有着足以比肩神明的心智。"
    ac"每一位嘉心糖，每一位一个魂心中的所思所想，对你们的期待和希望都会化作一段以光速飞行的信息，进入你的脑海。"
    ac"你会在数个微秒内理解，分析，并记住这些信息。随后，你会完美无缺地给出相应的回应。"
    show ac proud at l2 with dissolve
    ac"就好像你能同时与所有喜欢你们的人进行毫无虚假可言的坦诚对话，并且能够在每次对话中给出完美的答案。"
    ac"你会成为所有人所爱的对象，你会成为一位真正的神明。"
    ac"最重要的是，你会成为一个理想化的思想的聚合体，你会成为有史以来最成功的偶像，你会以最杰出的姿态完成你给所有人带去幸福的使命。"
    ac"你会如真正的神一样一劳永逸地解决所有问题。"
    show ac happy at l2 with dissolve
    ac"而这一切，只需要你付出一点小小的代价……"
    hide screen memory_filter
    scene 密室内 with dissolve
    show xw gloomy at l2
    show jr sad tears at r2
    with dissolve
    #jr"
    jr"我没有立刻喝，我把那瓶药带回了宿舍。"
    jr"我坐在床上，盯着那瓶药看。我左看右看也看不出来什么名堂，月亮高高地挂在天上，催促着我做出决定。"
    jr"我很想抱住你们，你们之中的任何一个人都行，如果能五个人抱在一起就更好了。"
    jr"我没有那么做。"
    jr"那天晚上，我把药倒入了热好的牛奶，然后一饮而尽。"
    #【出回忆】
    #【回到黄昏 墓园 抒情的音乐】
    "嘉然的脸上似乎露出了释然的笑容，她指了指那把匕首，说到"
    play music "audio/bgm/悬疑.mp3" fadeout 3.0 fadein 5.0

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw uncertain at l2 with dissolve
    show jr laugh at r2 with dissolve
    #立绘 嘉然（日常）：
    jr "用帷幕之末——用那把匕首——杀了我，晚晚。"
    jr "在我进行最后的仪式时，杀了我。"
    jr "唯有那把武器可以完全地杀掉我和我所代表的形象，唯有那把武器可以终结一切。"
    jr "晚晚，用那把武器杀了我，好吗。"
    with sshake
    #特效 【画面震动】
    show xw shock at l2 with dissolve
    #立绘 向晚（惊讶）：
    xw "你在说什么？然然？"
    xw "怎么可能！"
    xw "我怎么可能杀了你！然然！我永远不会伤害你的！不要说了好吗？"

    "嘉然轻轻的摇了摇头，她的脸上没有悲伤，只有从容的微笑。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw shock at l2 with dissolve
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "我的身体里寄宿着强大的力量，这种力量甚至本不属于我。"
    jr "只有帷幕之末，可以无视这种力量。这种力量很恐怖，晚晚。"
    jr "甚至我都为这种力量而感到恐惧……晚晚，而此刻，这种力量就存乎于我体内。"
    jr "你必须——"
    show xw angery at l2 with dissolve
    #立绘 向晚（愤怒）：
    xw "够了！"
    with sshake
    #特效 【画面震动】

    "向晚带着哭腔，愤怒地打断了嘉然的话。"

    #【向晚的立绘亮】
    show xw angery at l2 with dissolve
    show jr laugh at r2 with dissolve
    #立绘 向晚（愤怒）：
    xw "为什么！为什么你身上有着强大的力量，你就非死不可！"
    xw "如果有人要利用你的力量，那我就帮你干掉他们！"
    show xw cry at l2 with dissolve
    #立绘 向晚（哭泣）：
    xw "然然，你知道吗……你知道吗！"
    xw "你知道我为了救你，还有A-SOUL的大家，付出了多少吗！"
    xw "你们的生命比什么都要重要，甚至，甚至比我自己……"
    xw "然然，在此之前，我见过无数次清晨和日落，无数次。"
    xw "我已经数不清自己经历过多少次了，我把自己的噩梦重复一遍又一遍，一遍又一遍…… "
    xw "我还记得……我还记得第一次，最开始的那一次。我看到你们一个接着一个离我而去，我看着你在高高的楼顶堕入火中，我看到世界毁灭……"
    xw "我不希望这一切就这么结束，不行！我已经失去那么多了，为什么还要我继续失去！"
    xw "然然，不要这么做，好吗。"
    xw "不要牺牲自己，我做不到……我做不到，然然。"

    "向晚的话语逐渐被哭泣声所淹没，她感到自己的精神正在分崩离析。过量的悲伤和痛苦如海潮般一遍又一遍的拍击她的心房，向晚觉得自己快要窒息了。"
    "她紧紧地抓着嘉然的衣服，生怕下一秒嘉然就要消失一样，而嘉然紧紧地抱住了她。"

    #CG ——插入CG（向晚在哭泣，嘉然的表情平和而安详，两人紧紧抱在一起）——
    show jr laugh at r2 with dissolve
    show xw cry at l2 with dissolve
    #立绘 嘉然（CG）：
    jr "晚晚，我相信你，甚于相信我自己。"
    jr "我相信你，我愿意把一切都交付于你。"
    jr "由你去做决定，由你来完成这一切。"
    jr "晚晚，从小，教堂里的修女和神父就教育我，要成为一个善良的人。"
    jr "我的使命，就是为所有人带去幸福与快乐，去实现他们的愿望，去达成他们的要求，去满足他们渴望得到幸福的想法。"
    jr "而如今，完成这一目标的时刻到了。只有以我的死，才能带来他们的幸福与快乐。"
    jr "只有这样，我的使命才能被履行。"
    show xw angery at l2 with dissolve
    #立绘 向晚（CG）：
    xw "不……不可以……不可以，我不同意……"
    xw "不行，嘉然，不可以……一定还有……更好的方法"
    xw "一定可以两全其美……一定可以！我不相信这就是唯一的……唯一答案……"

    #立绘 嘉然（CG）：
    jr "对啊，晚晚，一定有更好的方法。"
    jr "看着我，晚晚，看着我的眼睛。"

    #CG ——CG淡出——
    "嘉然捧起向晚的脸颊，她那本应是湛蓝色的眸子此刻镀上了一层淡淡的金光，仿佛有神明寄宿于其中一样。"
    "向晚在凝视那双眼眸的时候，逐渐冷静了下来。"
    "嘉然轻声呢喃着，就好像在说世界上最大的秘密一样。"

    #【嘉然的立绘亮】
    show jr laugh with dissolve
    #立绘 嘉然（微笑）：
    jr "下一次……晚晚……在你的下一次……"
    jr "你会找到更好的方法，拯救我们每一个人……"
    jr "而为了达到下一次，晚晚，你必须这么做。"
    jr "晚晚……对于我而言，这是最后一次，所以，杀了我吧。"
    jr "晚晚，给我自由吧。"
    jr "我相信的，如果是你……一定可以的。"
    jr "我相信你。"

    #【嘉然的立绘暗】
    "嘉然再一次，紧紧的抱住了向晚。"
    "沉默持续了许久，终于，是向晚主动松开了自己的双手。"
    "她从木盒中取出了帷幕之末，似乎是听从了她的召唤，闪光的纹路与紫色的光晕再一次浮现在了漆黑的刀刃之上。"
    "嘉然轻轻地握住了向晚握刀的手。"

    #【嘉然的立绘亮】
    show jr laugh with dissolve
    #立绘 嘉然（微笑）：
    jr "不要怕，晚晚。"
    jr "我们先出去，这里不会有点冷吗？"
    jr "先把这个给我吧。晚晚，你已经做得很棒了。"
    jr "我们还有时间，不用操之过急。"
    #【嘉然的立绘暗】
    jump .part8

label .part8:
    #场景 黄昏 教堂内 抒情的音乐
    play music "audio/bgm/钢琴1 缓和.mp3" fadeout 1.0 fadein 3.0
    scene  教堂内 with fade
    "嘉然的话语让向晚的手稍稍松动了一些，嘉然随之再一起取走了帷幕之末。"
    "再一次入手时，帷幕之末没有发出金光。嘉然可以感受到向晚的温度的残留。"
    "那温度即便是单纯地感受，也能品味出一丝难以掩饰的焦虑与紧张。"
    "嘉然笑了笑，随后牵着向晚的手走出了密室。是的，黄昏还没有结束，最美好的时光还在。"
    "不该浪费这样的短暂瞬间，无论是谁。"

    #【嘉然的立绘亮】
    show jr normal with dissolve
    #立绘 嘉然（日常）：
    jr "晚晚，你看到这些房间了吗？"
    jr "以前，这里住的是比我大好多的哥哥姐姐，他们和我不一样，是被家里人送到这里的。"
    jr "虽然他们年纪比较大，但是修女总是让他们以我为榜样。大概是因为哥哥姐姐们的玩心都很重。"
    jr "他们虽然有些经文背不下来，但在找乐子这方面可以说是颇有心得。"
    jr "所以每次我都会帮他们背经文，而他们则带我去教堂内外那些不为人知的角落玩。"
    jr "这个教堂虽然不小，但是住得久了也会有些无聊。"
    jr "幸好有他们……不过他们在这里呆了几年后就离开了，从那之后，我再也没见过这些门被再一次打开。"

    #旁白：
    "嘉然带着向晚来到她的873号房间所在的走廊上。如她所言，除了873号房以外，所有的房门都紧锁。"
    "就好像时间都被这无情的门锁拒之门外，房间里的一切因为观测者的缺少，而永远地沉睡在福尔马林里。"
    "向晚有点好奇，里面会是什么样子呢？那些少年少女们离开此地的一瞬间，是否被完美无缺地保存着？"
    "他们是否也是在这样一个金光闪耀的黄昏离开？"
    "向晚看着嘉然的身影，张了张嘴，却什么都没有说出来。"
    "阳光像鱼一样懒散地游动在这沉静的空间内，它们从那么远的地方来，能带来什么？又将什么带走？"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr lost at r2
    show xw sad at l2 with dissolve
    #立绘 嘉然（落寞）：
    jr "一个人和修女还有神父住在这么大的教堂里，难免会有点寂寞。"
    jr "不过好在每周末都会有人来做礼拜。诶呀，说到这个，晚晚，你以前去过教堂做礼拜吗？"
    show xw sad at l2 with dissolve
    #立绘 向晚（日常）：
    xw "没有。"
    xw "我对这些……不是很了解。"
    xw "不过然然，你说吧，我很喜欢听你说这些。"
    xw "这些过去的事情。"
    xw "你多说点……没事的。"
    show jr smile at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "嗯！那我们去教堂大厅吧，那里有十字架和特别漂亮的彩色玻璃！"
    #声音 【有序的脚步声】
    play sound "/audio/se/脚步声walking_on_floor1.mp3"
    jump .part9

label .part9:

    #场景 黄昏 彩色玻璃与十字架的大特写
    #bgm 神圣而温柔的音乐
    play music "audio/bgm/b站女声圣咏.mp3" fadeout 1.0 fadein 3.0

    scene 教堂特写 with fade
    #【嘉然的立绘亮】
    show jr normal at r2
    show xw calm at l2
    with dissolve
    #立绘 嘉然（日常）：
    jr "晚晚，我刚刚去找猫咪的时候，虽然没有找到猫咪，却想起了教堂的名字。"
    jr "你看到那个十字架了吗，那上面悬挂的是一位圣女，她的名字叫司琪礼仆，也就是这个教堂所供奉的对象。"
    jr "我还小的时候，老修女每天晚上要给我讲的故事就包括司琪礼仆的传说。"
    jr "据说，她是一位特别善良而且特别喜欢艺术的圣女，平生最喜欢的事情就是修复那些破损的艺术品。无论是画，还是雕像，亦或者是瓷器，她都十分拿手。"
    jr "后来，她收集了好多好多被人们遗弃的，被当做垃圾看待的艺术品。"
    jr "那些破损不堪的瓷器和画作都已经面目全非，但是圣女却毫不介意。她利用自己的神力，将这些画作和瓷器变为崭新的大理石，垒砌出了一个小小的教堂。"
    jr "那个小小的教堂据说就是这座教堂的雏形。据说最初的小教堂只有一个房间那么大，如今却已经变成了这幅壮观的模样。"
    jr "修女说，圣女她爱着众生，一如她爱着描绘着众生身影的艺术品。在她眼中，每一件艺术品都代表一个鲜活的生命。"

    "悬挂在教堂尽头的十字架闪烁着黄铜色的光芒。在那上面，名为司琪礼仆的圣女正悲悯地垂下自己的头颅，双手合十。"
    "她身着着柔顺的长袍，尽管曾经的颜色已经褪去大半，依旧可以看出浅浅的紫色痕迹来。"
    "她的脚下，被细碎的花朵团团拥簇着。那些花朵如同一颗颗圆球形的硬糖，带着几乎是透明的质地。"
    "夕阳从彩色玻璃中一泄如注，那些染上颜色的暮色如光环一样朦朦胧胧地笼罩在十字架的边上。"
    "彩色玻璃上的连环画在时间与岁月的解构中，已经化作单纯的图案与颜色。"
    "向晚看着这些，难以自制地想象起嘉然在五年前，乃至于十年前，站在现在的位置，越过身着黑衣的人们的肩膀，看向神父在台上讲经的景象。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr normal at r2 with dissolve
    show xw normal at l2 with dissolve
    #立绘 向晚（日常）：
    xw "然然，礼拜的时候，你会去旁观吗？"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "会的哦，每一次礼拜我都准时参加了。"
    jr "作为教堂的成员，我其实不应该坐地太近，应该把更好的位置让给那些虔诚的信徒。"
    jr "不过我小时候有点矮，所以修女就破例让我坐在第一排。"
    jr "后来我长大了，坐的地方也越来越靠后。到最后，干脆只能和哥哥姐姐们一起坐在最后一排了。"
    jr "你看，就是这一排。"


    "说着，嘉然指了指离她们最近，却也离十字架最远的那一排座位。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show xw normal at l2 with dissolve
    show jr normal at r2 with dissolve
    #立绘 嘉然（日常）：
    jr "老修女在我十二岁之前，都不曾和我们坐在一起。那时候她还能站着撑过整个礼拜。"
    jr "后来她身体越来越差，只能和我们坐在一起听礼拜了。"
    jr "她最喜欢的是我，每次也都坐在我身边，还会偷偷地为我解释礼拜中提到的词句有何种含义。"
    #立绘 向晚（微笑）：
    xw "老修女一定是一个很好的人。"
    xw "可惜她不在这里，不然我真的很想见见她。"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "嗯，她的确是个很好的人……她教会了我很多很多东西。她让我成为了现在的自己。"
    jr "晚晚，其实老修女已经走了。按神父的话说，她已经被召回主的国度。"
    jr "我并不悲伤，因为神父告诉我，老修女离开的时候是面带微笑的。"
    jr "她满足于自己的一生，也许她也满足于我。"
    jr "晚晚，跟我来。"


    "嘉然领着向晚，从大厅的正门离开了教堂。"

    #声音【有序的脚步声】
    play sound "/audio/se/脚步声walking_on_floor1.mp3"
    jump .part10

label .part10:
    #场景 黄昏 教堂外
    #bgm 抒情的音乐
    play music "audio/bgm/抒情双吉他.mp3" fadeout 1.0 fadein 3.0

    scene 黄昏教堂外 with fade
    "向晚没有想到嘉然会带着自己离开这里，一时间没有反应过来。"
    "习惯了教堂里多少有些昏暗的照明后，室外的光芒令向晚有些不适应。"
    "天色依旧，一如她进入教堂时的模样。那闪光的圆球悬挂在天边，似乎已经下落了不少距离，可光芒却没减弱分毫。"
    "嘉然如灵敏的鹿，带着向晚绕过教堂边上繁复的装饰，来到了教堂的背后。"
    "在那被遮蔽的角落里，沐浴在教堂那连轮廓都沐浴着某种神性的阴影中，一座小小的墓园正安静地横卧着。"

    jump .part11

label .part11:

    #场景 黄昏 墓园
    #bgm 抒情的音乐
    #play music "audio/bgm/伤感双吉他.mp3" fadeout 1.0 fadein 3.0

    scene 墓园 with fade
    "样式统一的墓碑整齐有序地排列着，仿佛一个模子刻出来的小鼓包紧挨着那些墓碑。"
    "墓园里开满了白色或是蓝色的花，细碎的花瓣被风吹落了满地，如一层朴素的地毯。"
    "长满了青苔的石质围栏把墓园小心地保护了起来，嘉然和向晚从那留出的入口走了进来，最终停在了最中间的一块墓碑处。"
    "这块墓碑被安置在墓园的最中央，但上面却没有刻字。本应是一个小鼓包的地方，只有一个空荡荡的洞口。"
    "向晚知道，那是为某一个人的棺材所准备的空坟。这座令人深感不安的坟墓，正等待着它的房客。"
    "而在空坟的地下，开满了白蓝相间的野花。就好像死亡也变得甜蜜，金色的光芒里，所有的生命笑着走向终点。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr normal at r2
    show xw calm at l2
    with dissolve
    #立绘 向晚（日常）：
    xw "然然，这块墓碑……"

    #立绘 嘉然（日常）：
    jr "是老修女准备的。"
    jr "但不是为她自己。"
    jr "老修女走的时候，她不在枝江市，在另一个城市，一个很远很远的地方。"

    "嘉然蹲下身，轻轻地抚摸那冷而硬的墓碑。"
    "灰白色的质地散发着一股了无生气的气息，但嘉然只是单纯的触碰，就让那墓碑刹那间不再那么的冰冷了。"
    "向晚看着嘉然，一股难以言喻的情绪涌上心头。"

    #【嘉然的立绘亮】
    show jr laugh with dissolve
    #立绘 嘉然（微笑）：
    jr "晚晚，其实对我来说，和A-SOUL的大家在一起的日子更有趣一些。"
    jr "在教堂生活的十几年的的确确塑造了我的品性，这段过去的时光对我弥足珍贵。"
    jr "但如果我不能背负着这段时光留下的痕迹，去和你们一起生活，一起努力，一起舞蹈，那这一切都将毫无意义。"
    jr "晚晚，你还记得我们玩的那些游戏吗？那些反复练习的舞蹈动作，还有那些每天都会唱的歌？"
    jr "咱们第一次吃鸡的那次，如果不是你用烟雾弹拖延了时间，在第一次缩圈的时候我们就团灭了……哈哈，不过之所以差点团灭，也是因为把手榴弹当成烟雾弹了。"
    show jr smile with dissolve
    jr "还有那次团舞，是Quiet吧，直播的时候你不小心被我绊倒，然后乃琳被你绊倒，贝拉被乃琳绊倒，最后珈乐看着我们笑的时候自己把自己给绊倒了。"
    jr "幸好那一天是愚人节，不然就变成直播事故了。"
    show jr laugh with dissolve
    jr "这些细碎的回忆，和在教堂的过去一样珍贵。它们是我最珍惜最珍惜的宝物。"
    jr "而它们也不仅仅属于我。晚晚，这座教堂之所以能出现，得益于数千名信徒的善款和支持，没有他们的付出，这座教堂不会有如今这般壮丽。"
    jr "这座教堂不仅仅属于我，也属于所有来此参拜的信徒。对于我们所有人来说，这座教堂有着同样的价值。"
    jr "我们的过去，晚晚，我们有着那么美妙的过去。当我想起我们一起直播的时刻，我会不自觉地微笑的。和你，和乃琳，和贝拉，和珈乐。"
    jr "可它们也同样不仅仅属于我们，它们也属于所有一个魂，所有喜欢我们的人。"

    jr "老修女因为她虔诚的信仰，而得到了主的昭示。"
    jr "她已做好了她的准备，而我会做出我的选择——不因为任何人的干涉或是教育，不因为神父的礼拜，不因为老修女的说教。"

    show xw unconcerned frown with dissolve
    jr "晚晚，我因我个人之意志，希望你在仪式上杀了我。"
    show jr angery with dissolve
    jr "阿草的背叛已经显而易见，他恶毒地操纵了喜爱我们的一个魂们。他略施手段，就将真挚的热诚转变为盲目的愤怒。"
    jr"我的力量严格来说，和我自己并没有关系。"
    jr"这种力量的恐怖之处在于能够以近乎不存在的成本调动他人的情绪。"
    jr"我让他们疯狂，他们就会疯狂，我让他们快乐，他们就会快乐。"
    jr"而阿草只希望让一切毁灭，而我不能让他做到。"
    show jr sad with dissolve
    jr"我没能保护好嘉心糖和一个魂，我没能以最完美的姿态达成我的使命。"

    jr"但至少，我可以以另一种方式补完它。迷途未远，还有弥补的空间。"
    show jr laugh with dissolve
    jr"这宝贵的余地，就来自于这柄名为帷幕之末的匕首。只有握着它的人能伤害到我——不仅仅是物理层面的“触碰”和“攻击”，而是精神层面的“攻击意愿”"
    jr"一个赤手空拳，亦或者拿着其他武器的人在面对我的时候，是无法攻击我，乃至于伤害到我的。"
    jr"阿草的仪式之关键，在于对于情绪的调动。"
    jr"按照仪式，我需要站在舞台上起舞，而那个时候，人们的情绪就会得到最强烈的煽动与释放。"
    jr "就好像钓鱼要等到鱼上钩再拉杆一样，如果想要将这些情绪净化，使其不能为阿草所用，就必须在仪式进行的过程中将我的舞蹈强行中断。"
    jr "那些情绪就会变成无头苍蝇，离开了宿主的负面情绪同时也失去了自己的目标。"
    jr "我不知道会发生什么，但我能确定阿草没有将这些情绪重新收集起来的能力。"
    jr "这些情绪将会以尘埃的模样逸散在天地之间。"
    jr "这是最理想的结果，晚晚。我们必须等到那些情绪钻出它们的巢穴时行动。"
    jr "那会是一支你比我还要熟悉的舞蹈，你知道哪个动作代表着高潮的到来。"
    show jr smile with dissolve
    jr "今晚，你能否送我去野花丛中入眠？"

    "嘉然拿出了名为帷幕之末的黑色匕首，将刀柄伸向了向晚。"
    "夕阳的最后一缕风缓缓吹过，吹起向晚那紫色的头发。"
    "她看不清嘉然的脸，一如她不知道自己的脸是何种模样。"
    "她要抱着怎样的心情，带着何种的神态，接过这一冠冕堂皇的凶器？"
    "向晚的大脑坠入了难以言喻的混沌深海，而就在同时，一股凶狠的力量撕开了迷茫的思绪。"
    "在一瞬间，向晚惊讶于自己的愤怒与恶意竟如此之汹涌。"

    #【嘉然的立绘亮】
    #【向晚的立绘亮】
    show jr laugh at r2 with dissolve
    show xw sad at l2 with dissolve
    #立绘 向晚（落寞，低垂双眼，嘴角向下弯）：
    xw "然然，如果我拒绝了你，你会生我气吗。"
    xw "在你说了这么多，向我分享了这么多属于你的过去之后。"
    xw "在你对我动之以情晓之以理之后。"
    xw "如果我依然拒绝，如果我依然说……不，我无法做到。"
    xw "你会生我气吗？"
    show jr laugh at r2 with dissolve
    #立绘 嘉然（微笑）：
    jr "我不会，晚晚。我理解你。"
    jr "这不是一个简单的选择，换作任何人都会犹豫，即便是司琪礼仆圣女也难以在一刹那给出自己的选择。"

    #立绘 向晚（落寞，低垂双眼，嘴角向下弯）：
    xw "那如果我干脆利落地答应，你会生我气吗。"
    xw "你会觉得，我是一个无情而残忍的人吗？"

    #立绘 嘉然（微笑）：
    jr "同样不会，晚晚。你答应了我的请求，我荒诞而近乎于冒犯性的请求。"
    jr "我会铭记你为我做的一切，晚晚。"
    show xw cry at l2 with dissolve
    #立绘 向晚（落寞，低垂双眼，嘴角向下弯）：
    xw "如果我欺骗了你，然然，你会——"

    #立绘 嘉然（微笑）：

    jr "晚晚。"
    jr "无论结果如何，我都不会怪任何人。"
    jr "我接受每一种结局，每一种你选择的结局。"
    show xw sad at l2 with dissolve
    "向晚和嘉然久久地凝视着彼此的双眼。"
    "两个人之间的空隙从未如此之遥远过，向晚的心剧烈地搏动着，愧疚感和罪恶感在心底滋生。"
    "嘉然那不带有任何杂质的信任，令她的灵魂感到深刻的刺痛。"
    "当向晚伸出手，接过帷幕之末的时候，她感到自己仿佛被万箭穿心。"

    #【嘉然的立绘亮】
    show jr laugh with dissolve
    #立绘 嘉然（微笑）：
    jr "谢谢你，晚晚。"
    jr "我们走吧，趁着还有光。"
    jr "太阳快落山了。"

    #转场 黑屏
    scene black with Fade(1,0.5,1)
    #7.5章结束
    stop music fadeout 2.0
    #【嘉然的立绘暗】
    scene black with Dissolve(3)

    jump .cp7_g_end

label .cp7_g_end:

    jump cp8_g_start
define flash_in = Fade(1.0, 0, 0, color="#fff")
define flash_out = Fade(0.5, 0.0,1.0, color="#fff")
init python:
    declare('密室内', 'bg/7 5 密室內.png', 'p')
    declare('教堂外', 'bg/7 5 教堂外.png', 'p')
    declare('墓园', 'bg/75 墓园.png', 'p')
    declare('嘉然房间', 'bg/75 嘉然房间内.png', 'p')
    declare('教堂特写', 'bg/75 教堂特写.png', 'p')
    declare('教堂内', 'bg/75 教堂内.png', 'p')
    declare('黄昏病房', 'bg/0 3 病房(黄昏).png', 'p')
    declare('黄昏教堂外', 'bg/75 教堂(黄昏).png', 'p')
    declare('黄昏街头', 'bg/1 2枝江街头(黄昏).png', 'p')
    declare('黄昏医院', 'bg/1 医院走廊1(黄昏).png', 'p')
    declare('黄昏林荫小道', 'bg/75 林荫小径.png', 'p')
    declare('贫民窟', 'bg/贫民窟.png', 'p')
    declare('教堂走廊', 'bg/82 教堂走廊.png', 'p')
image xw uncertain:
    "tatie/向晚/疑惑.png"
    zoom 0.285
    yoffset 55
image xw mow:
    "tatie/向晚/皱眉.png"
    zoom 0.285
    yoffset 55
image jtw_1:
    "bg/75 教堂(黄昏).png"
    align (0,0) xoffset -2080 yoffset -1460
    1.5
    easeout 1.5 xoffset -1280 yoffset -1460
    easein_cubic 3.0 xoffset -1280 yoffset -200
    easein 1.5 xoffset -1280 yoffset -1300
    #easein 1.0 xoffset -980 yoffset -960
image msn_1:
    "bg/7 5 密室內.png"
    align (0,0)
    #xoffset -2080 yoffset -1460
    easeout 3.0 xoffset -1280 yoffset -1460
    easein_cubic 3.0 xoffset -1280 yoffset -200
    easein 2.0 xoffset -1280 yoffset -1000
image jrfj_1:
    "bg/75 嘉然房间内.png"
    align (0,0)
    #xoffset -2080 yoffset -1460
    0.7
    easeout 2.5 xoffset -280 yoffset -760
    easein 3.0 xoffset -2280 yoffset -1060
    easein 2.0 xoffset -2080 yoffset -800
